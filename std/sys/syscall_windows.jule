// Copyright 2022 The Jule Authors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

#pass "-lshell32" // Link shell32.lib

use "std/integ"
use "std/integ/c"
use "std/mem"

extern use "<fcntl.h>"
extern use "<io.h>"
extern use "<windows.h>"

extern type DWORD: u32
extern type HANDLE: *unsafe

#typedef
extern struct LARGE_INTEGER{}

#typedef
extern struct WIN32_FIND_DATAW{}

#typedef
extern struct SECURITY_ATTRIBUTES{}

#typedef
extern struct BY_HANDLE_FILE_INFORMATION{}

#typedef
extern struct timeval{}

#typedef
extern struct FILETIME{}

// It is a C enum in the Windows API.
extern type GET_FILEEX_INFO_LEVELS: int

extern let LoadLibraryW: *unsafe
extern let LoadLibraryExW: *unsafe
extern let GetProcAddress: *unsafe
extern let FreeLibrary: *unsafe

extern fn ExitProcess(exitcode: u32)
extern fn GetStdHandle(stdh: uintptr): *unsafe
extern unsafe fn CloseHandle(stdh: *unsafe): bool
extern unsafe fn GetEnvironmentVariableW(key: *c::Wchar, mut out: *c::Wchar, nSize: extern.DWORD): extern.DWORD
extern unsafe fn SetEnvironmentVariableW(key: *c::Wchar, val: *c::Wchar): bool
extern fn GetLastError(): u32
extern unsafe fn GetCurrentDirectoryW(bufflen: u32, buff: *c::Wchar): u32
extern unsafe fn SetCurrentDirectoryW(path: *c::Wchar): bool
extern unsafe fn GetFullPathNameW(path: *c::Wchar, bufflen: u32, buff: *c::Wchar, fname: **c::Wchar): u32
extern unsafe fn DeleteFileW(path: *c::Wchar): bool
extern unsafe fn CreateDirectoryW(path: *c::Wchar, passNullHere: *bool): bool
extern unsafe fn RemoveDirectoryW(path: *c::Wchar): bool
extern unsafe fn GetConsoleMode(handle: extern.HANDLE, mut mode: *extern.DWORD): bool
extern unsafe fn SetConsoleMode(handle: extern.HANDLE, mode: extern.DWORD): bool
extern unsafe fn WriteConsoleW(handle: extern.HANDLE, buffer: *unsafe,
	toWrite: extern.DWORD, written: *extern.DWORD, reserved: *unsafe): bool
extern unsafe fn ReadConsoleW(handle: extern.HANDLE, mut buffer: *unsafe,
	toRead: extern.DWORD, read: *extern.DWORD, inputControl: *unsafe): bool
extern fn WSAGetLastError(): int
extern unsafe fn FindFirstFileW(*c::Wchar, *extern.WIN32_FIND_DATAW): extern.HANDLE
extern unsafe fn FindNextFileW(extern.HANDLE, *extern.WIN32_FIND_DATAW): int
extern fn FindClose(extern.HANDLE): int
extern unsafe fn GetFileAttributesExW(name: *c::Wchar, infoLevel: extern.GET_FILEEX_INFO_LEVELS, info: *unsafe): bool
extern unsafe fn CreateFileW(path: *c::Wchar, desiredAccess: extern.DWORD, shareMode: extern.DWORD, securityAttrs: *extern.SECURITY_ATTRIBUTES, creationDisposition: extern.DWORD, flagsAndAttrs: extern.DWORD, templateFile: extern.HANDLE): extern.HANDLE
extern unsafe fn GetFileType(h: extern.HANDLE): extern.DWORD
extern unsafe fn GetFileInformationByHandle(h: extern.HANDLE, *extern.BY_HANDLE_FILE_INFORMATION): bool
extern unsafe fn ReadFile(h: extern.HANDLE, buf: *unsafe, bytesToRead: extern.DWORD, mut bytesRead: *extern.DWORD, overlap: *extern.OVERLAPPED): bool
extern unsafe fn WriteFile(h: extern.HANDLE, buf: *unsafe, bytesToWrite: extern.DWORD, mut bytesWritten: *extern.DWORD, overlap: *extern.OVERLAPPED): bool
extern unsafe fn SetEndOfFile(h: extern.HANDLE): bool
extern unsafe fn FlushFileBuffers(h: extern.HANDLE): bool
extern unsafe fn CreatePipe(r: *extern.HANDLE, w: *extern.HANDLE, attrs: *extern.SECURITY_ATTRIBUTES, size: extern.DWORD): bool
extern unsafe fn SetFilePointerEx(h: extern.HANDLE, toMove: extern.LARGE_INTEGER, mut newPointer: *extern.LARGE_INTEGER, moveMethod: extern.DWORD): bool
extern unsafe fn GetFileAttributesW(name: *c::Wchar): extern.DWORD
extern unsafe fn SetHandleInformation(hObject: extern.HANDLE, dwMask: extern.DWORD, dwFlags: extern.DWORD): bool
extern fn GetCurrentProcess(): extern.HANDLE
extern unsafe fn DuplicateHandle(hSourceProcessHandle: extern.HANDLE, hSourceHandle: extern.HANDLE, hTargetProcessHandle: extern.HANDLE, lpTargetHandle: *extern.HANDLE, dwDesiredAccess: extern.DWORD, bInheritHandle: bool, dwOptions: extern.DWORD): bool
extern unsafe fn QueryPerformanceCounter(mut counter: *extern.LARGE_INTEGER): bool
extern unsafe fn GetSystemTimeAsFileTime(mut ft: *extern.FILETIME)

#typedef
extern struct OVERLAPPED{}

type Handle: uintptr

const InvalidHandle = ^Handle(0)

const (
	STD_INPUT_HANDLE  = -10
	STD_OUTPUT_HANDLE = -11
	STD_ERROR_HANDLE  = -12
)

const (
	MAX_PATH      = 260
	MAX_LONG_PATH = 32768
)

// Invented values to support what package os expects.
const (
	O_RDONLY   = 0x00000
	O_WRONLY   = 0x00001
	O_RDWR     = 0x00002
	O_CREAT    = 0x00040
	O_EXCL     = 0x00080
	O_NOCTTY   = 0x00100
	O_TRUNC    = 0x00200
	O_NONBLOCK = 0x00800
	O_APPEND   = 0x00400
	O_SYNC     = 0x01000
	O_ASYNC    = 0x02000
	O_CLOEXEC  = 0x80000
)

// Invented values to support what package os expects.
const (
	S_IFMT   = 0x1f000
	S_IFIFO  = 0x1000
	S_IFCHR  = 0x2000
	S_IFDIR  = 0x4000
	S_IFBLK  = 0x6000
	S_IFREG  = 0x8000
	S_IFLNK  = 0xa000
	S_IFSOCK = 0xc000
	S_ISUID  = 0x800
	S_ISGID  = 0x400
	S_ISVTX  = 0x200
	S_IRUSR  = 0x100
	S_IWRITE = 0x80
	S_IWUSR  = 0x80
	S_IXUSR  = 0x40
)

let STDIN = GetStdHandle(uintptr(u32(int(STD_INPUT_HANDLE))))
let STDOUT = GetStdHandle(uintptr(u32(int(STD_OUTPUT_HANDLE))))
let STDERR = GetStdHandle(uintptr(u32(int(STD_ERROR_HANDLE))))

fn GetStdHandle(stdh: uintptr): Handle { ret Handle(extern.GetStdHandle(stdh)) }

fn CloseHandle(stdh: Handle)! {
	r := unsafe { extern.CloseHandle(unsafe { (*unsafe)(stdh) }) }
	if !r {
		error(GetLastErrno())
	}
}

// Wrapper for C's _wopen function.
fn SetEndOfFile(h: Handle)! {
	r := unsafe { extern.SetEndOfFile(extern.HANDLE(h)) }
	if !r {
		error(GetLastErrno())
	}
}

// Creates an anonymous pipe, and returns handles to the read and write ends of the pipe.
unsafe fn CreatePipe(mut r: *Handle, mut w: *Handle, attrs: *SecurityAttributes, size: u32): bool {
	ret extern.CreatePipe((*extern.HANDLE)(r), (*extern.HANDLE)(w), (*extern.SECURITY_ATTRIBUTES)(attrs), extern.DWORD(size))
}

// Flushes the buffers of a specified file and causes all buffered data to be written to a file.
fn FlushFileBuffers(h: Handle)! {
	r := unsafe { extern.FlushFileBuffers(extern.HANDLE(h)) }
	if !r {
		error(GetLastErrno())
	}
}

// Returns last Windows error.
fn GetLastError(): u32 { ret extern.GetLastError() }

// Reads working directory into buff and returns read
// rune count of current process. Returns 0 if fail.
unsafe fn GetCurrentDirectory(bufflen: u32, mut buff: *u16): u32 {
	ret extern.GetCurrentDirectoryW(bufflen, (*c::Wchar)(buff))
}

// Sets working directory to path.
// Reports operation is success.
unsafe fn SetCurrentDirectory(path: *u16): bool {
	ret extern.SetCurrentDirectoryW((*c::Wchar)(path))
}

// Create file.
unsafe fn CreateFile(path: *u16, desiredAccess: u32, shareMode: u32, securityAttrs: *SecurityAttributes, creationDisposition: u32, flagsAndAttrs: u32, templateFile: Handle): Handle {
	ret Handle(extern.CreateFileW(
		(*c::Wchar)(path),
		extern.DWORD(desiredAccess),
		extern.DWORD(shareMode),
		(*extern.SECURITY_ATTRIBUTES)(securityAttrs),
		extern.DWORD(creationDisposition),
		extern.DWORD(flagsAndAttrs),
		extern.HANDLE(templateFile)))
}

// Read file.
// Passes nil for overlap.
fn ReadFile(h: Handle, buf: []byte)!: int {
	mut read := u32(0)
	ok := unsafe { extern.ReadFile(extern.HANDLE(h), &buf[0], extern.DWORD(len(buf)), (*extern.DWORD)(&read), nil) }
	if !ok {
		error(GetLastErrno())
	}
	ret int(read)
}

// Write file.
// Passes nil for overlap.
fn WriteFile(h: Handle, buf: []byte)!: int {
	mut written := u32(0)
	ok := unsafe { extern.WriteFile(extern.HANDLE(h), &buf[0], extern.DWORD(len(buf)), (*extern.DWORD)(&written), nil) }
	if !ok {
		error(GetLastErrno())
	}
	ret int(written)
}

// Calls Windows's GetFileType function.
unsafe fn GetFileType(h: Handle): (t: u32, err: bool) {
	t = u32(extern.GetFileType(extern.HANDLE(h)))
	err = t == FILE_TYPE_UNKNOWN && GetLastError() != 0
	ret
}

// Calls Windows's GetFileInformationByHandle function.
unsafe fn GetFileInformationByHandle(h: Handle, info: *ByHandleFileInformation): bool {
	ret extern.GetFileInformationByHandle(extern.HANDLE(h), (*extern.BY_HANDLE_FILE_INFORMATION)(info))
}

// Delete file.
unsafe fn DeleteFile(path: *u16): bool {
	ret extern.DeleteFileW((*c::Wchar)(path))
}

// Creates directory.
unsafe fn CreateDirectory(path: *u16): bool {
	ret extern.CreateDirectoryW((*c::Wchar)(path), nil)
}

// Removes empty directory.
unsafe fn RemoveDirectory(path: *u16): bool {
	ret extern.RemoveDirectoryW((*c::Wchar)(path))
}

// Calls Windows's GetFullPathNameW function.
unsafe fn GetFullPathName(path: *u16, bufflen: u32,
	buff: *u16, fname: **u16): u32 {
	ret extern.GetFullPathNameW((*c::Wchar)(path), bufflen, (*c::Wchar)(buff), (**c::Wchar)(fname))
}

// Calls Windows's GetConsoleMode function.
unsafe fn GetConsoleMode(handle: Handle, mut mode: *u32): bool {
	ret unsafe { extern.GetConsoleMode(extern.HANDLE(handle), (*extern.DWORD)(mode)) }
}

// Calls Windows's SetConsoleMode function.
unsafe fn SetConsoleMode(handle: Handle, mode: u32): bool {
	ret unsafe { extern.SetConsoleMode(extern.HANDLE(handle), extern.DWORD(mode)) }
}

// Calls Windows's WriteConsoleW function.
unsafe fn WriteConsole(handle: Handle, buffer: *u16, toWrite: u32,
	mut &written: *u32, reserved: *unsafe)! {
	ok := extern.WriteConsoleW(extern.HANDLE(handle), (*unsafe)(buffer),
		extern.DWORD(toWrite), (*extern.DWORD)(written), reserved)
	if !ok {
		error(GetLastErrno())
	}
}

// Calls Windows's ReadConsoleW function.
// Passes nil pointer for input control.
unsafe fn ReadConsole(handle: Handle, mut buffer: *u16, toRead: u32, mut &read: *u32)! {
	r := extern.ReadConsoleW(extern.HANDLE(handle), (*unsafe)(buffer),
		extern.DWORD(toRead), (*extern.DWORD)(read), nil)
	if !r {
		error(GetLastErrno())
	}
}

// Calls Windows's WSAGetLastError function.
fn WSAGetLastError(): int {
	ret extern.WSAGetLastError()
}

// Call's Windows FindFirstFileW function.
unsafe fn FindFirstFile(path: *u16, mut data: *Win32FindData): Handle {
	ret Handle(extern.FindFirstFileW((*c::Wchar)(path), (*extern.WIN32_FIND_DATAW)(data)))
}

// Call's Windows FindNextFileW function.
unsafe fn FindNextFile(h: Handle, mut data: *Win32FindData): int {
	ret extern.FindNextFileW(extern.HANDLE(h), (*extern.WIN32_FIND_DATAW)(data))
}

// Call's Windows FindClose function.
fn FindClose(h: Handle): int {
	ret unsafe { extern.FindClose(extern.HANDLE(h)) }
}

// Windows's GetEnvironmentVariableW function.
unsafe fn GetEnvironmentVariable(key: *u16, mut out: *u16, outLen: u32): u32 {
	ret u32(extern.GetEnvironmentVariableW((*c::Wchar)(key), (*c::Wchar)(out), extern.DWORD(outLen)))
}

// Windows's SetEnvironmentVariableW function.
unsafe fn SetEnvironmentVariable(key: *u16, val: *u16): bool {
	ret extern.SetEnvironmentVariableW((*c::Wchar)(key), (*c::Wchar)(val))
}

// Windows's GetFileAttributesExW function.
unsafe fn GetFileAttributesEx(name: *u16, infoLevel: int, info: *unsafe): bool {
	ret extern.GetFileAttributesExW((*c::Wchar)(name), extern.GET_FILEEX_INFO_LEVELS(infoLevel), info)
}

// Windows's GetFileAttributes function.
unsafe fn GetFileAttributesW(name: *u16): u32 {
	ret u32(extern.GetFileAttributesW((*c::Wchar)(name)))
}

// Windows's SetHandleInformation function.
unsafe fn SetHandleInformation(h: Handle, dwMask: u32, dwFlags: u32): bool {
	ret extern.SetHandleInformation(extern.HANDLE(h), extern.DWORD(dwMask), extern.DWORD(dwFlags))
}

// Windows's GetCurrentProcess function.
fn GetCurrentProcess(): Handle {
	ret unsafe { Handle(extern.GetCurrentProcess()) }
}

// Windows's DuplicateHandle function.
unsafe fn DuplicateHandle(hSourceProcessHandle: Handle, hSourceHandle: Handle,
	hTargetProcessHandle: Handle, mut lpTargetHandle: *Handle, dwDesiredAccess: u32,
	bInheritHandle: bool, dwOptions: u32): bool {
	ret extern.DuplicateHandle(extern.HANDLE(hSourceProcessHandle), extern.HANDLE(hSourceHandle),
		extern.HANDLE(hTargetProcessHandle), (*extern.HANDLE)(lpTargetHandle), extern.DWORD(dwDesiredAccess),
		bInheritHandle, extern.DWORD(dwOptions))
}

// Calls SetFilePointerEx.
// See https://learn.microsoft.com/en-us/windows/win32/api/fileapi/nf-fileapi-setfilepointerex
fn setFilePointerEx(handle: Handle, distToMove: i64, mut &newFilePointer: *i64, whence: u32)! {
	ok := unsafe { extern.SetFilePointerEx(extern.HANDLE(handle), *(*extern.LARGE_INTEGER)(&distToMove), (*extern.LARGE_INTEGER)(newFilePointer), extern.DWORD(whence)) }
	if !ok {
		error(GetLastErrno())
	}
}

// Imitates the POSIX seek function.
fn Seek(h: Handle, offset: i64, whence: int)!: (newoffset: i64) {
	let mut w: u32
	match whence {
	| 0: // io::SeekStart
		w = FILE_BEGIN
	| 1: // io::SeekCurrent
		w = FILE_CURRENT
	| 2: // io::SeekEnd
		w = FILE_END
	}
	setFilePointerEx(h, offset, &newoffset, w)?
	ret
}

// Imitates the POSIX ftruncate function.
fn Ftruncate(h: Handle, size: i64)! {
	Seek(h, size, 0)?
	SetEndOfFile(h)?
}

fn makeInheritSa(mut &sa: *SecurityAttributes) {
	sa.Length = u32(mem::SizeOf(*sa))
	sa.InheritHandle = 1
}

fn Pipe()!: (r: Handle, w: Handle) {
	let p: [2]Handle
	let mut sa: SecurityAttributes
	makeInheritSa(&sa)
	ok := unsafe { CreatePipe(&p[0], &p[1], &sa, 0) }
	if !ok {
		error(GetLastErrno())
	}
	r = p[0]
	w = p[1]
	ret
}

fn Open(path: str, flag: int, perm: u32)!: Handle {
	if path == "" {
		error(Errno(ERROR_FILE_NOT_FOUND))
	}
	utf16Path := integ::UTF16FromStr(path)
	let mut access: u32
	match flag & (O_RDONLY | O_WRONLY | O_RDWR) {
	| O_RDONLY:
		access = GENERIC_READ
	| O_WRONLY:
		access = GENERIC_WRITE
	| O_RDWR:
		access = GENERIC_READ | GENERIC_WRITE
	}
	if flag&O_CREAT != 0 {
		access |= GENERIC_WRITE
	}
	if flag&O_APPEND != 0 {
		// Remove GENERIC_WRITE unless O_TRUNC is set, in which case we need it to truncate the file.
		// We can't just remove FILE_WRITE_DATA because GENERIC_WRITE without FILE_WRITE_DATA
		// starts appending at the beginning of the file rather than at the end.
		if flag&O_TRUNC == 0 {
			access &= ^GENERIC_WRITE
		}
		// Set all access rights granted by GENERIC_WRITE except for FILE_WRITE_DATA.
		access |= FILE_APPEND_DATA | FILE_WRITE_ATTRIBUTES | FILE_WRITE_EA | STANDARD_RIGHTS_WRITE | SYNCHRONIZE
	}
	sharemode := u32(FILE_SHARE_READ | FILE_SHARE_WRITE)
	let mut sa: SecurityAttributes
	if flag&O_CLOEXEC == 0 {
		makeInheritSa(&sa)
	}
	// We don't use CREATE_ALWAYS, because when opening a file with
	// FILE_ATTRIBUTE_READONLY these will replace an existing file
	// with a new, read-only one. See https://go.dev/issue/38225.
	//
	// Instead, we ftruncate the file after opening when O_TRUNC is set.
	let mut createmode: u32
	match {
	| flag&(O_CREAT|O_EXCL) == (O_CREAT|O_EXCL):
		createmode = CREATE_NEW
	| flag&O_CREAT == O_CREAT:
		createmode = OPEN_ALWAYS
	|:
		createmode = OPEN_EXISTING
	}
	let mut attrs: u32 = FILE_ATTRIBUTE_NORMAL
	if perm&S_IWRITE == 0 {
		attrs = FILE_ATTRIBUTE_READONLY
	}
	if flag&O_WRONLY == 0 && flag&O_RDWR == 0 {
		// We might be opening or creating a directory.
		// CreateFile requires FILE_FLAG_BACKUP_SEMANTICS
		// to work with directories.
		attrs |= FILE_FLAG_BACKUP_SEMANTICS
	}
	if flag&O_SYNC != 0 {
		const _FILE_FLAG_WRITE_THROUGH = 0x80000000
		attrs |= _FILE_FLAG_WRITE_THROUGH
	}
	h := unsafe { CreateFile(&utf16Path[0], access, sharemode, &sa, createmode, attrs, 0) }
	if h == InvalidHandle {
		mut e := GetLastErrno()
		if e == ERROR_ACCESS_DENIED && (flag&O_WRONLY != 0 || flag&O_RDWR != 0) {
			// We should return EISDIR when we are trying to open a directory with write access.
			fa := unsafe { GetFileAttributesW(&utf16Path[0]) }
			if fa != INVALID_FILE_ATTRIBUTES && fa&FILE_ATTRIBUTE_DIRECTORY != 0 {
				e = EISDIR
			}
		}
		error(GetLastErrno())
	}
	// Ignore O_TRUNC if the file has just been created.
	if flag&O_TRUNC == O_TRUNC &&
		(createmode == OPEN_EXISTING || (createmode == OPEN_ALWAYS && GetLastErrno() == ERROR_ALREADY_EXISTS)) {
		Ftruncate(h, 0) else {
			CloseHandle(h)?
			error(error)
		}
	}
	ret h
}

fn ExitProcess(exitcode: u32) {
	extern.ExitProcess(exitcode)
}

fn Exit(code: int) {
	ExitProcess(u32(code))
}

fn QueryPerformanceCounter()!: i64 {
	mut counter := i64(0)
	if unsafe { !extern.QueryPerformanceCounter((*extern.LARGE_INTEGER)(&counter)) } {
		panic("runtime: nanotime failed")
	}
	ret counter
}

fn GetSystemTimeAsFileTime(mut &ft: *Filetime) {
	unsafe { extern.GetSystemTimeAsFileTime((*extern.FILETIME)(ft)) }
}

fn LoadLibrary(libname: str)!: Handle {
	utf16libname := integ::UTF16FromStr(libname)
	phandle := Addrcall[*unsafe](uintptr(extern.LoadLibraryW), &utf16libname[0])
	if phandle == nil {
		error(GetLastErrno())
	}
	ret Handle(phandle)
}

fn LoadLibraryEx(libname: str, zero: Handle, flags: uintptr)!: Handle {
	utf16libname := integ::UTF16FromStr(libname)
	phandle := Addrcall[*unsafe](uintptr(extern.LoadLibraryExW), &utf16libname[0], unsafe { (*unsafe)(zero) }, flags)
	if phandle == nil {
		error(GetLastErrno())
	}
	ret Handle(phandle)
}

fn GetProcAddress(module: Handle, procname: str)!: (proc: uintptr) {
	_procname := integ::BytesFromStr(procname)
	procp := Addrcall[*unsafe](uintptr(extern.GetProcAddress), unsafe { (*unsafe)(module) }, &_procname[0])
	if procp == nil {
		error(GetLastErrno())
	}
	ret uintptr(procp)
}

fn FreeLibrary(handle: Handle)! {
	r := Addrcall[i32](uintptr(extern.FreeLibrary), unsafe { (*unsafe)(handle) })
	if r == 0 {
		error(GetLastErrno())
	}
}