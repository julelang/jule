// Copyright 2022-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

#pass "-lshell32" // Link shell32.lib

use integ "std/jule/integrated"
use "std/mem"

cpp use "<fcntl.h>"
cpp use "<io.h>"
cpp use "<windows.h>"

cpp type DWORD: u32
cpp type HANDLE: *unsafe

#typedef
cpp struct LARGE_INTEGER{}

#typedef
cpp struct WIN32_FIND_DATAW{}

#typedef
cpp struct SECURITY_ATTRIBUTES{}

#typedef
cpp struct BY_HANDLE_FILE_INFORMATION{}

#typedef
cpp struct timeval{}

#typedef
cpp struct FILETIME{}

// It is a C enum in the Windows API.
cpp type GET_FILEEX_INFO_LEVELS: int

cpp let LoadLibraryW: *unsafe
cpp let LoadLibraryExW: *unsafe
cpp let GetProcAddress: *unsafe
cpp let FreeLibrary: *unsafe
cpp let GetSystemDirectoryW: *unsafe

cpp fn ExitProcess(exitcode: u32)
cpp fn GetStdHandle(stdh: uintptr): *unsafe
cpp unsafe fn CloseHandle(stdh: *unsafe): bool
cpp unsafe fn GetEnvironmentVariableW(key: *integ::Wchar, mut out: *integ::Wchar, nSize: cpp.DWORD): cpp.DWORD
cpp unsafe fn SetEnvironmentVariableW(key: *integ::Wchar, val: *integ::Wchar): bool
cpp fn GetLastError(): u32
cpp unsafe fn GetCurrentDirectoryW(bufflen: u32, buff: *integ::Wchar): u32
cpp unsafe fn SetCurrentDirectoryW(path: *integ::Wchar): bool
cpp unsafe fn GetFullPathNameW(path: *integ::Wchar, bufflen: u32, buff: *integ::Wchar, fname: **integ::Wchar): u32
cpp unsafe fn DeleteFileW(path: *integ::Wchar): bool
cpp unsafe fn CreateDirectoryW(path: *integ::Wchar, passNullHere: *bool): bool
cpp unsafe fn RemoveDirectoryW(path: *integ::Wchar): bool
cpp unsafe fn GetConsoleMode(handle: cpp.HANDLE, mut mode: *cpp.DWORD): bool
cpp unsafe fn SetConsoleMode(handle: cpp.HANDLE, mode: cpp.DWORD): bool
cpp unsafe fn WriteConsoleW(handle: cpp.HANDLE, buffer: *unsafe,
	toWrite: cpp.DWORD, written: *cpp.DWORD, reserved: *unsafe): bool
cpp unsafe fn ReadConsoleW(handle: cpp.HANDLE, mut buffer: *unsafe,
	toRead: cpp.DWORD, read: *cpp.DWORD, inputControl: *unsafe): bool
cpp fn WSAGetLastError(): int
cpp unsafe fn FindFirstFileW(*integ::Wchar, *cpp.WIN32_FIND_DATAW): cpp.HANDLE
cpp unsafe fn FindNextFileW(cpp.HANDLE, *cpp.WIN32_FIND_DATAW): int
cpp fn FindClose(cpp.HANDLE): int
cpp unsafe fn GetFileAttributesExW(name: *integ::Wchar, infoLevel: cpp.GET_FILEEX_INFO_LEVELS, info: *unsafe): bool
cpp unsafe fn CreateFileW(path: *integ::Wchar, desiredAccess: cpp.DWORD, shareMode: cpp.DWORD, securityAttrs: *cpp.SECURITY_ATTRIBUTES, creationDisposition: cpp.DWORD, flagsAndAttrs: cpp.DWORD, templateFile: cpp.HANDLE): cpp.HANDLE
cpp unsafe fn GetFileType(h: cpp.HANDLE): cpp.DWORD
cpp unsafe fn GetFileInformationByHandle(h: cpp.HANDLE, *cpp.BY_HANDLE_FILE_INFORMATION): bool
cpp unsafe fn ReadFile(h: cpp.HANDLE, buf: *unsafe, bytesToRead: cpp.DWORD, mut bytesRead: *cpp.DWORD, overlap: *cpp.OVERLAPPED): bool
cpp unsafe fn WriteFile(h: cpp.HANDLE, buf: *unsafe, bytesToWrite: cpp.DWORD, mut bytesWritten: *cpp.DWORD, overlap: *cpp.OVERLAPPED): bool
cpp unsafe fn SetEndOfFile(h: cpp.HANDLE): bool
cpp unsafe fn FlushFileBuffers(h: cpp.HANDLE): bool
cpp unsafe fn CreatePipe(r: *cpp.HANDLE, w: *cpp.HANDLE, attrs: *cpp.SECURITY_ATTRIBUTES, size: cpp.DWORD): bool
cpp unsafe fn SetFilePointerEx(h: cpp.HANDLE, toMove: cpp.LARGE_INTEGER, mut newPointer: *cpp.LARGE_INTEGER, moveMethod: cpp.DWORD): bool
cpp unsafe fn GetFileAttributesW(name: *integ::Wchar): cpp.DWORD
cpp unsafe fn SetHandleInformation(hObject: cpp.HANDLE, dwMask: cpp.DWORD, dwFlags: cpp.DWORD): bool
cpp fn GetCurrentProcess(): cpp.HANDLE
cpp unsafe fn DuplicateHandle(hSourceProcessHandle: cpp.HANDLE, hSourceHandle: cpp.HANDLE, hTargetProcessHandle: cpp.HANDLE, lpTargetHandle: *cpp.HANDLE, dwDesiredAccess: cpp.DWORD, bInheritHandle: bool, dwOptions: cpp.DWORD): bool
cpp unsafe fn QueryPerformanceCounter(mut counter: *cpp.LARGE_INTEGER): bool
cpp unsafe fn GetSystemTimeAsFileTime(mut ft: *cpp.FILETIME)

#typedef
cpp struct OVERLAPPED{}

type Handle: uintptr

const InvalidHandle = ^Handle(0)

const (
	STD_INPUT_HANDLE  = -10
	STD_OUTPUT_HANDLE = -11
	STD_ERROR_HANDLE  = -12
)

const (
	MAX_PATH      = 260
	MAX_LONG_PATH = 32768
)

// Invented values to support what package os expects.
const (
	O_RDONLY   = 0x00000
	O_WRONLY   = 0x00001
	O_RDWR     = 0x00002
	O_CREAT    = 0x00040
	O_EXCL     = 0x00080
	O_NOCTTY   = 0x00100
	O_TRUNC    = 0x00200
	O_NONBLOCK = 0x00800
	O_APPEND   = 0x00400
	O_SYNC     = 0x01000
	O_ASYNC    = 0x02000
	O_CLOEXEC  = 0x80000
)

// Invented values to support what package os expects.
const (
	S_IFMT   = 0x1f000
	S_IFIFO  = 0x1000
	S_IFCHR  = 0x2000
	S_IFDIR  = 0x4000
	S_IFBLK  = 0x6000
	S_IFREG  = 0x8000
	S_IFLNK  = 0xa000
	S_IFSOCK = 0xc000
	S_ISUID  = 0x800
	S_ISGID  = 0x400
	S_ISVTX  = 0x200
	S_IRUSR  = 0x100
	S_IWRITE = 0x80
	S_IWUSR  = 0x80
	S_IXUSR  = 0x40
)

let STDIN = GetStdHandle(uintptr(u32(int(STD_INPUT_HANDLE))))
let STDOUT = GetStdHandle(uintptr(u32(int(STD_OUTPUT_HANDLE))))
let STDERR = GetStdHandle(uintptr(u32(int(STD_ERROR_HANDLE))))

fn GetStdHandle(stdh: uintptr): Handle { ret Handle(cpp.GetStdHandle(stdh)) }

fn CloseHandle(stdh: Handle)! {
	r := unsafe { cpp.CloseHandle(unsafe { (*unsafe)(stdh) }) }
	if !r {
		error(GetLastErrno())
	}
}

// Wrapper for C's _wopen function.
fn SetEndOfFile(h: Handle)! {
	r := unsafe { cpp.SetEndOfFile(cpp.HANDLE(h)) }
	if !r {
		error(GetLastErrno())
	}
}

// Creates an anonymous pipe, and returns handles to the read and write ends of the pipe.
unsafe fn CreatePipe(mut r: *Handle, mut w: *Handle, attrs: *SecurityAttributes, size: u32): bool {
	ret cpp.CreatePipe((*cpp.HANDLE)(r), (*cpp.HANDLE)(w), (*cpp.SECURITY_ATTRIBUTES)(attrs), cpp.DWORD(size))
}

// Flushes the buffers of a specified file and causes all buffered data to be written to a file.
fn FlushFileBuffers(h: Handle)! {
	r := unsafe { cpp.FlushFileBuffers(cpp.HANDLE(h)) }
	if !r {
		error(GetLastErrno())
	}
}

// Returns last Windows error.
fn GetLastError(): u32 { ret cpp.GetLastError() }

// Reads working directory into buff and returns read
// rune count of current process. Returns 0 if fail.
unsafe fn GetCurrentDirectory(bufflen: u32, mut buff: *u16): u32 {
	ret cpp.GetCurrentDirectoryW(bufflen, (*integ::Wchar)(buff))
}

// Sets working directory to path.
// Reports operation is success.
unsafe fn SetCurrentDirectory(path: *u16): bool {
	ret cpp.SetCurrentDirectoryW((*integ::Wchar)(path))
}

// Create file.
unsafe fn CreateFile(path: *u16, desiredAccess: u32, shareMode: u32, securityAttrs: *SecurityAttributes, creationDisposition: u32, flagsAndAttrs: u32, templateFile: Handle): Handle {
	ret Handle(cpp.CreateFileW(
		(*integ::Wchar)(path),
		cpp.DWORD(desiredAccess),
		cpp.DWORD(shareMode),
		(*cpp.SECURITY_ATTRIBUTES)(securityAttrs),
		cpp.DWORD(creationDisposition),
		cpp.DWORD(flagsAndAttrs),
		cpp.HANDLE(templateFile)))
}

// Read file.
// Passes nil for overlap.
fn ReadFile(h: Handle, buf: []byte)!: int {
	mut read := u32(0)
	ok := unsafe { cpp.ReadFile(cpp.HANDLE(h), &buf[0], cpp.DWORD(len(buf)), (*cpp.DWORD)(&read), nil) }
	if !ok {
		error(GetLastErrno())
	}
	ret int(read)
}

// Write file.
// Passes nil for overlap.
fn WriteFile(h: Handle, buf: []byte)!: int {
	mut written := u32(0)
	ok := unsafe { cpp.WriteFile(cpp.HANDLE(h), &buf[0], cpp.DWORD(len(buf)), (*cpp.DWORD)(&written), nil) }
	if !ok {
		error(GetLastErrno())
	}
	ret int(written)
}

// Calls Windows's GetFileType function.
unsafe fn GetFileType(h: Handle): (t: u32, err: bool) {
	t = u32(cpp.GetFileType(cpp.HANDLE(h)))
	err = t == FILE_TYPE_UNKNOWN && GetLastError() != 0
	ret
}

// Calls Windows's GetFileInformationByHandle function.
unsafe fn GetFileInformationByHandle(h: Handle, info: *ByHandleFileInformation): bool {
	ret cpp.GetFileInformationByHandle(cpp.HANDLE(h), (*cpp.BY_HANDLE_FILE_INFORMATION)(info))
}

// Delete file.
unsafe fn DeleteFile(path: *u16): bool {
	ret cpp.DeleteFileW((*integ::Wchar)(path))
}

// Creates directory.
unsafe fn CreateDirectory(path: *u16): bool {
	ret cpp.CreateDirectoryW((*integ::Wchar)(path), nil)
}

// Removes empty directory.
unsafe fn RemoveDirectory(path: *u16): bool {
	ret cpp.RemoveDirectoryW((*integ::Wchar)(path))
}

// Calls Windows's GetFullPathNameW function.
unsafe fn GetFullPathName(path: *u16, bufflen: u32,
	buff: *u16, fname: **u16): u32 {
	ret cpp.GetFullPathNameW((*integ::Wchar)(path), bufflen, (*integ::Wchar)(buff), (**integ::Wchar)(fname))
}

// Calls Windows's GetConsoleMode function.
unsafe fn GetConsoleMode(handle: Handle, mut mode: *u32): bool {
	ret unsafe { cpp.GetConsoleMode(cpp.HANDLE(handle), (*cpp.DWORD)(mode)) }
}

// Calls Windows's SetConsoleMode function.
unsafe fn SetConsoleMode(handle: Handle, mode: u32): bool {
	ret unsafe { cpp.SetConsoleMode(cpp.HANDLE(handle), cpp.DWORD(mode)) }
}

// Calls Windows's WriteConsoleW function.
unsafe fn WriteConsole(handle: Handle, buffer: *u16, toWrite: u32,
	mut &written: *u32, reserved: *unsafe)! {
	ok := cpp.WriteConsoleW(cpp.HANDLE(handle), (*unsafe)(buffer),
		cpp.DWORD(toWrite), (*cpp.DWORD)(written), reserved)
	if !ok {
		error(GetLastErrno())
	}
}

// Calls Windows's ReadConsoleW function.
// Passes nil pointer for input control.
unsafe fn ReadConsole(handle: Handle, mut buffer: *u16, toRead: u32, mut &read: *u32)! {
	r := cpp.ReadConsoleW(cpp.HANDLE(handle), (*unsafe)(buffer),
		cpp.DWORD(toRead), (*cpp.DWORD)(read), nil)
	if !r {
		error(GetLastErrno())
	}
}

// Calls Windows's WSAGetLastError function.
fn WSAGetLastError(): int {
	ret cpp.WSAGetLastError()
}

// Call's Windows FindFirstFileW function.
unsafe fn FindFirstFile(path: *u16, mut data: *Win32FindData): Handle {
	ret Handle(cpp.FindFirstFileW((*integ::Wchar)(path), (*cpp.WIN32_FIND_DATAW)(data)))
}

// Call's Windows FindNextFileW function.
unsafe fn FindNextFile(h: Handle, mut data: *Win32FindData): int {
	ret cpp.FindNextFileW(cpp.HANDLE(h), (*cpp.WIN32_FIND_DATAW)(data))
}

// Call's Windows FindClose function.
fn FindClose(h: Handle): int {
	ret unsafe { cpp.FindClose(cpp.HANDLE(h)) }
}

// Windows's GetEnvironmentVariableW function.
unsafe fn GetEnvironmentVariable(key: *u16, mut out: *u16, outLen: u32): u32 {
	ret u32(cpp.GetEnvironmentVariableW((*integ::Wchar)(key), (*integ::Wchar)(out), cpp.DWORD(outLen)))
}

// Windows's SetEnvironmentVariableW function.
unsafe fn SetEnvironmentVariable(key: *u16, val: *u16): bool {
	ret cpp.SetEnvironmentVariableW((*integ::Wchar)(key), (*integ::Wchar)(val))
}

// Windows's GetFileAttributesExW function.
unsafe fn GetFileAttributesEx(name: *u16, infoLevel: int, info: *unsafe): bool {
	ret cpp.GetFileAttributesExW((*integ::Wchar)(name), cpp.GET_FILEEX_INFO_LEVELS(infoLevel), info)
}

// Windows's GetFileAttributes function.
unsafe fn GetFileAttributesW(name: *u16): u32 {
	ret u32(cpp.GetFileAttributesW((*integ::Wchar)(name)))
}

// Windows's SetHandleInformation function.
unsafe fn SetHandleInformation(h: Handle, dwMask: u32, dwFlags: u32): bool {
	ret cpp.SetHandleInformation(cpp.HANDLE(h), cpp.DWORD(dwMask), cpp.DWORD(dwFlags))
}

// Windows's GetCurrentProcess function.
fn GetCurrentProcess(): Handle {
	ret unsafe { Handle(cpp.GetCurrentProcess()) }
}

// Windows's DuplicateHandle function.
unsafe fn DuplicateHandle(hSourceProcessHandle: Handle, hSourceHandle: Handle,
	hTargetProcessHandle: Handle, mut lpTargetHandle: *Handle, dwDesiredAccess: u32,
	bInheritHandle: bool, dwOptions: u32): bool {
	ret cpp.DuplicateHandle(cpp.HANDLE(hSourceProcessHandle), cpp.HANDLE(hSourceHandle),
		cpp.HANDLE(hTargetProcessHandle), (*cpp.HANDLE)(lpTargetHandle), cpp.DWORD(dwDesiredAccess),
		bInheritHandle, cpp.DWORD(dwOptions))
}

// Calls SetFilePointerEx.
// See https://learn.microsoft.com/en-us/windows/win32/api/fileapi/nf-fileapi-setfilepointerex
fn setFilePointerEx(handle: Handle, distToMove: i64, mut &newFilePointer: *i64, whence: u32)! {
	ok := unsafe { cpp.SetFilePointerEx(cpp.HANDLE(handle), *(*cpp.LARGE_INTEGER)(&distToMove), (*cpp.LARGE_INTEGER)(newFilePointer), cpp.DWORD(whence)) }
	if !ok {
		error(GetLastErrno())
	}
}

// Imitates the POSIX seek function.
fn Seek(h: Handle, offset: i64, whence: int)!: (newoffset: i64) {
	let mut w: u32
	match whence {
	| 0: // io::SeekStart
		w = FILE_BEGIN
	| 1: // io::SeekCurrent
		w = FILE_CURRENT
	| 2: // io::SeekEnd
		w = FILE_END
	}
	setFilePointerEx(h, offset, &newoffset, w)?
	ret
}

// Imitates the POSIX ftruncate function.
fn Ftruncate(h: Handle, size: i64)! {
	Seek(h, size, 0)?
	SetEndOfFile(h)?
}

fn makeInheritSa(mut &sa: *SecurityAttributes) {
	sa.Length = u32(mem::SizeOf(*sa))
	sa.InheritHandle = 1
}

fn Pipe()!: (r: Handle, w: Handle) {
	let p: [2]Handle
	let mut sa: SecurityAttributes
	makeInheritSa(&sa)
	ok := unsafe { CreatePipe(&p[0], &p[1], &sa, 0) }
	if !ok {
		error(GetLastErrno())
	}
	r = p[0]
	w = p[1]
	ret
}

fn Open(path: str, flag: int, perm: u32)!: Handle {
	if path == "" {
		error(Errno(ERROR_FILE_NOT_FOUND))
	}
	utf16Path := integ::UTF16FromStr(path)
	let mut access: u32
	match flag & (O_RDONLY | O_WRONLY | O_RDWR) {
	| O_RDONLY:
		access = GENERIC_READ
	| O_WRONLY:
		access = GENERIC_WRITE
	| O_RDWR:
		access = GENERIC_READ | GENERIC_WRITE
	}
	if flag&O_CREAT != 0 {
		access |= GENERIC_WRITE
	}
	if flag&O_APPEND != 0 {
		// Remove GENERIC_WRITE unless O_TRUNC is set, in which case we need it to truncate the file.
		// We can't just remove FILE_WRITE_DATA because GENERIC_WRITE without FILE_WRITE_DATA
		// starts appending at the beginning of the file rather than at the end.
		if flag&O_TRUNC == 0 {
			access &= ^GENERIC_WRITE
		}
		// Set all access rights granted by GENERIC_WRITE except for FILE_WRITE_DATA.
		access |= FILE_APPEND_DATA | FILE_WRITE_ATTRIBUTES | FILE_WRITE_EA | STANDARD_RIGHTS_WRITE | SYNCHRONIZE
	}
	sharemode := u32(FILE_SHARE_READ | FILE_SHARE_WRITE)
	let mut sa: SecurityAttributes
	if flag&O_CLOEXEC == 0 {
		makeInheritSa(&sa)
	}
	// We don't use CREATE_ALWAYS, because when opening a file with
	// FILE_ATTRIBUTE_READONLY these will replace an existing file
	// with a new, read-only one. See https://go.dev/issue/38225.
	//
	// Instead, we ftruncate the file after opening when O_TRUNC is set.
	let mut createmode: u32
	match {
	| flag&(O_CREAT|O_EXCL) == (O_CREAT|O_EXCL):
		createmode = CREATE_NEW
	| flag&O_CREAT == O_CREAT:
		createmode = OPEN_ALWAYS
	|:
		createmode = OPEN_EXISTING
	}
	let mut attrs: u32 = FILE_ATTRIBUTE_NORMAL
	if perm&S_IWRITE == 0 {
		attrs = FILE_ATTRIBUTE_READONLY
	}
	if flag&O_WRONLY == 0 && flag&O_RDWR == 0 {
		// We might be opening or creating a directory.
		// CreateFile requires FILE_FLAG_BACKUP_SEMANTICS
		// to work with directories.
		attrs |= FILE_FLAG_BACKUP_SEMANTICS
	}
	if flag&O_SYNC != 0 {
		const _FILE_FLAG_WRITE_THROUGH = 0x80000000
		attrs |= _FILE_FLAG_WRITE_THROUGH
	}
	h := unsafe { CreateFile(&utf16Path[0], access, sharemode, &sa, createmode, attrs, 0) }
	if h == InvalidHandle {
		mut e := GetLastErrno()
		if e == ERROR_ACCESS_DENIED && (flag&O_WRONLY != 0 || flag&O_RDWR != 0) {
			// We should return EISDIR when we are trying to open a directory with write access.
			fa := unsafe { GetFileAttributesW(&utf16Path[0]) }
			if fa != INVALID_FILE_ATTRIBUTES && fa&FILE_ATTRIBUTE_DIRECTORY != 0 {
				e = EISDIR
			}
		}
		error(GetLastErrno())
	}
	// Ignore O_TRUNC if the file has just been created.
	if flag&O_TRUNC == O_TRUNC &&
		(createmode == OPEN_EXISTING || (createmode == OPEN_ALWAYS && GetLastErrno() == ERROR_ALREADY_EXISTS)) {
		Ftruncate(h, 0) else {
			CloseHandle(h)?
			error(error)
		}
	}
	ret h
}

fn ExitProcess(exitcode: u32) {
	cpp.ExitProcess(exitcode)
}

fn Exit(code: int) {
	ExitProcess(u32(code))
}

fn QueryPerformanceCounter()!: i64 {
	mut counter := i64(0)
	if unsafe { !cpp.QueryPerformanceCounter((*cpp.LARGE_INTEGER)(&counter)) } {
		panic("runtime: nanotime failed")
	}
	ret counter
}

fn GetSystemTimeAsFileTime(mut &ft: *Filetime) {
	unsafe { cpp.GetSystemTimeAsFileTime((*cpp.FILETIME)(ft)) }
}

fn LoadLibrary(libname: str)!: Handle {
	utf16libname := integ::UTF16FromStr(libname)
	phandle := Addrcall[*unsafe](uintptr(cpp.LoadLibraryW), &utf16libname[0])
	if phandle == nil {
		error(GetLastErrno())
	}
	ret Handle(phandle)
}

fn LoadLibraryEx(libname: str, zero: Handle, flags: uintptr)!: Handle {
	utf16libname := integ::UTF16FromStr(libname)
	phandle := Addrcall[*unsafe](uintptr(cpp.LoadLibraryExW), &utf16libname[0], unsafe { (*unsafe)(zero) }, flags)
	if phandle == nil {
		error(GetLastErrno())
	}
	ret Handle(phandle)
}

fn GetProcAddress(module: Handle, procname: str)!: (proc: uintptr) {
	_procname := integ::StrToBytes(procname)
	procp := Addrcall[*unsafe](uintptr(cpp.GetProcAddress), unsafe { (*unsafe)(module) }, &_procname[0])
	if procp == nil {
		error(GetLastErrno())
	}
	ret uintptr(procp)
}

fn FreeLibrary(handle: Handle)! {
	r := Addrcall[i32](uintptr(cpp.FreeLibrary), unsafe { (*unsafe)(handle) })
	if r == 0 {
		error(GetLastErrno())
	}
}