// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/sys"

fn open(name: str): (uintptr, ok: bool) {
	fd := sys::Open(name, sys::O_RDONLY, 0) else {
		ret 0, false
	}
	ret uintptr(fd), true
}

fn read(fd: uintptr, mut buf: []byte): (n: int, ok: bool) {
	if len(buf) == 0 {
		// If the caller wanted a zero byte read, return immediately
		// without trying to read.
		ret 0, true
	}
	ok = true
	n = sys::Read(int(fd), buf) else {
		ok = false
		use 0
	}
	ret
}

fn preadn(fd: uintptr, mut buf: []byte, off: int): (ok: bool) {
	mut whence := seekStart
	if off < 0 {
		whence = seekEnd
	}
	sys::Seek(int(fd), i64(off), whence) else {
		ret false
	}
	for len(buf) > 0 {
		m := sys::Read(int(fd), buf) else {
			ret false
		}
		buf = buf[m:]
	}
	ret true
}

fn closefd(fd: uintptr) {
	sys::Close(int(fd))!
}