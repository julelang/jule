// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Parse "zoneinfo" time zone file.
// This is a fairly standard file format used on OS X, Linux, BSD, Sun, and others.
// See tzfile(5), https://en.wikipedia.org/wiki/Zoneinfo,
// and ftp://munnari.oz.au/pub/oldtz/

use "std/sys"

// Many systems use /usr/share/zoneinfo, Solaris 2 has
// /usr/share/lib/zoneinfo, IRIX 6 has /usr/lib/locale/TZ,
// NixOS has /etc/zoneinfo.
static platformZoneSources = [
	"/usr/share/zoneinfo/",
	"/usr/share/lib/zoneinfo/",
	"/usr/lib/locale/TZ/",
	"/etc/zoneinfo",
]

fn initLocal() {
	// consult $TZ to find the time zone to use.
	// no $TZ means use the system default /etc/localtime.
	// $TZ="" means use UTC.
	// $TZ="foo" or $TZ=":foo" if foo is an absolute path, then the file pointed
	// by foo will be used to initialize timezone; otherwise, file
	// /usr/share/zoneinfo/foo will be used.

	mut tz := "TZ\000"
	tz, _ = unsafe { sys::Getenv(&tz[0]) }
	match {
	| len(tz) == 0:
		mut z, ok := loadLocation("localtime", ["/etc"])
		if ok {
			localLoc = *z
			localLoc.name = "Local"
			ret
		}
	|:
		if tz[0] == ':' {
			tz = tz[1:]
		}
		if tz != "" && tz[0] == '/' {
			mut z, ok := loadLocation(tz, [""])
			if ok {
				localLoc = *z
				if tz == "/etc/localtime" {
					localLoc.name = "Local"
				} else {
					localLoc.name = tz
				}
				ret
			}
		} else if tz != "" && tz != "UTC" {
			mut z, ok := loadLocation(tz, platformZoneSources)
			if ok {
				localLoc = *z
				ret
			}
		}
	}

	// Fall back to UTC.
	localLoc.name = "UTC"
}