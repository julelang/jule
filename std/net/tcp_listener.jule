// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/poll"
use "std/mem"
use "std/sys"
use "std/time"

// TCP listener.
// In most cases, represents TCP server.
struct TCPListener {
	addr:   &TCPAddr
	mut v6: bool
	mut fd: &netFD
}

impl Listener for TCPListener {}

impl TCPListener {
	// Binds new TCP listener and starts listening given address.
	// Returns relevant created &TCPListener if success.
	// If addr is not a valid address, it will forward relevant parse exceptionals.
	// In addition, any bind and listening error will be return as exceptional.
	//
	// See the [Dial] function for a description of the addr parameter.
	async fn Bind(addr: str)!: &TCPListener {
		ret tcpBind(Network.TCP, addr).await?
	}

	// Accepts incoming connection, returns &TCPConn.
	// Panics if connection is closed.
	async fn Accept(*self)!: Conn {
		if self.fd == nil {
			panic("net: TCPListener.Accept: connection is not open")
		}
		if self.v6 { // IPv6
			mut clientAddr := sys::RawSockaddrInet6{}
			mut clientAddrLen := sys::Socklen(mem::SizeOf(clientAddr))
			handle := self.fd.accept(6, uintptr(&clientAddr), &clientAddrLen).await?
			mut ip := make(IP, IPv6Len)
			copy(ip, IP(clientAddr.Addr[:]))
			ret newTCPConn(&TCPConn{
				Addr: &TCPAddr{
					IP: ip,
					Port: int(ntohs(int(clientAddr.Port))),
				},
				fd: newFD(handle, poll::Socket, sys::SOCK_STREAM),
				v6: true,
			})
		} else { // IPv4
			mut clientAddr := sys::RawSockaddrInet4{}
			mut clientAddrLen := sys::Socklen(mem::SizeOf(clientAddr))
			handle := self.fd.accept(4, uintptr(&clientAddr), &clientAddrLen).await?
			mut ip := make(IP, IPv4Len)
			copy(ip, IP(clientAddr.Addr[:]))
			ret newTCPConn(&TCPConn{
				Addr: &TCPAddr{
					IP: ip,
					Port: int(ntohs(int(clientAddr.Port))),
				},
				fd: newFD(handle, poll::Socket, sys::SOCK_STREAM),
			})
		}
	}

	// Returns network name which is listening.
	// If connection closed, returns Network.TCP as a general network.
	fn Network(*self): Network {
		match {
		| self.fd == nil:
			ret Network.TCP
		| self.v6:
			ret Network.TCP6
		|:
			ret Network.TCP4
		}
	}

	// Closes connection.
	async fn Close(mut *self)! {
		if self.fd == nil {
			ret
		}
		self.fd.Close().await?
		self.fd = nil
		self.v6 = false
	}
}

async fn tcpBind(network: Network, addr: str)!: &TCPListener {
	mut tcpAddr := TCPAddr.Resolve(network, addr).await?
	mut fd := (&netFD)(nil)
	mut v6 := false
	if tcpAddr.IP.Empty() {
		match network {
		| TCP | TCP4:
			mut handle := tcpsocketipv4()?
			fd = newFD(handle, poll::Socket, sys::SOCK_STREAM)
			mut sockAddr := sys::RawSockaddrInet4{}
			sockAddr.Family = sys::AF_INET
			sockAddr.Port = htons(tcpAddr.Port)
			handleprepare(handle) else {
				fd.Close().await?
				error(error)
			}
			sys::Bind(handle, uintptr(&sockAddr), sys::Socklen(mem::SizeOf(sockAddr))) else {
				fd.Close().await?
				error(error)
			}
		| TCP6:
			v6 = true
			handle := tcpsocketipv6()?
			fd = newFD(handle, poll::Socket, sys::SOCK_STREAM)
			mut sockAddr := sys::RawSockaddrInet6{}
			sockAddr.Family = sys::AF_INET6
			sockAddr.Port = htons(tcpAddr.Port)
			handleprepare(handle) else {
				fd.Close().await?
				error(error)
			}
			sys::Bind(handle, uintptr(&sockAddr), sys::Socklen(mem::SizeOf(sockAddr))) else {
				fd.Close().await?
				error(error)
			}
		|:
			error(&AddrError{Err: "invalid network: " + str(network)})
		}
	} else {
		mut ipv4 := tcpAddr.IP.To4()
		if ipv4.Empty() {
			if network == Network.TCP4 {
				error(&AddrError{Err: "expected IPv4 address but found IPv6", Addr: addr})
			}
			v6 = true
			handle := tcpsocketipv6()?
			fd = newFD(handle, poll::Socket, sys::SOCK_STREAM)
			mut sockAddr := sys::RawSockaddrInet6{}
			sockAddr.Family = sys::AF_INET6
			sockAddr.Port = htons(tcpAddr.Port)
			ipv6 := tcpAddr.IP.To16()
			copy(sockAddr.Addr[:], ipv6)
			handleprepare(handle) else {
				fd.Close().await?
				error(error)
			}
			sys::Bind(handle, uintptr(&sockAddr), sys::Socklen(mem::SizeOf(sockAddr))) else {
				fd.Close().await?
				error(error)
			}
		} else {
			if network == Network.TCP6 {
				error(&AddrError{Err: "expected IPv6 address but found IPv4", Addr: addr})
			}
			tcpAddr.IP = ipv4
			handle := tcpsocketipv4()?
			fd = newFD(handle, poll::Socket, sys::SOCK_STREAM)
			mut sockAddr := sys::RawSockaddrInet4{}
			sockAddr.Family = sys::AF_INET
			sockAddr.Port = htons(tcpAddr.Port)
			copy(sockAddr.Addr[:], ipv4)
			handleprepare(handle) else {
				fd.Close().await?
				error(error)
			}
			sys::Bind(handle, uintptr(&sockAddr), sys::Socklen(mem::SizeOf(sockAddr))) else {
				fd.Close().await?
				error(error)
			}
		}
	}
	backlog := maxListenerBacklog().await
	sys::Listen(fd.Handle(), backlog) else {
		fd.Close().await?
		error(error)
	}
	ret &TCPListener{
		v6: v6,
		addr: tcpAddr,
		fd: fd,
	}
}