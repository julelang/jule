// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/poll"
use "std/mem"
use "std/sys"
use "std/time"

// TCP listener.
// In most cases, represents TCP server.
struct TCPListener {
	addr:   &TCPAddr
	ipv:    int // 4 for IPv4, 6 for IPv6
	mut fd: &netFD
}

impl Listener for TCPListener {}

impl TCPListener {
	// Binds new TCP listener and starts listening given address.
	// Returns relevant created &TCPListener if success.
	// If addr is not a valid address, it will forward relevant parse exceptionals.
	// In addition, any bind and listening error will be return as exceptional.
	//
	// See the [Dial] function for a description of the addr parameter.
	async fn Bind(addr: str)!: &TCPListener {
		ret tcpBind(Network.TCP, addr).await?
	}

	// Accepts incoming connection, returns &TCPConn.
	// Panics if connection is closed.
	async fn Accept(*self)!: Conn {
		if self.fd == nil {
			panic("net: TCPListener.Accept: connection is not open")
		}
		mut fd, mut addr := self.fd.accept(self.ipv).await?
		ret newTCPConn(&TCPConn{fd: fd, Addr: addr})
	}

	// Returns network name which is listening.
	// If connection closed, returns Network.TCP as a general network.
	fn Network(*self): Network {
		match {
		| self.fd == nil:
			ret Network.TCP
		| self.ipv == 4:
			ret Network.TCP4
		| self.ipv == 6:
			ret Network.TCP6
		|:
			ret Network.TCP
		}
	}

	// Closes connection.
	async fn Close(mut *self)! {
		if self.fd == nil {
			ret
		}
		self.fd.Close().await?
		self.fd = nil
		self.ipv = 0
	}
}

async fn tcpBind(network: Network, addr: str)!: &TCPListener {
	mut tcpAddr := TCPAddr.Resolve(network, addr).await?
	mut fd := (&netFD)(nil)
	let mut ipv: int
	if tcpAddr.IP.Empty() {
		match network {
		| TCP | TCP4:
			ipv = 4
			mut sockaddr := sys::RawSockaddrInet4{}
			fd = socketipv4(false, uintptr(&sockaddr), tcpAddr, sys::SOCK_STREAM, 0).await?
		| TCP6:
			ipv = 6
			mut sockaddr := sys::RawSockaddrInet6{}
			fd = socketipv6(false, uintptr(&sockaddr), tcpAddr, sys::SOCK_STREAM, 0).await?
		|:
			error(&AddrError{Err: "invalid network: " + str(network)})
		}
	} else {
		mut ipv4 := tcpAddr.IP.To4()
		if ipv4.Empty() {
			if network == Network.TCP4 {
				error(&AddrError{Err: "expected IPv4 address but found IPv6", Addr: addr})
			}
			ipv = 6
			mut sockaddr := sys::RawSockaddrInet6{}
			fd = socketipv6(false, uintptr(&sockaddr), tcpAddr, sys::SOCK_STREAM, 0).await?
		} else {
			if network == Network.TCP6 {
				error(&AddrError{Err: "expected IPv6 address but found IPv4", Addr: addr})
			}
			ipv = 4
			tcpAddr.IP = ipv4
			mut sockaddr := sys::RawSockaddrInet4{}
			fd = socketipv4(false, uintptr(&sockaddr), tcpAddr, sys::SOCK_STREAM, 0).await?
		}
	}
	ret &TCPListener{
		ipv: ipv,
		addr: tcpAddr,
		fd: fd,
	}
}