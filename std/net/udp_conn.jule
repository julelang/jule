// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/poll"
use "std/io"
use "std/mem"
use "std/sys"
use "std/time"

// UDP connection.
// This structure represents server and client connections.
struct UDPConn {
	mut sockaddr4: sys::SockaddrInet4
	mut sockaddr6: sys::SockaddrInet6
	mut fd:        &poll::FD
	Addr:          &UDPAddr
	v6:            bool
}

impl Conn for UDPConn {}
impl io::Reader for UDPConn {}
impl io::Writer for UDPConn {}
impl io::Stream for UDPConn {}
impl io::WriteCloser for UDPConn {}

impl UDPConn {
	// Binds new UDP listener and starts listening given address.
	// Returns relevant created &UDPConn if success.
	// If addr is not a valid address, it will forward relevant parse exceptionals.
	// In addition, any bind and listening error will be return as exceptional.
	//
	// See the [Dial] function for a description of the addr parameter.
	fn Bind(addr: str)!: &UDPConn {
		ret udpBind(Network.UDP, addr) else { error(error) }
	}

	// Connects to UDP listener by given address.
	// Returns relevant created &UDPConn if success.
	// If addr is not a valid address, it will forward relevant parse exceptionals.
	// In addition, any bind and listening error will be return as exceptional.
	//
	// See the [Dial] function for a description of the addr parameter.
	fn Dial(addr: str)!: &UDPConn {
		ret udpDial(Network.UDP, addr) else { error(error) }
	}

	// Read bytes to buffer from connection and returns read byte count.
	// The number of bytes read can never exceed the length of the buffer.
	// If the buffer is larger than the number of bytes that can be read,
	// the buffer will not cause an overflow.
	// It will panic if connection is closed.
	fn Read(mut self, mut buf: []byte)!: (n: int) {
		if self.fd == nil {
			panic("net: UDPConn.Read: connection is closed")
		}
		if self.v6 {
			ret self.fd.ReadV6(buf, self.sockaddr6) else { error(error) }
		} else {
			ret self.fd.ReadV4(buf, self.sockaddr4) else { error(error) }
		}
	}

	// Writes bytes to connection and returns written byte count.
	// The number of bytes written can never exceed the length of the buffer.
	fn Write(mut self, buf: []byte)!: (n: int) {
		if self.fd == nil {
			panic("net: UDPConn.Write: connection is closed")
		}
		if self.v6 {
			ret self.fd.WriteV6(buf, self.sockaddr6) else { error(error) }
		} else {
			ret self.fd.WriteV4(buf, self.sockaddr4) else { error(error) }
		}
	}

	// Sets read timeout for connection.
	// Timeout precision is microseconds.
	// If the timeout is below one microsecond it will be accepted as zero.
	// The zero timeout, clears current timeout if exist.
	fn SetReadTimeout(mut self, timeout: time::Duration)! {
		if self.fd == nil {
			panic("net: TCPConn.SetReadTimeout: connection is closed")
		}
		setSocketTimeout(poll::NetHandle(self.fd.File), sys::SO_RCVTIMEO, timeout) else { error(error) }
	}

	// Sets write timeout for connection.
	// Timeout precision is microseconds.
	// If the timeout is below one microsecond it will be accepted as zero.
	// The zero timeout, clears current timeout if exist.
	fn SetWriteTimeout(mut self, timeout: time::Duration)! {
		if self.fd == nil {
			panic("net: TCPConn.SetReadTimeout: connection is closed")
		}
		setSocketTimeout(poll::NetHandle(self.fd.File), sys::SO_SNDTIMEO, timeout) else { error(error) }
	}

	// Returns network name which is connected or listening.
	// If connection closed, returns Network.UDP as a general network.
	fn Network(self): Network {
		match {
		| self.fd == nil:
			ret Network.UDP
		| self.v6:
			ret Network.UDP6
		|:
			ret Network.UDP4
		}
	}

	// Closes connection.
	fn Close(mut self)! {
		if self.fd == nil {
			ret
		}
		self.fd.Close() else { error(error) }
		self.fd = nil
		self.v6 = false
	}
}

fn udpBind(network: Network, &addr: str)!: &UDPConn {
	mut connAddr := UDPAddr.Resolve(network, addr) else { error(error) }
	mut conn := UDPConn{
		Addr: connAddr,
	}
	if conn.Addr.IP.Empty() {
		match network {
		| UDP | UDP4:
			handle := sys::Socket(sys::AF_INET, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			conn.sockaddr4 = sys::SockaddrInet4{}
			conn.sockaddr4.Family = sys::AF_INET
			conn.sockaddr4.Port = htons(conn.Addr.Port)
			sys::Bind(handle, uintptr(&conn.sockaddr4), sys::Socklen(mem::SizeOf(conn.sockaddr4))) else {
				conn.fd.Close() else { error(error) }
				error(error)
			}
		| UDP6:
			conn.v6 = true
			handle := sys::Socket(sys::AF_INET6, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			mut sockAddr := sys::SockaddrInet6{}
			sockAddr.Family = sys::AF_INET6
			sockAddr.Port = htons(conn.Addr.Port)
			sys::Bind(handle, uintptr(&sockAddr), sys::Socklen(mem::SizeOf(sockAddr))) else {
				conn.fd.Close() else { error(error) }
				error(error)
			}
		|:
			error(&AddrError{Err: "invalid network: " + str(network)})
		}
	} else {
		mut ipv4 := conn.Addr.IP.To4()
		if ipv4.Empty() {
			if network == Network.UDP4 {
				error(&AddrError{Err: "expected IPv4 address but found IPv6", Addr: addr})
			}
			conn.v6 = true
			handle := sys::Socket(sys::AF_INET6, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			conn.sockaddr6 = sys::SockaddrInet6{}
			conn.sockaddr6.Family = sys::AF_INET6
			conn.sockaddr6.Port = htons(conn.Addr.Port)
			ipv6 := conn.Addr.IP.To16()
			copy(conn.sockaddr6.Addr[:], ipv6)
			sys::Bind(handle, uintptr(&conn.sockaddr6), sys::Socklen(mem::SizeOf(conn.sockaddr6))) else {
				conn.fd.Close() else { error(error) }
				error(error)
			}
		} else {
			if network == Network.UDP6 {
				error(&AddrError{Err: "expected IPv6 address but found IPv4", Addr: addr})
			}
			conn.Addr.IP = ipv4
			handle := sys::Socket(sys::AF_INET, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			conn.sockaddr4 = sys::SockaddrInet4{}
			conn.sockaddr4.Family = sys::AF_INET
			conn.sockaddr4.Port = htons(conn.Addr.Port)
			copy(conn.sockaddr4.Addr[:], ipv4)
			sys::Bind(handle, uintptr(&conn.sockaddr4), sys::Socklen(mem::SizeOf(conn.sockaddr4))) else {
				conn.fd.Close() else { error(error) }
				error(error)
			}
		}
	}
	ret new(UDPConn, conn)
}

fn udpDial(network: Network, &addr: str)!: &UDPConn {
	mut connAddr := UDPAddr.Resolve(network, addr) else { error(error) }
	mut conn := UDPConn{
		Addr: connAddr,
	}

	if conn.Addr.IP.Empty() {
		match network {
		| UDP | UDP4:
			handle := sys::Socket(sys::AF_INET, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			conn.sockaddr4 = sys::SockaddrInet4{}
			conn.sockaddr4.Family = sys::AF_INET
			conn.sockaddr4.Port = htons(conn.Addr.Port)
		| UDP6:
			handle := sys::Socket(sys::AF_INET6, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			conn.sockaddr6 = sys::SockaddrInet6{}
			conn.sockaddr6.Family = sys::AF_INET6
			conn.sockaddr6.Port = htons(conn.Addr.Port)
		|:
			error(&AddrError{Err: "invalid network: " + str(network)})
		}
	} else {
		mut ipv4 := conn.Addr.IP.To4()
		if ipv4.Empty() {
			if network == Network.UDP4 {
				error(&AddrError{Err: "expected IPv4 address but found IPv6", Addr: addr})
			}
			conn.v6 = true
			handle := sys::Socket(sys::AF_INET6, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			conn.sockaddr6 = sys::SockaddrInet6{}
			conn.sockaddr6.Family = sys::AF_INET6
			conn.sockaddr6.Port = htons(conn.Addr.Port)
			ipv6 := conn.Addr.IP.To16()
			copy(conn.sockaddr6.Addr[:], ipv6)
		} else {
			if network == Network.UDP6 {
				error(&AddrError{Err: "expected IPv6 address but found IPv4", Addr: addr})
			}
			conn.Addr.IP = ipv4
			handle := sys::Socket(sys::AF_INET, sys::SOCK_DGRAM, sys::IPPROTO_UDP) else { error(error) }
			conn.fd = newFD(handle, poll::SocketNoConn, sys::SOCK_DGRAM)
			conn.sockaddr4 = sys::SockaddrInet4{}
			conn.sockaddr4.Family = sys::AF_INET
			conn.sockaddr4.Port = htons(conn.Addr.Port)
			copy(conn.sockaddr4.Addr[:], ipv4)
		}
	}
	ret new(UDPConn, conn)
}