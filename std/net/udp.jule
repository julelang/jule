// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/conv"

// Represents the address of a UDP end point.
struct UDPAddr {
	IP:   IP
	Port: int
	Zone: str // IPv6 scoped addressing zone.
}

impl Addr for UDPAddr {
	// Returns the address's network name.
	fn Network(*self): str {
		ret "udp"
	}

	// Returns string form of address.
	fn Str(*self): str {
		ip := self.IP.ipEmptyStr()
		if self.Zone != "" {
			ret JoinHostPort(ip+"%"+self.Zone, conv::Itoa(self.Port))
		}
		ret JoinHostPort(ip, conv::Itoa(self.Port))
	}
}

impl UDPAddr {
	// Returns an address of UDP end point.
	// The network must be a UDP network name.
	//
	// See the [Dial] function for a description of the network and addr parameters.
	fn Resolve(mut network: Network, addr: str)!: &UDPAddr {
		match network {
		| UDP | UDP4 | UDP6:
			break
		|:
			error(&AddrError{Err: "invalid network: " + str(network)})
		}
		mut intAddr := resolveInternetAddr(network, addr) else { error(error) }
		if intAddr == nil {
			error(&AddrError{Err: "unable to parse address", Addr: addr})
		}
		ret intAddr.(&UDPAddr)
	}
}