// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use integ "std/jule/integrated"
use "std/sys"

impl Status {
	// Returns a Status describing the path.
	//
	// Possible errors: Denied IO Loop LongPath NotExist NotDir Overflow
	static fn Of(path: str)!: &Status {
		if path == "" {
			error(FsError.NotExist)
		}
		utf16Path := integ::UTF16FromStr(path)
		mut handle := sys::SysStat{}
		code := unsafe { sys::Wstat(&utf16Path[0], &handle) }
		if code == -1 {
			error(getLastFsError())
		}
		mut stat := new(Status)
		stat.size = handle.st_size
		if handle.st_mode&sys::S_IFDIR == sys::S_IFDIR {
			stat.mode |= statusMode.Dir
		} else if handle.st_mode&sys::S_IFREG == sys::S_IFREG {
			stat.mode |= statusMode.Reg
		}
		ret stat
	}
}