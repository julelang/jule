// Copyright 2023 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use path for std::fs::path
use integrated for std::jule::integrated
use sys for std::sys

impl Directory {
    // Reads the named directory and returs all its directory entries can read.
    // Returns nil if no any directory entry or error occurs.
    //
    // Possible errors:
    //  Denied InvalidDescriptor PerProcessLimit SystemWideLimit
    //  NotExist InsufficientMemory NotDir
    pub static fn read(path: str): (dirents: []&DirEntry, err: FsError) {
        let utf16_path = integrated::utf16_from_str(path)
        let mut dir = unsafe { sys::_wopendir(&utf16_path[0]) }
        if dir == nil {
            err = get_last_fs_error()
            ret
        }
        for {
            let dirent = unsafe { sys::_wreaddir(dir) }
            if dirent == nil {
                break
            }
            let name = unsafe { integrated::u16_ptr_to_str((*u16)(dirent.d_name)) }
            let (mut stat, serr) = Status.of(path::join(path, name))
            if serr != FsError.Ok {
                err = serr
                dirents = nil
                break
            }
            dirents = append(dirents, &DirEntry{
                name: name,
                stat: stat,
            })
        }
        _ = unsafe { sys::_wclosedir(dir) }
        ret
    }

    // Creates directory.
    //
    // Possible errors: Denied Exist ReadOnly NoSpace
    pub static fn create(path: str): FsError {
        // NOTICE
        // Also can has EMLINK error-code.
        // Make sure this situation should documented or not.

        let utf16_path = integrated::utf16_from_str(path)
        if (unsafe { std::sys::create_directory(&utf16_path[0]) }) {
            ret FsError.Ok
        }
        ret get_last_fs_error()
    }

    // Removes empty directory.
    //
    // Possible errors: Denined NotExist NotEmpty SyncIO IO Loop NotDir
    pub static fn remove(path: str): FsError {
        let utf16_path = integrated::utf16_from_str(path)
        if (unsafe { std::sys::remove_directory(&utf16_path[0]) }) {
            ret FsError.Ok
        }
        ret get_last_fs_error()
    }
}
