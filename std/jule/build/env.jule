// Copyright 2023 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/mod"
use "std/os"
use "std/os/filepath"
use "std/runtime"

// Target operating system.
// Set to runtime operating system by default.
let mut OS = runtime::OS

// Target architecture.
// Set to runtime architecture by default.
let mut Arch = runtime::Arch

// Environment variables.
let mut pathStdlib = ""
let mut pathExec = ""
let mut pathWd = ""
let mut pathAPI = ""
let mut modStdlib: &mod::Mod = nil

// Returns path of standard library.
// Returns empty string if not initialized by [SetEnv].
fn PathStdlib(): str { ret pathStdlib }

// Returns path of compiler's executable file.
// Returns empty string if not initialized by [SetEnv].
fn PathExec(): str { ret pathExec }

// Returns path of working directory.
// Returns empty string if not initialized by [SetEnv].
fn PathWd(): str { ret pathWd }

// Returns path of main API header file.
// Returns empty string if not initialized by [SetEnv].
fn PathAPI(): str { ret pathAPI }

// Sets the environment variables of the compiler.
// The exec should hold the path of the compiler's executable path.
// The wd should hold the path of working directory.
// SetEnv panics is exec or wd is empty and will not check if paths are exist and
// appropriate for compiler. Therefore, any misinformation for environment variables
// may cause analysis issues.
//
// SetEnv is a mandatory call if you need to use package sema.
// Because semantic analysis and all relevant behavior relies to environment
// variables. Therefore, there might be analysis issues if environment variables
// will not be initialized before.
fn SetEnv(exec: str, wd: str) {
	if exec == "" {
		panic("SetEnv: empty executable file path")
	}
	if wd == "" {
		panic("SetEnv: empty working directory")
	}

	pathWd = wd
	pathExec = exec

	bin := filepath::Dir(exec)
	pathStdlib = filepath::Join(bin, "..", Stdlib)
	pathAPI = filepath::Join(bin, "..", "api", "jule.hpp")

	if modStdlib == nil {
		modStdlib = new(mod::Mod)
		modStdlib.ID = ModStdlibID
		modStdlib.Name = "std"
	}
	modStdlib.Path = pathStdlib
}

// Reserved module identity for the standard library module.
const ModStdlibID: mod::ID = 0

// Returns the reserved module instance of the standard library module.
// Returns nil if not initialized by [SetEnv].
fn ModStdlib(): &mod::Mod {
	ret modStdlib
}