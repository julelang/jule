// Copyright 2023 The Jule Authors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/conv"
use "std/jule/build"
use "std/strings"

type bitChecker = fn(v: str, base: int, bit: int): bool

// Bit-size of target architecture.
// Possible values are: 32, and 64.
// Initialized using build::Arch by the package when imported.
let BitSize = 0

// Signed integer kind of target architecture.
// Is equivalent to "int", but specific bit-sized integer kind.
// Initialized using build::Arch by the package when imported.
let SysInt = ""

// Unsigned integer kind of target architecture.
// Is equivalent to "uint" and "uintptr", but specific bit-sized integer kind.
// Initialized using build::Arch by the package when imported.
let SysUint = ""

fn checkBit(v: str, bit: int, checker: bitChecker): bool {
	match {
	| v == "":
		ret false
	| len(v) == 1:
		ret true
	| strings::HasPrefix(v, "0x"):
		ret checker(v[2:], 16, bit) // Hexadecimal
	| strings::HasPrefix(v, "0b"):
		ret checker(v[2:], 2, bit) // Binary
	| strings::HasPrefix(v, "0o"):
		ret checker(v[2:], 8, bit) // Octal
	| v[0] == '0':
		ret checker(v[1:], 8, bit) // Octal
	|:
		ret checker(v, 10, bit) // Decimal
	}
}

// Returns kind's bit-specific kind if bit-specific like int, uint, and uintptr.
// Returns kind if not bit-specific.
// Bit-size is determined by runtime.
fn RealKindOf(kind: str): str {
	match kind {
	| Int:
		ret SysInt
	| Uint
	| Uintptr:
		ret SysUint
	|:
		ret kind
	}
}

// Returns kind's bit-size.
// Returns -1 if kind is not numeric.
fn BitSizeOf(k: str): int {
	match k {
	| I8 | U8:
		ret 8
	| I16 | U16:
		ret 16
	| I32 | U32 | F32:
		ret 32
	| I64 | U64 | F64 | Cmplx64:
		ret 64
	| Cmplx128:
		ret 128
	| Uint | Int | Uintptr:
		ret BitSize
	|:
		ret -1
	}
}

// Returns signed integer kind by bit-size.
// Possible bit-sizes are: 8, 16, 32, and 64.
// Returns empty string if bits is invalid.
fn IntFromBits(bits: int): str {
	match bits {
	| 8:
		ret I8
	| 16:
		ret I16
	| 32:
		ret I32
	| 64:
		ret I64
	|:
		panic("unreachable")
	}
}

// Returns unsigned integer kind by bit-size.
// Possible bit-sizes are: 8, 16, 32, and 64.
// Panics if bits is invalid.
fn UintFromBits(bits: int): str {
	match bits {
	| 8:
		ret U8
	| 16:
		ret U16
	| 32:
		ret U32
	| 64:
		ret U64
	|:
		panic("unreachable")
	}
}

// Returns floating-point kind by bit-size.
// Possible bit-sizes are: 32, and 64.
// Panics if bits is invalid.
fn FloatFromBits(bits: int): str {
	match bits {
	| 32:
		ret F32
	| 64:
		ret F64
	|:
		panic("unreachable")
	}
}

// Reports whether signed integer literal is compatible given bit-size.
fn CheckBitInt(v: str, bit: int): bool {
	ret checkBit(v, bit, fn|v, base, bit| {
		_ = conv::ParseInt(v, base, bit) else { ret false }
		ret true
	})
}

// Reports whether unsigned integer literal is compatible given bit-size.
fn CheckBitUint(v: str, bit: int): bool {
	ret checkBit(v, bit, fn|v, base, bit| {
		_ = conv::ParseUint(v, base, bit) else { ret false }
		ret true
	})
}

// Reports whether float literal is compatible given bit-size.
fn CheckBitFloat(val: str, bit: int): bool {
	_ = conv::ParseFloat(val, bit) else { ret false }
	ret true
}

// Reports minimum bit-size of given floating-point.
//
// Possible values are:
//	- 32 for 32-bit
//	- 64 for 64-bit
fn BitSizeOfFloat(x: f64): int {
	match {
	| MinF32 <= x && x <= MaxF32:
		ret 32
	| MinF64 <= x && x <= MaxF64:
		ret 64
	|:
		panic("unreachable")
	}
}

// Reports minimum bit-size of given signed integer.
//
// Possible values are:
//	- 8 for 8-bit
//	- 16 for 16-bit
//	- 32 for 32-bit
//	- 64 for 64-bit
fn BitSizeOfInt(x: i64): int {
	match {
	| MinI8 <= x && x <= MaxI8:
		ret 8
	| MinI16 <= x && x <= MaxI16:
		ret 16
	| MinI32 <= x && x <= MaxI32:
		ret 32
	| MinI64 <= x && x <= MaxI64:
		ret 64
	|:
		panic("unreachable")
	}
}

// Reports minimum bit-size of given unsigned integer.
//
// Possible values are:
//	- 8 for 8-bit
//	- 16 for 16-bit
//	- 32 for 32-bit
//	- 64 for 64-bit
fn BitSizeOfUint(x: u64): int {
	match {
	| x <= MaxU8:
		ret 8
	| x <= MaxU16:
		ret 16
	| x <= MaxU32:
		ret 32
	| x <= MaxU64:
		ret 64
	|:
		panic("unreachable")
	}
}

// Updates platform-specific information based on the target.
// If you will update target configuration, you should call this function.
// In other words, new configurations is not applied for types.
fn UpdateTarget() {
	unsafe {
		match build::Arch {
		| "arm64" | "amd64":
			*(&BitSize) = 64
			*(&SysInt) = I64
			*(&SysUint) = U64
		| "i386":
			*(&BitSize) = 32
			*(&SysInt) = I32
			*(&SysUint) = U32
		|:
			panic("unreachable")
		}
	}
}

fn init() {
	UpdateTarget()
}