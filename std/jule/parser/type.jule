// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/build"
use "std/jule/log"
use "std/jule/token"

fn buildVoidType(): &ast::Type {
	ret new(ast::Type)
}

fn buildPrimType(mut t: &token::Token): &ast::Type {
	ret &ast::Type{
		Token: t,
		Kind: &ast::IdentType{
			Token: t,
			Ident: t.Kind,
		},
	}
}

struct typeBuilder {
	p:      &parser
	tokens: []&token::Token
	i:      *int
	err:    bool
}

impl typeBuilder {
	fn pushErr(mut self, token: &token::Token, fmt: str) {
		if self.err {
			self.p.pushErr(token, fmt)
		}
	}

	// Push suggestion to last log.
	fn pushSuggestion(mut self, fmt: str, args: ...any) {
		if self.err {
			self.p.pushSuggestion(fmt, args...)
		}
	}

	unsafe fn buildNamespace(mut self): &ast::Type {
		mut t := &ast::Type{
			Token: self.tokens[*self.i],
		}
		mut ns := new(ast::NamespaceType)
		ns.Namespace = self.tokens[*self.i]
		if ns.Namespace.Id != token::Id.Ident && ns.Namespace.Id != token::Id.Unsafe {
			self.pushErr(ns.Namespace, log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedIdentifier)
		}
		*self.i++
		if len(self.tokens) <= *self.i {
			self.pushErr(ns.Namespace, log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedDblColon)
			ret nil
		}
		if self.tokens[*self.i].Id != token::Id.DblColon {
			self.pushErr(self.tokens[*self.i], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedDblColon)
			ret nil
		}
		*self.i++
		if len(self.tokens) <= *self.i {
			self.pushErr(self.tokens[*self.i-1], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedIdentifier)
			ret nil
		}
		ns.Kind = self.buildIdent()
		t.Kind = ns
		ret t
	}

	unsafe fn buildGenerics(mut self): []&ast::Type {
		if *self.i >= len(self.tokens) {
			ret nil
		}
		token := self.tokens[*self.i]
		if token.Id != token::Id.LBracket {
			ret nil
		}

		mut parts := self.identGenerics()
		if len(parts) == 0 {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}

		mut types := make([]&ast::Type, 0, len(parts))
		for (_, mut part) in parts {
			mut j := 0
			mut t, _ := unsafe { self.p.buildType(part, &j, true) }
			if j < len(part) {
				self.pushErr(part[j], log::InvalidSyntax)
			}
			types = append(types, t)
		}
		ret types
	}

	unsafe fn identGenerics(mut self): [][]&token::Token {
		first := *self.i
		mut rangeN := 0
		for *self.i < len(self.tokens); *self.i++ {
			token := self.tokens[*self.i]
			match token.Id {
			| token::Id.LBracket:
				rangeN++
			| token::Id.RBracket:
				rangeN--
			}
			if rangeN == 0 {
				*self.i++ // Skip right bracket
				break
			}
		}
		mut tokens := self.tokens[first+1 : *self.i-1] // Take range of brackets.
		mut parts, errors := parts(tokens, token::Id.Comma, true)
		if self.err {
			self.p.errors = append(self.p.errors, errors...)
		}
		ret parts
	}

	unsafe fn buildIdent(mut self): &ast::Type {
		mut next := *self.i+1 < len(self.tokens)
		if next && self.tokens[*self.i+1].Id == token::Id.DblColon {
			ret self.buildNamespace()
		}
		mut token := self.tokens[*self.i]
		mut it := &ast::IdentType{
			Token: token,
			Ident: token.Kind,
			Binded: false,
		}
		*self.i++
		it.Generics = self.buildGenerics()
		ret &ast::Type{
			Token: token,
			Kind: it,
		}
	}

	unsafe fn buildCppLink(mut self): &ast::Type {
		if *self.i+1 >= len(self.tokens) || self.tokens[*self.i+1].Id != token::Id.Dot {
			self.pushErr(self.tokens[*self.i], log::InvalidSyntax)
			ret nil
		}
		*self.i += 2 // Skip the bind keyword and dot token.
		mut t := self.buildIdent()
		t.Kind.(&ast::IdentType).Binded = true
		ret t
	}

	unsafe fn buildFunc(mut self): &ast::Type {
		mut token := self.tokens[*self.i]
		mut f := self.p.buildFuncPrototype(self.tokens, *self.i, false)
		if f == nil {
			ret nil
		}
		if !f.IsAnon() {
			self.pushErr(f.Token, log::InvalidSyntax)
		}
		ret &ast::Type{
			Token: token,
			Kind: f,
		}
	}

	unsafe fn buildPtr(mut self): &ast::Type {
		mut token := self.tokens[*self.i]
		if *self.i+1 >= len(self.tokens) {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}

		*self.i++
		if self.tokens[*self.i].Id == token::Id.Unsafe {
			*self.i++
			ret &ast::Type{
				Token: token,
				Kind: &ast::PtrType{
					Elem: nil, // Set Elem as nil for unsafe pointer (*unsafe) type.
				},
			}
		}

		mut elem := self.step()
		if elem == nil {
			ret nil
		}

		ret &ast::Type{
			Token: token,
			Kind: &ast::PtrType{
				Elem: elem,
			},
		}
	}

	unsafe fn buildSptr(mut self): &ast::Type {
		mut token := self.tokens[*self.i]
		if *self.i+1 >= len(self.tokens) {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}

		*self.i++
		mut elem := self.step()
		if elem == nil {
			ret nil
		}

		ret &ast::Type{
			Token: token,
			Kind: &ast::SptrType{
				Elem: elem,
			},
		}
	}

	unsafe fn buildSlice(mut self): &ast::Type {
		mut token := self.tokens[*self.i]
		*self.i++ // skip right bracket
		mut elem := self.step()
		if elem == nil {
			ret nil
		}
		ret &ast::Type{
			Token: token,
			Kind: &ast::SliceType{
				Elem: elem,
			},
		}
	}

	unsafe fn buildArr(mut self): &ast::Type {
		mut exprTokens := range(*self.i, token::Id.LBracket, token::Id.RBracket, self.tokens)
		if *self.i >= len(self.tokens) {
			self.pushErr(self.tokens[*self.i-1], log::MissingType)
			ret nil
		}

		mut elem := self.step()
		if elem == nil {
			ret nil
		}

		mut arrt := &ast::ArrayType{
			Elem: elem,
		}

		mut token := exprTokens[0]
		if len(exprTokens) == 1 && token.Id == token::Id.TripleDot {
			// Ignore.
		} else {
			arrt.Size = self.p.buildExpr(exprTokens)
		}

		ret &ast::Type{
			Token: token,
			Kind: arrt,
		}
	}

	unsafe fn buildMap(mut self): &ast::Type {
		mut mapToken := self.tokens[*self.i]
		*self.i++ // Skip map token.
		if *self.i >= len(self.tokens) {
			self.pushErr(mapToken, log::MissingType)
			ret nil
		}

		// Get key type tokens without brackets.
		mut keyTokens := range(*self.i, token::Id.LBracket, token::Id.RBracket, self.tokens)
		if *self.i >= len(self.tokens) {
			self.pushErr(self.tokens[*self.i-1], log::MissingType)
			ret nil
		} else if len(keyTokens) == 0 {
			self.pushErr(mapToken, log::MissingType)
			ret nil
		}

		mut mapt := new(ast::MapType)
		mut j := 0
		mut keyt, mut ok := self.p.buildType(keyTokens, &j, self.err)
		if !ok {
			ret nil
		} else if j < len(keyTokens) {
			self.pushErr(keyTokens[j], log::InvalidSyntax)
		}
		mapt.Key = keyt

		mut valt, ok := self.p.buildType(self.tokens, self.i, self.err)
		if !ok {
			ret nil
		}
		mapt.Val = valt
		ret &ast::Type{
			Token: mapToken,
			Kind: mapt,
		}
	}

	unsafe fn buildEnumerable(mut self): &ast::Type {
		mut token := self.tokens[*self.i]
		if *self.i+2 >= len(self.tokens) {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}
		*self.i++
		token = self.tokens[*self.i]
		if token.Id == token::Id.RBracket {
			ret self.buildSlice()
		}
		*self.i-- // Point to left bracket.
		ret self.buildArr()
	}

	unsafe fn buildChan(mut self): &ast::Type {
		mut token := self.tokens[*self.i]
		if *self.i+1 >= len(self.tokens) {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}
		*self.i++

		mut recv := true
		mut send := true

		if token.Id == token::Id.RArrow {
			send = false
			if *self.i+1 >= len(self.tokens) {
				self.pushErr(token, log::InvalidSyntax)
				ret nil
			}
			token = self.tokens[*self.i]
			*self.i++
		}

		if token.Id != token::Id.Chan {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}

		// Check send-only probability if channel is not recv-only.
		// Otherwise, accept arrow for element type if exist.
		if send && self.tokens[*self.i].Id == token::Id.RArrow {
			recv = false
			if *self.i+1 >= len(self.tokens) {
				self.pushErr(self.tokens[*self.i], log::InvalidSyntax)
				ret nil
			}
			*self.i++
		}

		mut elem := (&ast::Type)(nil)
		if send && recv && self.tokens[*self.i].Id == token::Id.LParent {
			elem = self.buildParenthesesRange()
		} else {
			elem = self.step()
		}
		if elem == nil {
			ret nil
		}

		ret &ast::Type{
			Token: token,
			Kind: &ast::ChanType{
				Recv: recv,
				Send: send,
				Elem: elem,
			},
		}
	}

	unsafe fn buildParenthesesRange(mut self): &ast::Type {
		token := self.tokens[*self.i] // Save token for error log.
		mut rangeTokens := range(*self.i, token::Id.LParent, token::Id.RParent, self.tokens)
		if len(rangeTokens) == 0 {
			// Empty range.
			self.pushErr(token, log::MissingType)
			ret nil
		}
		oldOffset := *self.i
		mut oldTokens := self.tokens
		self.tokens = rangeTokens
		*self.i = 0
		mut t := self.step()
		*self.i = oldOffset
		self.tokens = oldTokens
		ret t
	}

	unsafe fn step(mut self): &ast::Type {
		mut token := self.tokens[*self.i]
		match token.Id {
		| token::Id.Ident:
			ret self.buildIdent()
		| token::Id.Cpp:
			ret self.buildCppLink()
		| token::Id.Chan | token::Id.RArrow:
			ret self.buildChan()
		| token::Id.Unsafe:
			if len(self.tokens)-*self.i > 1 {
				match self.tokens[*self.i+1].Id {
				| token::Id.Fn:
					// unsafe function
					*self.i++ // point to function keyword
					mut f := self.buildFunc()
					if f != nil {
						f.Kind.(&ast::Func).Unsafety = true
					}
					ret f
				| token::Id.DblColon:
					// unsafe namespace
					ret self.buildNamespace()
				}
			}
		| token::Id.Fn:
			ret self.buildFunc()
		| token::Id.Star:
			ret self.buildPtr()
		| token::Id.Amper:
			ret self.buildSptr()
		| token::Id.DblAmper:
			ret &ast::Type{
				Token: token,
				Kind: &ast::SptrType{
					Elem: self.buildSptr(),
				},
			}
		| token::Id.LBracket:
			ret self.buildEnumerable()
		| token::Id.Map:
			ret self.buildMap()
		}
		*self.i++
		self.pushErr(token, log::InvalidSyntax)
		ret nil
	}

	// Builds type.
	// Returns void if error occurs.
	unsafe fn build(mut self): (&ast::Type, ok: bool) {
		mut root := self.step()
		if root == nil {
			ret buildVoidType(), false
		}
		ret root, true
	}
}