// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/build"
use "std/jule/log"
use "std/jule/token"
use "std/strings"

struct exprBuilder {
	p: &parser
}

impl exprBuilder {
	fn pushErr(mut self, token: &token::Token, fmt: str, args: ...any) {
		self.p.pushErr(token, fmt, args...)
	}

	// Push suggestion to last log.
	fn pushSuggestion(mut self, fmt: str, args: ...any) {
		self.p.pushSuggestion(fmt, args...)
	}

	fn buildTuple(mut self, mut &parts: [][]&token::Token): &ast::TupleExpr {
		mut tuple := &ast::TupleExpr{
			X: make([]&ast::Expr, 0, len(parts)),
		}
		for (_, mut part) in parts {
			tuple.X = append(tuple.X, self.buildFromTokens(part))
		}
		ret tuple
	}

	fn buildLit(self, mut token: &token::Token): &ast::LitExpr {
		ret &ast::LitExpr{
			Token: token,
			Value: token.Kind,
		}
	}

	fn buildSingle(mut self, mut token: &token::Token): ast::ExprData {
		match token.Id {
		| token::Lit:
			ret self.buildLit(token)
		| token::TripleDot:
			ret &ast::VariadicExpr{
				Token: token,
			}
		| token::Name
		| token::Self
		| token::Error:
			ret buildIdentExpr(token)
		}
		self.pushErr(token, log::InvalidSyntax)
		ret nil
	}

	fn buildBindName(mut self, mut &tokens: []&token::Token): &ast::NameExpr {
		if tokens[0].Id != token::Cpp {
			ret nil
		} else if tokens[1].Id != token::Dot {
			self.pushErr(tokens[1], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedDotForBind)
			ret nil
		}
		mut token := tokens[2]

		// Save parser mode and check whether token is identifier.
		// Allow keywords for checking, then restore the mode immediately.
		mode := self.p.mode
		self.p.mode |= allowKeywordIdentifiers
		isIdent := self.p.isIdent(token)
		self.p.mode = mode

		if !isIdent {
			self.pushErr(tokens[2], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedIdentifier)
			ret nil
		}

		mut expr := buildIdentExpr(token)
		expr.Bind = true
		ret expr
	}

	fn buildUnary(mut self, mut tokens: []&token::Token): &ast::UnaryExpr {
		mut op := tokens[0]
		if len(tokens) == 1 {
			self.pushErr(op, log::MissingExprForUnary)
			ret nil
		} else if !token::IsUnaryOp(op.Id) {
			self.pushErr(op, log::InvalidOpForUnary, op.Kind)
			ret nil
		}

		// Length is 1 cause all length of operator tokens is 1.
		// Change "1" with length of token's value
		// if all operators length is not 1.
		tokens = tokens[1:]

		ret &ast::UnaryExpr{
			Op: op,
			X: self.buildFromTokens(tokens),
		}
	}

	fn buildObjSubIdent(mut self, mut tokens: []&token::Token): &ast::SelectorExpr {
		mut i := len(tokens) - 1
		mut nameToken := tokens[i]
		i--                 // Set offset to delimiter token.
		tokens = tokens[:i] // Remove dot token and selected identifier token.
		if len(tokens) == 0 {
			self.pushErr(nameToken, log::InvalidSyntax)
			ret nil
		}
		ret &ast::SelectorExpr{
			Name: nameToken,
			X: self.buildFromTokens(tokens),
		}
	}

	fn buildNamespace(mut self, mut &tokens: []&token::Token): &ast::NamespaceExpr {
		mut ns := new(ast::NamespaceExpr)
		if len(tokens) > 3 {
			self.pushErr(tokens[3], log::InvalidSyntax)
		}
		ns.Namespace = tokens[0]
		if ns.Namespace.Id != token::Name && ns.Namespace.Id != token::Unsafe {
			self.pushErr(ns.Namespace, log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedIdentifier)
		}
		if len(tokens) < 2 {
			self.pushErr(ns.Namespace, log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedDblColon)
			ret nil
		}
		if tokens[1].Id != token::DblColon {
			self.pushErr(tokens[1], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedDblColon)
			ret nil
		}
		if len(tokens) < 3 {
			self.pushErr(tokens[1], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedIdentifier)
			ret nil
		}
		ns.Name = tokens[2]
		if ns.Name.Id != token::Name {
			self.pushErr(ns.Name, log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedIdentifier)
			ret nil
		}
		ret ns
	}

	fn buildType(mut self, mut &tokens: []&token::Token): ast::ExprData {
		mut i := 0
		mut typ, ok := unsafe { self.p.buildType(tokens, &i, true) }
		if !ok {
			ret nil
		}
		if i < len(tokens) {
			self.pushErr(tokens[i], log::InvalidSyntax)
		}
		ret typ.Data
	}

	fn buildSubIdent(mut self, mut &tokens: []&token::Token): ast::ExprData {
		i := len(tokens) - 2 // Set offset to delimiter token.
		token := tokens[i]
		match token.Id {
		| token::Dot:
			ret self.buildObjSubIdent(tokens)
		| token::DblColon:
			ret self.buildNamespace(tokens)
		| token::RBracket:
			// Catch slice, array, and map types.
			ret self.buildType(tokens)
		}

		// Catch anonymous functions.
		if isAnonFuncHead(tokens) {
			ret self.buildType(tokens)
		}

		self.pushErr(token, log::InvalidSyntax)
		ret nil
	}

	fn buildVariadic(mut self, mut tokens: []&token::Token): &ast::VariadicExpr {
		mut token := tokens[len(tokens)-1] // Variadic operator token.
		tokens = tokens[:len(tokens)-1]    // Remove variadic operator token.
		ret &ast::VariadicExpr{
			Token: token,
			X: self.buildFromTokens(tokens),
		}
	}

	fn buildBetweenParentheses(mut self, mut tokens: []&token::Token): &ast::RangeExpr {
		if len(tokens) == 2 {
			self.pushErr(tokens[0], log::MissingExpr)
			self.pushSuggestion(log::EmptyParentNotValid)
			ret nil
		}
		tokens = tokens[1 : len(tokens)-1] // Remove parentheses.
		ret &ast::RangeExpr{
			X: self.buildFromTokens(tokens),
		}
	}

	fn tryBuildCast(mut self, mut &tokens: []&token::Token): &ast::CastExpr {
		mut rangeN := 0
		for i, token in tokens {
			match token.Id {
			| token::LBrace
			| token::LBracket
			| token::LParent:
				rangeN++
				continue
			| token::RBrace
			| token::RBracket
			| token::RParent:
				rangeN--
			}

			if rangeN > 0 {
				continue
			} else if i+1 == len(tokens) {
				ret nil
			}

			mut typeTokens := tokens[:i+1]
			mut exprTokens := tokens[i+1:]

			if len(exprTokens) == 0 {
				// Expression is parentheses group.
				ret nil
			}

			tok := exprTokens[0]
			if tok.Id != token::LParent {
				ret nil
			}

			mut cast := &ast::CastExpr{}

			// Expression tokens just parentheses.
			if len(exprTokens) == 2 {
				self.pushErr(exprTokens[0], log::MissingExpr)
				self.pushSuggestion(log::GiveExprToCast)
			}

			// Type tokens just parentheses.
			if len(typeTokens) == 2 {
				self.pushErr(typeTokens[0], log::MissingType)
				self.pushSuggestion(log::GiveTypeForCast)
			} else {
				typeTokens = typeTokens[1 : len(typeTokens)-1] // Remove parentheses.
				mut typeIndex := 0
				mut typ, ok := unsafe { self.p.buildType(typeTokens, &typeIndex, true) }
				if ok && typeIndex < len(typeTokens) {
					self.pushErr(typeTokens[typeIndex], log::InvalidSyntax)
				} else if !ok {
					ret cast
				}
				cast.Type = typ

				if len(exprTokens) > 2 {
					// Remove parentheses.
					mut j := 0
					mut exprTokens2 := range(j, token::LParent, token::RParent, exprTokens)
					if len(exprTokens2) == 0 || j < len(exprTokens) {
						ret nil
					}
					cast.X = self.buildFromTokens(exprTokens2)
				}
			}
			ret cast
		}

		ret nil
	}

	fn buildArgs(mut self, mut tokens: []&token::Token): []&ast::Expr {
		// No argument.
		if len(tokens) < 2 {
			ret nil
		}
		tokens = tokens[1 : len(tokens)-1] // Remove parentheses.
		mut parts, errs := parts(tokens, token::Comma, true)
		self.p.errors = append(self.p.errors, errs...)
		mut args := make([]&ast::Expr, 0, len(parts))
		for (_, mut part) in parts {
			if len(part) > 0 {
				args = append(args, self.buildFromTokens(part))
			}
		}
		ret args
	}

	fn buildCall(mut self, mut &token: &token::Token, mut &expr: []&token::Token, mut &args: []&token::Token): &ast::CallExpr {
		ret &ast::CallExpr{
			Token: token,
			Func: self.buildFromTokens(expr),
			Args: self.buildArgs(args),
		}
	}

	fn buildTypeAssertion(mut self, mut &exprTokens: []&token::Token, mut &typeTokens: []&token::Token): &ast::TypeAssertionExpr {
		exprTokens = exprTokens[:len(exprTokens)-1] // Remove trailing dot.
		mut x := self.buildFromTokens(exprTokens)
		if x == nil {
			ret nil
		}
		typeTokens = typeTokens[1 : len(typeTokens)-1] // Remove parentheses.
		mut typ := self.buildType(typeTokens)
		if typ == nil {
			ret nil
		}
		ret &ast::TypeAssertionExpr{
			X: x,
			Type: &ast::Expr{
				Token: typeTokens[0],
				End: typeTokens[len(typeTokens)-1],
				Data: typ,
			},
		}
	}

	fn buildParenthesesRange(mut self, mut &tokens: []&token::Token): ast::ExprData {
		mut token := tokens[0]
		if token.Id == token::LParent {
			mut expr := self.tryBuildCast(tokens)
			if expr != nil {
				ret expr
			}
		}
		mut exprTokens, mut argsTokens := rangeLast(tokens)
		// Expression is parentheses group if exprTokens is zero.
		// argsTokens holds tokens of parentheses range (include parentheses).
		if len(exprTokens) == 0 {
			ret self.buildBetweenParentheses(argsTokens)
		}
		// Catch type assertions.
		if exprTokens[len(exprTokens)-1].Id == token::Dot {
			ret self.buildTypeAssertion(exprTokens, argsTokens)
		}
		ret self.buildCall(token, exprTokens, argsTokens)
	}

	fn buildUnsafeExpr(mut self, mut tokens: []&token::Token): &ast::UnsafeExpr {
		mut token := tokens[0]
		tokens = tokens[1:] // Remove unsafe keyword.
		mut i := 0
		mut rangeTokens := range(i, token::LBrace, token::RBrace, tokens)
		if len(rangeTokens) == 0 {
			self.pushErr(tokens[0], log::MissingExpr)
			ret nil
		}
		ret &ast::UnsafeExpr{
			Token: token,
			X: self.buildFromTokens(rangeTokens),
		}
	}

	fn buildAnonFunc(mut self, mut &tokens: []&token::Token): &ast::Func {
		mut f := self.p.buildFunc(tokens, false, false)
		if f != nil && !f.IsAnon() {
			self.pushErr(f.Token, log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedAnonFunc)
		}
		ret f
	}

	fn buildUnsafe(mut self, mut &tokens: []&token::Token): ast::ExprData {
		match tokens[1].Id {
		| token::Fn:
			// Unsafe anonymous function.
			ret self.buildAnonFunc(tokens)
		|:
			ret self.buildUnsafeExpr(tokens)
		}
	}

	fn pushRangeLitPart(mut self, mut part: []&token::Token,
		errorToken: &token::Token, mut &parts: [][]&token::Token) {
		if len(part) == 0 {
			self.pushErr(errorToken, log::MissingExpr)
			self.pushSuggestion(log::ExpectedExpr)
			ret
		}
		parts = append(parts, part)
	}

	// Tokens should include brace tokens.
	fn getBraceRangeLitExprParts(mut self, mut tokens: []&token::Token): [][]&token::Token {
		// No part.
		if len(tokens) < 2 {
			ret nil
		}

		let mut parts: [][]&token::Token = nil

		mut last := 0
		mut rangeN := 0
		tokens = tokens[1 : len(tokens)-1] // Remove parentheses.
		for i, token in tokens {
			match token.Id {
			| token::LBrace
			| token::LBracket
			| token::LParent:
				rangeN++
			| token::RBrace
			| token::RBracket
			| token::RParent:
				rangeN--
			}
			if rangeN > 0 || token.Id != token::Comma {
				continue
			}
			self.pushRangeLitPart(tokens[last:i], token, parts)
			last = i + 1
		}

		if last < len(tokens) {
			if last == 0 {
				if len(tokens) > 0 {
					self.pushRangeLitPart(tokens[last:], tokens[last], parts)
				}
			} else {
				self.pushRangeLitPart(tokens[last:], tokens[last-1], parts)
			}
		}

		ret parts
	}

	fn buildTypedStructLiteral(mut self, mut tokens: []&token::Token): &ast::TypedBraceLit {
		mut i := 0
		mut t, ok := unsafe { self.p.buildType(tokens, &i, true) }
		if !ok {
			ret nil
		} else if i >= len(tokens) {
			self.pushErr(tokens[i], log::InvalidSyntax)
			ret nil
		}

		tokens = tokens[i:] // Remove type tokens.
		token := tokens[0]
		if token.Id != token::LBrace {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}
		ret &ast::TypedBraceLit{
			Type: t,
			Lit: self.buildBraceLit(tokens),
		}
	}

	fn buildBraceLitPart(mut self, mut &tokens: []&token::Token): &ast::Expr {
		mut l, mut r := splitDelim(tokens, token::Colon)
		// If left is not nil, colon token found.
		if l != nil {
			if len(r) == 0 {
				self.pushErr(tokens[len(l)], log::MissingExpr)
				self.pushSuggestion(log::ExpectedExpr)
				ret nil
			}
			ret &ast::Expr{
				Token: tokens[0],
				End: tokens[len(tokens)-1],
				Data: &ast::KeyValueExpr{
					Colon: tokens[len(l)],
					Key: self.buildFromTokens(l),
					Value: self.buildFromTokens(r),
				},
			}
		}
		ret self.buildFromTokens(tokens)
	}

	fn buildBraceLit(mut self, mut &tokens: []&token::Token): &ast::BraceLit {
		mut lit := &ast::BraceLit{
			Token: tokens[0],
			End: tokens[len(tokens)-1],
		}

		mut parts := self.getBraceRangeLitExprParts(tokens)
		if parts == nil {
			ret lit
		}

		lit.X = make([]&ast::Expr, 0, len(parts))
		for (_, mut part) in parts {
			lit.X = append(lit.X, self.buildBraceLitPart(part))
		}

		ret lit
	}

	fn buildBraceRange(mut self, mut &tokens: []&token::Token): ast::ExprData {
		mut exprTokens, rangeN := getRangeExprTokens(tokens)

		match {
		| len(exprTokens) == 0:
			ret self.buildBraceLit(tokens)
		| rangeN > 0:
			self.pushErr(tokens[0], log::InvalidSyntax)
			ret nil
		}

		// Exceptional handling.
		elseToken := exprTokens[len(exprTokens)-1]
		if exprTokens[len(exprTokens)-1].Id == token::Else {
			exprTokens = exprTokens[:len(exprTokens)-1] // Ignore keyword "else"
			mut d := self.build(exprTokens)
			if d == nil {
				ret nil
			}
			mut model, ok := d.(&ast::CallExpr)
			if !ok {
				self.pushErr(exprTokens[0], log::InvalidSyntax)
				ret nil
			}
			tokens = tokens[len(exprTokens)+1:] // Get range: {...}
			mut i := 0
			mut rangeTokens := range(i, token::LBrace, token::RBrace, tokens)
			if model.Ignored() {
				self.pushErr(elseToken, log::InvalidSyntax)
				self.pushSuggestion(log::JustIgnoreOrHandle)
			}
			model.Exception = self.p.buildScope(rangeTokens, tokens[i-1])
			ret d
		}

		match exprTokens[0].Id {
		| token::Unsafe:
			ret self.buildUnsafe(tokens)
		| token::Fn:
			ret self.buildAnonFunc(tokens)
		| token::Map | token::Name | token::Cpp:
			ret self.buildTypedStructLiteral(tokens)
		|:
			self.pushErr(exprTokens[0], log::InvalidSyntax)
			ret nil
		}
	}

	// Tokens is should be store enumerable range tokens.
	fn getEnumerableParts(mut self, mut tokens: []&token::Token): [][]&token::Token {
		tokens = tokens[1 : len(tokens)-1] // Remove range tokens.
		mut parts, errors := parts(tokens, token::Comma, true)
		self.p.errors = append(self.p.errors, errors...)
		ret parts
	}

	fn buildSlice(mut self, mut tokens: []&token::Token): &ast::SliceExpr {
		mut slc := &ast::SliceExpr{
			Token: tokens[0],
			End: tokens[len(tokens)-1],
		}

		mut parts := self.getEnumerableParts(tokens)
		if len(parts) == 0 {
			ret slc
		}

		slc.X = make([]&ast::Expr, 0, len(parts))
		for (_, mut p) in parts {
			if len(p) == 0 {
				continue
			}
			slc.X = append(slc.X, self.buildFromTokens(p))
		}

		ret slc
	}

	fn buildIndex(mut self, mut exprTokens: []&token::Token,
		mut tokens: []&token::Token, mut errorToken: &token::Token): &ast::IndexExpr {
		mut end := tokens[len(tokens)-1]
		tokens = tokens[1 : len(tokens)-1] // Remove brackets.
		if len(tokens) == 0 {
			self.pushErr(errorToken, log::MissingExpr)
			ret nil
		}
		mut expr := self.buildFromTokens(exprTokens)
		if expr == nil {
			ret nil
		}
		mut index := self.buildFromTokens(tokens)
		if index == nil {
			ret nil
		}
		ret &ast::IndexExpr{
			Token: errorToken,
			End: end,
			X: expr,
			Index: index,
		}
	}

	fn buildSlicing(mut self, mut &x: []&token::Token, mut &low: []&token::Token,
		mut high: []&token::Token, mut &errorToken: &token::Token, mut end: &token::Token): &ast::SlicingExpr {
		mut slc := &ast::SlicingExpr{
			Token: errorToken,
			End: end,
			X: self.buildFromTokens(x),
		}
		if len(low) > 0 {
			slc.Low = self.buildFromTokens(low)
		}
		if len(high) > 0 {
			mut high2, mut max := splitDelim(high, token::Colon) // Catch max index.
			if len(high2) > 0 || len(max) > 0 {
				if len(high2) > 0 {
					slc.High = self.buildFromTokens(high2)
				} else {
					panic("unreachable")
				}
				if len(max) > 0 {
					slc.Max = self.buildFromTokens(max)
				} else {
					self.pushErr(errorToken, log::FinalIndexRequired)
				}
			} else {
				slc.High = self.buildFromTokens(high)
			}
		}
		ret slc
	}

	fn buildBracketRange(mut self, mut tokens: []&token::Token): ast::ExprData {
		mut errorToken := tokens[0]
		mut exprTokens, rangeN := getRangeExprTokens(tokens)

		if len(exprTokens) == 0 {
			ret self.buildSlice(tokens)
		} else if rangeN > 0 {
			self.pushErr(errorToken, log::InvalidSyntax)
			ret nil
		}

		// Remove expression tokens.
		// Holds only indexing tokens.
		// Includes brackets.
		mut bracketTokens := tokens[len(exprTokens):]

		// Catch slicing expressions.
		mut splitTokens := bracketTokens[1 : len(bracketTokens)-1] // Remove brackets.
		mut start, mut to := splitDelim(splitTokens, token::Colon)
		if start != nil || to != nil {
			ret self.buildSlicing(exprTokens, start, to, errorToken, bracketTokens[len(bracketTokens)-1])
		}
		if bracketTokens[1].Id == token::DblColon {
			// Special case: catch x[::] and x[::index3] like expressions.
			self.pushErr(errorToken, log::MiddleIndexRequired)
			ret nil
		}
		ret self.buildIndex(exprTokens, bracketTokens, errorToken)
	}

	fn buildExclRight(mut self, mut &tokens: []&token::Token): ast::ExprData {
		token := tokens[len(tokens)-1]
		tokens = tokens[:len(tokens)-1] // Ignore "!" token.
		mut d := self.build(tokens)
		if d == nil {
			ret nil
		}
		mut f, ok := d.(&ast::CallExpr)
		if !ok {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		}
		// Catch already ignored exceptional calls.
		// Like: foo()!!!!
		//         the ^^^ part is unnecessary and invalid
		if f.Ignored() {
			self.pushErr(token, log::InvalidSyntax)
			ret nil
		} else {
			f.Exception = &ast::ScopeTree{
				Deferred: true,
			}
		}
		ret d
	}

	// First token should be "<-" of the tokens.
	fn buildChanRecv(mut self, mut &tokens: []&token::Token): &ast::ChanRecv {
		if len(tokens) == 1 {
			// Expression is just "<-", invalid syntax.
			self.pushErr(tokens[0], log::InvalidSyntax)
			ret nil
		}
		mut x := self.buildFromTokens(tokens[1:])
		if x == nil {
			ret nil
		}
		ret &ast::ChanRecv{
			X: x,
		}
	}

	fn buildData(mut self, mut &tokens: []&token::Token): ast::ExprData {
		match len(tokens) {
		| 0:
			ret nil
		| 1:
			ret self.buildSingle(tokens[0])
		| 3:
			if tokens[0].Id == token::Cpp {
				ret self.buildBindName(tokens)
			}
		}

		mut token := tokens[len(tokens)-1]
		if token.Id == token::TripleDot {
			ret self.buildVariadic(tokens)
		}

		token = tokens[0]
		match token.Id {
		| token::RArrow:
			if len(tokens) > 1 && tokens[1].Id != token::Chan {
				ret self.buildChanRecv(tokens)
			}
			fall
		| token::Chan:
			// Handle channel type declaration.
			ret self.buildType(tokens)
		}

		// Unary operators.
		if token::IsUnaryOp(token.Id) {
			// Handle pointer to primitive type.
			if len(tokens) > 1 {
				token = tokens[1]
				if token.Id == token::Unsafe {
					ret self.buildType(tokens)
				}
			}
			ret self.buildUnary(tokens)
		}

		if len(tokens) >= 3 {
			token = tokens[0]
			match token.Id {
			| token::LParent
			| token::LBrace
			| token::LBracket:
				// Catch type casting.
				if len(tokens) > 3 {
					t := tokens[len(tokens)-1]
					if t.Id == token::RParent {
						break
					}
				}
				if tokens[1].Id == token::RBracket {
					ret self.buildType(tokens)
				}
			}
		}

		token = tokens[len(tokens)-1]
		match token.Id {
		| token::Name:
			ret self.buildSubIdent(tokens)
		| token::Excl:
			ret self.buildExclRight(tokens)
		| token::RParent:
			ret self.buildParenthesesRange(tokens)
		| token::RBrace:
			ret self.buildBraceRange(tokens)
		| token::RBracket:
			ret self.buildBracketRange(tokens)
		}
		self.pushErr(token, log::InvalidSyntax)
		ret nil
	}

	fn buildBinary(mut self, mut &tokens: []&token::Token, i: int): ast::ExprData {
		mut op := tokens[i]
		mut x := tokens[:i]
		if isTypeOp(op.Id) && isTypeRange(x) {
			// Catch slice and array types.
			ret self.buildType(tokens)
		}
		if len(x) == 0 {
			self.pushErr(tokens[i], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedLeftOperand)
			ret nil
		}
		mut y := tokens[i+1:]
		if len(y) == 0 {
			self.pushErr(tokens[i], log::InvalidSyntax)
			self.pushSuggestion(log::ExpectedRightOperand)
			ret nil
		}
		ret &ast::BinaryExpr{
			X: self.buildFromTokens(x),
			Y: self.buildFromTokens(y),
			Op: op,
		}
	}

	fn build(mut self, mut &tokens: []&token::Token): ast::ExprData {
		i := findLowestPrecOp(tokens)
		if i == -1 {
			ret self.buildData(tokens)
		}
		ret self.buildBinary(tokens, i)
	}

	fn buildKind(mut self, mut &tokens: []&token::Token): ast::ExprData {
		if isTupleExpr(tokens) {
			mut parts, errors := parts(tokens, token::Comma, true)
			if errors != nil {
				self.p.errors = append(self.p.errors, errors...)
				ret nil
			} else if len(parts) > 1 {
				ret self.buildTuple(parts)
			}
		}
		ret self.build(tokens)
	}

	fn buildFromTokens(mut self, mut tokens: []&token::Token): &ast::Expr {
		if len(tokens) == 0 {
			ret nil
		}
		mut typ := self.buildKind(tokens)
		if typ == nil {
			ret nil
		}
		ret &ast::Expr{
			Token: tokens[0],
			End: tokens[len(tokens)-1],
			Data: typ,
		}
	}
}

// Reports whether kind is potentially part of a type declaration.
fn isTypeOp(id: int): bool {
	ret id == token::Amper ||
		id == token::DblAmper ||
		id == token::Star ||
		id == token::Chan ||
		id == token::RArrow
}

// Reports whether range is potentially part of a type declaration.
fn isTypeRange(mut &tokens: []&token::Token): bool {
	mut op := false
	for i, token in tokens {
		if token.Id == token::LBracket {
			if op {
				ret true
			}
			mut rangeN := 0
			mut lTokens := tokens[i:]
			for _, lToken in lTokens {
				match lToken.Id {
				| token::LBracket:
					rangeN++
					continue
				| token::RBracket:
					rangeN--
					continue
				}
				if rangeN == 0 {
					ret isTypeOp(lToken.Id)
				}
			}
			ret true
		}
		if !isTypeOp(token.Id) {
			ret false
		}
		op = true
	}
	ret true
}

// Returns expression tokens comes before block if exist, nil if not.
fn getBlockExpr(mut &tokens: []&token::Token): []&token::Token {
	mut braceN := 0
	mut skipToNextBraceClose := 0
	for i, tok in tokens {
		match tok.Id {
		| token::Unsafe:
			if len(tokens)-i > 1 && tokens[i+1].Id == token::LBrace {
				skipToNextBraceClose++
			}
		| token::LBrace:
			if braceN > 0 || skipToNextBraceClose > 0 {
				if skipToNextBraceClose > 0 {
					skipToNextBraceClose--
				}
				braceN++
				break
			}
			ret tokens[:i]
		| token::LBracket
		| token::LParent:
			braceN++
		| token::RBrace
		| token::RBracket
		| token::RParent:
			braceN--
		}
	}
	ret nil
}

// Returns delimiter index, left range and right range tokens.
// Returns nil slice and -1 if not found.
fn splitDelim(mut &tokens: []&token::Token, delim: int): ([]&token::Token, []&token::Token) {
	mut func := -1
	mut rangeN := 0
	mut i := 0
	for i < len(tokens); i++ {
		token := tokens[i]
		match token.Id {
		| token::Fn:
			func = rangeN
		| token::LBrace
		| token::LBracket
		| token::LParent:
			rangeN++
			continue
		| token::RParent:
			rangeN--
			if func == rangeN {
				func = -1
				// Skip colon token if function built.
				if len(tokens)-i > 1 && tokens[i+1].Id == token::Colon {
					i++
					continue
				}
			}
		| token::RBrace
		| token::RBracket:
			rangeN--
		}
		if token.Id == delim && rangeN == 0 {
			mut l := tokens[:i]
			mut r := tokens[i+1:]
			ret l, r
		}
	}
	ret nil, nil
}

// Finds index of priority operator and returns index of operator
// if found, returns -1 if not.
fn findLowestPrecOp(tokens: []&token::Token): int {
	// Set to max byte value, there is nothing for precedence it.
	// It's provides optimization, avoid prec != -1 (if not set) checking.
	// Always greater than actual precedences.
	// So, It's will initialized by first precedence without any checking that if not set.
	const MaxPrec = byte(255)
	mut prec := MaxPrec
	mut precPos := -1
	mut op := true // Ready to read operator.

	mut braceN := 0
	mut mapType := 0

	// Skips all unary operators of tokens by i and increases the i for each token.
	// At the end, i will point to last skipped unary operator, if any.
	// Reports whether any token skipped.
	skipunary := fn(mut &i: int): bool {
		mut inc := false
		for i < len(tokens) {
			if token::IsUnaryOp(tokens[i].Id) {
				i++
				inc = true
			} else {
				if inc {
					i--
				}
				break
			}
		}
		ret inc
	}

	mut i := 0
	// Skip the unary operators of the beginning.
	// If skipped any operator, point to correct start token.
	if skipunary(i) {
		i++
	}
	for i < len(tokens); i++ {
		token := tokens[i]
		match token.Id {
		| token::Map:
			mapType++
		| token::LBrace
		| token::LParent
		| token::LBracket:
			op = true
			braceN++
			skipunary(i) // Skip the unary expressions of the range's beginning.
			continue
		| token::RParent:
			// For the closing parentheses range, it might be a function call.
			// Therefore, we need to handle ! operators.
			// Allow multiple EXCL operator here,
			// error logging should be handled by expression parser.
			i++ // Skip the range closer token.
			for i < len(tokens); i++ {
				if tokens[i].Id != token::Excl {
					break
				}
			}
			i-- // Fallback to the range closer token or last skipped unary operator.
			fall // Fallback to ordinary range closing algorithm.
		| token::RBracket:
			op = mapType == 0
			if mapType > 0 {
				mapType--
			}
			braceN--
			continue
		| token::RBrace:
			op = true
			braceN--
			continue
		}
		match {
		| i == 0:
			continue
		| braceN > 0:
			continue
		| !token::IsBinOp(token.Id):
			op = true
			continue
		| !op:
			continue
		}

		// Skip type annotations.
		left := tokens[i-1]
		if left.Id == token::Colon {
			continue
		}
		if i > 1 && left.Id == token::RBracket {
			lleft := tokens[i-2]
			if lleft.Id == token::LBracket {
				// Skip potential type annotations.
				if token.Id == token::Amper || token.Id == token::Star {
					continue
				}
			}
		}

		op = false
		p := token.Prec()
		if p != 0 && p <= prec {
			prec = p
			precPos = i
		}

		// After binary expression, skip the unary operators of the following operand.
		skipunary(i)
	}
	ret precPos
}

fn buildIdentExpr(mut &token: &token::Token): &ast::NameExpr {
	ret &ast::NameExpr{
		Token: token,
		Name: token.Kind,
		Bind: false,
	}
}

fn getRangeExprTokens(mut &tokens: []&token::Token): ([]&token::Token, int) {
	mut rangeN := 0
	mut i := len(tokens) - 1
	for i >= 0; i-- {
		tok := tokens[i]
		match tok.Id {
		| token::RBrace
		| token::RBracket
		| token::RParent:
			rangeN++
		| token::LBrace
		| token::LBracket
		| token::LParent:
			rangeN--
		}

		if rangeN == 0 {
			ret tokens[:i], rangeN
		}
	}
	ret nil, rangeN
}

fn isAnonFuncHead(&tokens: []&token::Token): bool {
	match tokens[0].Id {
	| token::Unsafe:
		if len(tokens) == 1 || tokens[1].Id != token::Fn {
			break
		}
		fall
	| token::Fn:
		if tokens[len(tokens)-1].Id == token::RBrace {
			// Not function type declaration, anonymous function expression.
			break
		}
		ret true
	}
	ret false
}