// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/build"
use "std/jule/log"
use "std/jule/token"

// Stores information about file parsing.
struct FileInfo {
	AST:    &ast::AST
	Errors: []log::Log
}

// Stores information about package parsing.
struct PackageInfo {
	Files: []&FileInfo
}

// Parses FileSet's tokens and builds AST.
// Returns nil if f is not real.
// FileSet should not contain comment tokens.
fn ParseFile(mut f: &token::FileSet): &FileInfo {
	if f == nil {
		ret nil
	}
	mut finf := new(FileInfo)
	finf.AST, finf.Errors = parseFileSet(f)
	if len(finf.Errors) > 0 {
		finf.AST = nil
	}
	ret finf
}

// Parses FilsSet's tokens and builds AST.
// Returns nil if filesets is nil.
// Skips FileSet if nil.
// FileSets should not contain comment tokens.
fn ParsePackage(mut filesets: []&token::FileSet): &PackageInfo {
	if filesets == nil {
		ret nil
	}
	mut pinf := new(PackageInfo)
	pinf.Files = make([]&FileInfo, 0, len(filesets))
	for (_, mut f) in filesets {
		if f == nil {
			continue
		}
		mut finfo := ParseFile(f)
		pinf.Files = append(pinf.Files, finfo)
	}
	ret pinf
}

fn parseFileSet(mut f: &token::FileSet): (&ast::AST, []log::Log) {
	mut p := new(parser)
	p.parse(f)
	ret p.ast, p.errors
}