// Copyright 2023 The Jule Authors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/token"

// Assignment information.
struct assignInfo {
	l:      []&token::Token
	r:      []&token::Token
	setter: &token::Token
	ok:     bool
}

// Reports given token id is allow for
// assignment left-expression or not.
fn isAssign(id: int): bool {
	ret id == token::NAME ||
		id == token::EXTERN ||
		id == token::LET ||
		id == token::MUT ||
		id == token::SELF ||
		id == token::LPAREN ||
		id == token::MUL ||
		id == token::AND
}

// Checks assignment tokens and whether reports is ok or not.
fn checkAssignTokens(tokens: []&token::Token): bool {
	if len(tokens) == 0 || !isAssign(tokens[0].ID) {
		ret false
	}
	mut braceN := 0
	for _, t in tokens {
		match t.ID {
		| token::LBRACE
		| token::LBRACK
		| token::LPAREN:
			braceN++
		| token::RBRACE
		| token::RBRACK
		| token::RPAREN:
			braceN--
		}
		match {
		| braceN < 0:
			ret false
		| braceN > 0:
			continue
		| token::IsAssign(t.ID)
		| t.ID == token::ARROW
		| t.ID == token::DEFINE:
			ret true
		}
	}
	ret false
}