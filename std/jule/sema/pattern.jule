// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Pattern checker for functions and methods.
struct FuncPattern{}

impl FuncPattern {
	// Reports whether function is the reserved main function.
	fn Main(f: &Func): bool {
		ret f != nil &&
			f.Owner == nil &&
			f.Name == "main" &&
			!f.Bind &&
			!f.Static &&
			!f.Unsafe &&
			!f.Exceptional &&
			f.IsVoid() &&
			len(f.Generics) == 0 &&
			len(f.Params) == 0
	}

	// Reports whether function is the reserved init function.
	fn Init(f: &Func): bool {
		ret f != nil &&
			f.Owner == nil &&
			f.Name == "init" &&
			!f.Bind &&
			!f.Static &&
			!f.Unsafe &&
			!f.Exceptional &&
			f.IsVoid() &&
			len(f.Generics) == 0 &&
			len(f.Params) == 0
	}

	// Reports whether function is the reserved Str function.
	fn Str(f: &Func): bool {
		if f == nil ||
			f.Owner == nil ||
			f.Name != "Str" ||
			f.Static ||
			f.Unsafe ||
			f.IsVoid() ||
			len(f.Generics) != 0 ||
			len(f.Params) != 1 ||
			f.Params[0].Mutable ||
			f.Params[0].IsSmartptr() {
			ret false
		}

		mut ins := unsafe { *(&f.Instances[0]) }
		prim := ins.Result.Prim()
		if prim == nil {
			ret false
		}
		ret prim.IsStr()
	}
}