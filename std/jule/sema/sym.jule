// Copyright 2023 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/conv"
use "std/jule"
use "std/jule/ast"
use "std/jule/build"
use "std/jule/directive"
use "std/jule/log"
use "std/jule/mod"
use "std/jule/token"
use "std/os"
use "std/os/filepath"
use "std/strings"
use "std/unicode"
use "std/unicode/utf8"

// Stack for symbol references.
// It used by Sema to store necessary references.
//
// List of necessary references;
//	- &FuncIns
//	- &StructIns
//	- &Trait
//	- &Var -> Only global ones.
struct ReferenceStack {
	buffer: []any
}

impl ReferenceStack {
	// Returns new reference stack instance.
	fn new(): &ReferenceStack {
		ret &ReferenceStack{
			buffer: make([]any, 0, 10),
		}
	}
}

impl ReferenceStack {
	// Returns count of references.
	fn Len(*self): int {
		ret len(self.buffer)
	}

	// Returns reference by index.
	fn At(mut *self, i: int): any {
		ret self.buffer[i]
	}

	// Push new reference to stack.
	fn Push(mut *self, mut ref: any) {
		self.buffer = append(self.buffer, ref)
	}

	fn clear(mut *self) {
		self.buffer = self.buffer[:0]
	}

	// Reports whether reference is exist.
	fn Exist[T](*self, t: T): bool {
		for _, ref in self.buffer {
			if ref == t {
				ret true
			}
		}
		ret false
	}
}

// Directive pass.
struct Pass {
	Token: &token::Token
	Text:  str
}

fn buildType(mut t: &ast::Expr): &TypeSym {
	if t == nil {
		ret nil
	}
	ret &TypeSym{
		Decl: t,
	}
}

fn buildExpr(mut expr: &ast::Expr): &ValueSym {
	if expr == nil {
		ret nil
	}
	ret &ValueSym{
		Expr: expr,
	}
}

fn buildTypeAlias(mut decl: &ast::TypeAlias): &TypeAlias {
	ret &TypeAlias{
		Scope: decl.Scope,
		Strict: decl.Strict,
		Public: decl.Public,
		Extern: decl.Extern,
		Token: decl.Token,
		Name: decl.Name,
		TypeSym: buildType(decl.Type),
		Generics: decl.Generics,
	}
}

fn buildField(mut decl: &ast::Field): &Field {
	ret &Field{
		Token: decl.Token,
		Public: decl.Public,
		Mutable: decl.Mutable,
		Name: decl.Name,
		TypeSym: buildType(decl.Type),
		Tag: decl.Tag,
	}
}

fn buildFields(mut decls: []&ast::Field): []&Field {
	mut fields := make([]&Field, 0, len(decls))
	for (_, mut decl) in decls {
		fields = append(fields, buildField(decl))
	}
	ret fields
}

fn buildStruct(mut decl: &ast::Struct): &Struct {
	ret &Struct{
		Token: decl.Token,
		Name: decl.Name,
		Fields: buildFields(decl.Fields),
		Public: decl.Public,
		Extern: decl.Extern,
		Directives: decl.Directives,
		Generics: decl.Generics,
	}
}

fn buildParam(mut decl: &ast::Param): &Param {
	ret &Param{
		Token: decl.Token,
		Mutable: decl.Mutable,
		Variadic: decl.Variadic,
		Reference: decl.Reference,
		TypeSym: buildType(decl.Type),
		Name: decl.Name,
	}
}

fn buildParams(mut decls: []&ast::Param): []&Param {
	mut params := make([]&Param, 0, len(decls))
	for (_, mut decl) in decls {
		params = append(params, buildParam(decl))
	}
	ret params
}

fn buildRetType(mut decl: &ast::RetType): &RetType {
	if decl.Names == nil && decl.Type == nil {
		ret nil // Void type.
	}
	ret &RetType{
		TypeSym: buildType(decl.Type),
		Names: decl.Names,
	}
}

fn buildFunc(mut decl: &ast::Func): &Func {
	ret &Func{
		Token: decl.Token,
		Global: decl.Global,
		Async: decl.Async,
		Unsafe: decl.Unsafe,
		Public: decl.Public,
		Extern: decl.Extern,
		Static: decl.Static,
		Exceptional: decl.Exceptional,
		Name: decl.Name,
		Directives: decl.Directives,
		Scope: decl.Scope,
		Generics: decl.Generics,
		Result: buildRetType(decl.Result),
		Params: buildParams(decl.Params),
	}
}

fn buildMethods(mut decls: []&ast::Func): []&Func {
	mut methods := make([]&Func, 0, len(decls))
	for (_, mut decl) in decls {
		methods = append(methods, buildFunc(decl))
	}
	ret methods
}

fn buildTraitInherits(mut inherits: []&ast::Expr): []&TypeSym {
	mut symbols := make([]&TypeSym, 0, len(inherits))
	for (_, mut it) in inherits {
		symbols = append(symbols, buildType(it))
	}
	ret symbols
}

fn buildTrait(mut decl: &ast::Trait): &Trait {
	ret &Trait{
		Token: decl.Token,
		Name: decl.Name,
		Public: decl.Public,
		Methods: buildMethods(decl.Methods),
		Inherits: buildTraitInherits(decl.Inherits),
	}
}

fn buildEnumItems(mut decls: []&ast::EnumItem): []&Var {
	mut items := make([]&Var, len(decls)+1)
	for (i, mut decl) in decls {
		items[i] = &Var{
			Public: true,
			Constant: true,
			Token: decl.Token,
			Name: decl.Name,
			ValueSym: buildExpr(decl.X),
			Group: items,
			GroupIndex: i,
			Refers: ReferenceStack.new(),
		}
	}
	// Set the last element to nil.
	// See developer reference (14).
	items[len(decls)] = nil
	ret items[: len(items)-1 : len(items)-1] // return with no trailing nil variable
}

fn buildEnum(mut decl: &ast::Enum): &Enum {
	ret &Enum{
		Token: decl.Token,
		Public: decl.Public,
		Name: decl.Name,
		TypeSym: buildType(decl.Type),
		Items: buildEnumItems(decl.Items),
	}
}

fn buildTypeEnumItem(mut decl: &ast::TypeEnumItem): &TypeEnumItem {
	ret &TypeEnumItem{
		Token: decl.Token,
		TypeSym: buildType(decl.Type),
	}
}

fn buildTypeEnumItems(mut decls: []&ast::TypeEnumItem): []&TypeEnumItem {
	mut items := make([]&TypeEnumItem, 0, len(decls))
	for (_, mut decl) in decls {
		items = append(items, buildTypeEnumItem(decl))
	}
	ret items
}

fn buildTypeEnum(mut decl: &ast::TypeEnum): &TypeEnum {
	ret &TypeEnum{
		Token: decl.Token,
		Public: decl.Public,
		Name: decl.Name,
		Items: buildTypeEnumItems(decl.Items),
	}
}

fn buildVar(mut decl: &ast::Var): &Var {
	ret &Var{
		Token: decl.Token,
		Name: decl.Name,
		Extern: decl.Extern,
		Constant: decl.Const,
		Mutable: decl.Mutable,
		Public: decl.Public,
		Static: decl.Static,
		Reference: decl.Reference,
		Directives: decl.Directives,
		Refers: ReferenceStack.new(),
		TypeSym: buildType(decl.Type),
		ValueSym: buildExpr(decl.X),
	}
}

fn buildImpl(mut decl: &ast::Impl): &Impl {
	ret &Impl{
		Base: decl.Base,
		Dest: decl.Dest,
		Methods: buildMethods(decl.Methods),
	}
}

// Symbol table builder.
// Just builds symbols, not analyze metadata
// like struct's implemented traits.
struct symBuilder {
	owner:      &symBuilder
	importPath: str // Import path, this symBuilder created for.
	importer:   Importer
	errors:     []log::Log
	ast:        &ast::AST
	table:      &SymTab
}

impl symBuilder {
	fn getRoot(mut &self): &symBuilder {
		mut root := self
		for root.owner != nil {
			root = root.owner
		}
		ret root
	}

	fn pushErr(mut *self, token: &token::Token, fmt: str, args: ...any) {
		self.errors = append(self.errors, compilerErr(token, true, fmt, args...))
	}

	fn pushErrText(mut *self, token: &token::Token, fmt: str, args: ...any) {
		self.errors = append(self.errors, compilerErr(token, false, fmt, args...))
	}

	// Push suggestion to last log.
	fn pushSuggestion(mut *self, fmt: str, args: ...any) {
		unsafe { pushSuggestion(&self.errors[len(self.errors)-1], fmt, args...) }
	}

	// Checks and returns absolute path of import filepath.
	// Designed for non-std package paths.
	// Returns empty string if error occurs.
	fn checkAbsPath(mut &self, mut filepath: str, mut decl: &ast::Use, mod: &mod::Mod): str {
		// Join filepath with the absolute module path and
		// make sure we have an absolute path to the package.
		filepath = filepath::Join(mod.Path, filepath)
		filepath = filepath::Abs(filepath) else {
			self.pushErr(decl.Path, "path not found or cannot be accessed: "+decl.Path.Kind)
			ret ""
		}

		ret filepath
	}

	// Same as the [checkAbsPath] method but designed for std package paths.
	fn checkStdAbsPath(mut &self, mut filepath: str, mut decl: &ast::Use): str {
		if filepath == "" {
			self.pushErr(decl.Path, "invalid import path: "+decl.Path.Kind)
			ret ""
		}
		filepath = filepath::Join(build::PathStdlib(), filepath)
		filepath = filepath::Abs(filepath) else {
			self.pushErr(decl.Path, "path not found or cannot be accessed: "+decl.Path.Kind)
			ret ""
		}

		ret filepath
	}

	// Checks import path and returns as filepath if no error exist.
	fn checkImpPath(mut &self, mut decl: &ast::Use): (std: bool, filepath: str) {
		path := decl.Path.Kind[1 : len(decl.Path.Kind)-1] // remove quotes
		if decl.Extern && build::IsStdHeaderPath(path) {
			ret false, path
		}

		// Take module name and submodule path from the import path.
		pathModName, submodulePath := splitModuleNameFromFilepath(path)
		std = pathModName == build::ModStdlib().Name

		// Check submodule path, if exist.
		if submodulePath != "" {
			filepath = checkImportPathValidity(submodulePath)
			if filepath == "" {
				self.pushErr(decl.Path, "invalid import path: "+decl.Path.Kind)
				ret false, ""
			}
		}

		// build absolute path
		if std {
			filepath = self.checkStdAbsPath(filepath, decl)
		} else {
			// If there is no module, no common directories.
			// For the local packages, the root package needs a module.
			mut mod := self.importer.GetMod()
			if mod == nil {
				self.pushErr(decl.Path, "module not found")
				self.pushSuggestion(`run "julec mod init <modulename>" to initialize a module in the current directory`)
				ret false, ""
			}

			// All import filepaths are must be start with module name.
			if mod.Name != pathModName {
				self.pushErr(decl.Path, "import path must use the root name of the module")
				self.pushSuggestion("module name of this package is: " + conv::Quote(mod.Name))
				ret false, ""
			}

			filepath = self.checkAbsPath(filepath, decl, mod)
		}

		if decl.Extern {
			ext := filepath::Ext(filepath)
			if !build::IsValidHeaderExt(ext) &&
				!build::IsValidCppExt(ext) &&
				!build::IsValidAssemblyExt(ext) {
				self.pushErr(decl.Token, "invalid C/C++/Assembly extension: "+conv::Quote(ext))
				ret false, ""
			}
		}

		// Exist?
		info := os::Stat(filepath) else {
			self.pushErr(decl.Token, "path not found or cannot be accessed: "+decl.Path.Kind)
			ret false, ""
		}
		exist := !decl.Extern && info.IsDir() ||
			decl.Extern && info.Mode().IsRegular()
		if !exist {
			self.pushErr(decl.Token, "path not found or cannot be accessed: "+decl.Path.Kind)
			ret false, ""
		}

		ret
	}

	// Assigns an alias to import.
	fn assignAlias(mut &self, mut imp: &ImportInfo) {
		mut i := strings::LastIndex(imp.LinkPath, jule::ImportPathSep)
		i++ // skip separator and also if -1 it takes full path
		path := imp.LinkPath[i:]
		imp.Alias = path
	}

	fn buildImport(mut &self, mut decl: &ast::Use): &ImportInfo {
		std, filepath := self.checkImpPath(decl)
		if filepath == "" { // error occurred
			ret nil
		}

		mut imp := &ImportInfo{
			Decl: decl,
			Path: filepath,
			LinkPath: decl.Path.Kind[1 : len(decl.Path.Kind)-1],
			Std: std,
		}

		if decl.Extern {
			imp.Extern = true
			imp.Package = nil // Cpp headers haven't symbol table.
			ret imp
		}

		imp.Package = &Package{
			Files: nil, // Appends by import algorithm.
		}
		if decl.Alias != nil {
			imp.Alias = decl.Alias.Kind
		} else if !isImplicitImport(imp) {
			self.assignAlias(imp)
		}
		ret imp
	}

	fn findUseDecl(mut *self, pkg: &ImportInfo): &ImportInfo {
		// Find package by path to detect external header imports.
		// Ignore implicit imports, see developer reference (6).
		ret self.table.SelectPackage(fn|spkg| !isImplicitImport(spkg) && unsafe { pkg.Path == spkg.Path })
	}

	fn checkDuplicateUseDecl(mut *self, mut pkg: &ImportInfo): (ok: bool) {
		mut lpkg := self.findUseDecl(pkg)
		if lpkg == nil {
			ret true
		}
		self.pushErr(pkg.Decl.Token, "package @ is already imported", pkg.LinkPath)
		self.pushSuggestion("remove duplicate use declarations for to the same package")
		ret false
	}

	fn pushCycleError(mut &self, sb: &symBuilder, path: str, mut &message: *strings::Builder) {
		const Padding = 7
		refersTo := log::Logf("@ refers to @", conv::Quote(sb.importPath), conv::Quote(path))
		message.WriteStr(strings::Repeat(" ", Padding))!
		message.WriteStr(refersTo)!
		message.WriteByte('\n')!
	}

	fn pushCrossCycleError(mut &self, target: &symBuilder, imp: &ImportInfo, errorToken: &token::Token) {
		mut message := strings::Builder{}
		self.pushCycleError(self, imp.LinkPath, &message)
		mut owner, mut old := self.owner, self
		for owner != nil; owner, old = owner.owner, owner {
			self.pushCycleError(old.owner, old.importPath, &message)
			if owner == target {
				break
			}
		}
		self.pushErr(errorToken, "illegal cross cycle in use declarations:\n"+message.Str())
	}

	fn checkImportCycles(mut &self, imp: &ImportInfo, decl: &ast::Use): bool {
		if imp.Path == self.table.File.Dir() {
			self.pushErr(decl.Token, "package @ cannot refer to itself", imp.LinkPath)
			ret false
		}
		if self.owner == nil {
			ret true
		}
		mut owner := self
		for owner != nil; owner = owner.owner {
			if owner.table.File.Dir() == imp.Path {
				self.pushCrossCycleError(owner, imp, decl.Token)
				ret false
			}
		}
		ret true
	}

	// Checks internal packages or etc. for accessibility.
	//
	// Parameters:
	//	- decl: relevant use declaration.
	//	- mod: module that imports relevant package.
	//	- path: path of package that importing.
	fn checkPackageAccessibility(mut *self, decl: &ast::Use, mod: &mod::Mod, path: str) {
		if self.importer.GetMod().Equal(mod) {
			ret
		}
		const InternalName = "internal"
		i := strings::Index(path, InternalName)
		if i == -1 {
			ret
		}
		if i == 0 {
			// "internal" or "internal/" ?
			if len(path)-len(InternalName) == 0 ||
				strings::HasPrefix(path, InternalName+jule::ImportPathSep) {
				self.pushErr(decl.Token, "internal packages cannot be accessed")
			}
			ret
		}
		// "/internal" ?
		if path[i-1:] == jule::ImportPathSep+InternalName {
			self.pushErr(decl.Token, "internal packages cannot be accessed")
			ret
		}
		// "/internal/" ?
		n := i + len(InternalName) + 1
		if n < len(path) && path[i-1:n] == jule::ImportPathSep+InternalName+jule::ImportPathSep {
			self.pushErr(decl.Token, "internal packages cannot be accessed")
			ret
		}
	}

	fn importPackage(mut &self, mut imp: &ImportInfo, mut decl: &ast::Use): (ok: bool) {
		if imp.Extern {
			ret true
		}

		mut port := self.importer.GetImport(imp.Path)
		if port != nil {
			imp.Package = port.Package
			imp.Duplicate = true

			mut oldMod := self.importer.GetMod()
			self.importer.SetMod(self.importer.ModById(port.ModID))
			if !isImplicitImport(imp) && !isRuntimeInternal(imp) {
				self.checkPackageAccessibility(decl, oldMod, port.Path)
			}
			self.importer.SetMod(oldMod)
		} else {
			if !self.checkImportCycles(imp, decl) {
				ret false
			}

			mut oldMod := self.importer.GetMod()
			defer { self.importer.SetMod(oldMod) }
			if imp.Std {
				self.importer.SetMod(build::ModStdlib())
			}
			mut asts, mut errors := self.importer.ImportPackage(imp.Path, !imp.Std)
			if len(errors) > 0 {
				self.errors = append(self.errors, errors...)
				ret false
			}

			if !isImplicitImport(imp) && !isRuntimeInternal(imp) {
				self.checkPackageAccessibility(decl, oldMod, imp.Path)
			}

			for (_, mut ast) in asts {
				mut table := (&SymTab)(nil)
				table, errors = buildSymbols(imp.LinkPath, ast, self.importer, self)
				// Break import if file has error(s).
				if len(errors) > 0 {
					self.errors = append(self.errors, errors...)
					ret false
				}
				imp.Package.Files = append(imp.Package.Files, table)
			}
		}
		ret true
	}

	fn importUseDecl(mut &self, mut decl: &ast::Use): &ImportInfo {
		mut imp := self.buildImport(decl)
		// Break analysis if error occurs.
		if imp == nil {
			ret nil
		}
		mut ok := self.checkDuplicateUseDecl(imp)
		if !ok {
			ret nil
		}
		ok = self.importPackage(imp, decl)
		self.table.Imports = append(self.table.Imports, imp)
		if ok {
			self.importer.Imported(imp)
			ret imp
		}
		ret nil
	}

	fn importUseDecls(mut &self) {
		for (_, mut decl) in self.ast.UseDecls {
			self.importUseDecl(decl)
			// Break analysis if error occurs.
			if len(self.errors) > 0 {
				break
			}
		}
	}

	fn appendNode(mut *self, mut node: ast::Node) {
		match type node.Data {
		| &ast::Impl:
			self.table.Impls = append(self.table.Impls, buildImpl(node.Data.(&ast::Impl)))
		| &ast::TypeAlias:
			mut ta := buildTypeAlias(node.Data.(&ast::TypeAlias))
			self.table.TypeAliases = append(self.table.TypeAliases, ta)
		| &ast::Struct:
			mut srct := buildStruct(node.Data.(&ast::Struct))
			self.table.Structs = append(self.table.Structs, srct)
		| &ast::Func:
			mut f := buildFunc(node.Data.(&ast::Func))
			self.table.Funcs = append(self.table.Funcs, f)
		| &ast::Trait:
			mut t := buildTrait(node.Data.(&ast::Trait))
			self.table.Traits = append(self.table.Traits, t)
		| &ast::Var:
			mut astv := node.Data.(&ast::Var)
			// Grouped variables represented by the root variable of group.
			// See developer reference (12).
			// In the HIR, all variables of the group should be in the HIR.
			// See developer reference (13).
			if astv.Group == nil {
				mut v := buildVar(astv)
				self.table.Vars = append(self.table.Vars, v)
			} else {
				n := len(self.table.Vars)
				for (_, mut gv) in astv.Group {
					self.table.Vars = append(self.table.Vars, buildVar(gv))
				}
				mut group := self.table.Vars[n:len(self.table.Vars):len(self.table.Vars)]
				for (i, mut v) in group {
					v.GroupIndex = i
					v.Group = group
				}
			}
		| &ast::Enum:
			mut e := buildEnum(node.Data.(&ast::Enum))
			self.table.Enums = append(self.table.Enums, e)
		| &ast::TypeEnum:
			mut e := buildTypeEnum(node.Data.(&ast::TypeEnum))
			self.table.TypeEnums = append(self.table.TypeEnums, e)
		|:
			self.pushErr(node.Token, "invalid syntax")
		}
	}

	fn appendNodes(mut *self) {
		for (_, mut node) in self.ast.Nodes {
			self.appendNode(node)
		}
	}

	fn pushDirectivePass(mut *self, mut d: &ast::Directive) {
		if len(d.Args) == 0 {
			self.pushErr(d.Tag, "missing expression")
			self.pushSuggestion(`use like; #pass "-arg"`)
			ret
		} else if len(d.Args) > 1 {
			arg := d.Args[1]
			self.pushErr(arg, `passed more argument than expected to directive #pass`)
			self.pushSuggestion(`use like; #pass "-arg"`)
		}

		arg := d.Args[0]
		if arg.ID != token::STR || arg.Kind[0] != '"' {
			self.pushErr(arg, "invalid syntax")
			ret
		}

		self.table.Passes = append(self.table.Passes, Pass{
			Token: d.Tag,
			Text: arg.Kind[1 : len(arg.Kind)-1],
		})
	}

	fn appendTopDirectives(mut *self) {
		for (_, mut d) in self.ast.TopDirectives {
			match d.Tag.Kind {
			| directive::Pass:
				self.pushDirectivePass(d)
			}
		}
	}

	fn build(mut &self) {
		self.table = &SymTab{
			File: self.ast.File,
		}

		self.appendTopDirectives()

		self.importUseDecls()
		// Break analysis if use declarations has error.
		if len(self.errors) > 0 {
			ret
		}

		self.appendNodes()
		// Break analysis if declarations has error.
		if len(self.errors) > 0 {
			ret
		}
	}
}

// Reports whether imp is implicitly imported.
// See developer reference (6).
fn isImplicitImport(imp: &ImportInfo): bool {
	ret imp.Decl.Token == nil
}

// Reports whether imp is runtime internal package.
fn isRuntimeInternal(imp: &ImportInfo): bool {
	ret strings::HasPrefix(imp.LinkPath, "std/internal/runtime")
}

// Takes filepath and returns module name from the path and submodule path if exist.
// For example: "std/jule/sema" -> ("std", "jule/sema")
// Returns empty if there is no submodule path.
fn splitModuleNameFromFilepath(filepath: str): (modName: str, submodulePath: str) {
	i := strings::IndexByte(filepath, '/')
	if i > 0 {
		modName = filepath[:i]
		// Remove the module name from the import filepath.
		// The module name will be replaced with module path.
		// The following path is submodule path, must remain.
		submodulePath = filepath[i+1:]
	} else {
		// The filepath is entirely is the module name.
		// The module name will be replaced with module path.
		// There is no submodules in the path, so set to empty string.
		modName = filepath
		submodulePath = ""
	}
	ret
}

// Checks validity of the import path and returns it as filepath.
// Returns empty string if path is invalid.
fn checkImportPathValidity(path: str): (filepath: str) {
	if strings::HasSuffix(path, jule::ImportPathSep) {
		ret ""
	}
	parts := strings::Split(path, jule::ImportPathSep)
	if len(parts) == 0 {
		ret ""
	}
	for _, part in parts {
		if part == "" || jule::IsBlank(part) || strings::ContainsAny(part, "\\") {
			ret ""
		}
		filepath = filepath::Join(filepath, part)
	}
	ret filepath
}