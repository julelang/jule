// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/build"
use "std/unsafe"

// Flags for semantic analysis.
enum Flag {
	Default: 0,        // Default semantic analysis of Jule.
	Shadowing: 1 << 0, // Default + enable shadowing.
}

// Builds symbol table of AST.
fn buildSymbols(mut &ast: &ast::AST, mut &importer: Importer, mut owner: &symBuilder): (&SymTab, []build::Log) {
	mut sb := &symBuilder{
		ast: ast,
		importer: importer,
		owner: owner,
	}
	sb.build()

	if len(sb.errors) == 0 {
		ret sb.table, nil
	}
	ret nil, sb.errors
}

// See developer reference (6).
fn collectImplicitImports(mut &s: &sema, mut &file: &SymTab) {
	for (_, mut imp) in file.Imports {
		if !isImplicitImport(imp) {
			break
		}
		match imp.LinkPath {
		| "std/runtime":
			s.meta.runtime = imp
		|:
			panic("sema: implementation mistake in implicit import collection, this panic call should be unreachable")
		}
	}
}

fn analyzePackage(mut &files: []&ast::AST, mut &importer: Importer, &flags: Flag): (&Package, []build::Log) {
	// Build symbol tables of files.
	mut tables := make([]&SymTab, 0, len(files))
	for (_, mut f) in files {
		mut table, mut errors := buildSymbols(f, importer, nil)
		if len(errors) > 0 {
			ret nil, errors
		}
		tables = append(tables, table)
	}

	mut sema := &sema{
		files: tables,
		flags: flags,
		meta: new(commonSemaMeta),
	}

	// Use first table (so first file) for this.
	// See developer reference (6).
	mut firstTable := tables[0]
	collectImplicitImports(sema, firstTable)

	sema.setSemaFields()
	ok := sema.checkPackages(importer.AllPackages())
	if !ok {
		ret nil, sema.errors
	}
	sema.check()
	if len(sema.errors) > 0 {
		ret nil, sema.errors
	}

	mut pkg := &Package{
		Files: sema.files,
	}

	ret pkg, nil
}

// Builds symbol table of package's ASTs.
// Returns nil if files is nil.
// Returns nil if pwd is empty.
// Returns nil if pstd is empty.
// Accepts current working directory is pwd.
//
// Parameters:
//	files:    abstract syntax trees of files
//	importer: importer that used for use declarations
//
// Dependent Parameters:
//	working-directory: uses working directory path provided by build
//	std-path:          uses standard library path provided by build
//
// Risks:
//	You can pass nil to importer, but panics if importer is nil and
//	semantic analyzer used nil importer.
fn AnalyzePackage(mut files: []&ast::AST, mut importer: Importer, flags: Flag): (&Package, []build::Log) {
	if len(files) == 0 {
		ret nil, nil
	}
	ret analyzePackage(files, importer, flags)
}

// Builds symbol table of AST.
// Returns nil if f is nil.
// Returns nil if pwd is empty.
// Returns nil if pstd is empty.
// Accepts current working directory is pwd.
//
// Parameters:
//	f:        file's abstract syntax tree
//	importer: importer that used for use declarations
//
// Dependent Parameters:
//	working-directory: uses working directory path provided by build
//	std-path:          uses standard library path provided by build
//
// Risks:
//	You can pass nil to importer, but panics if importer is nil and
//	semantic analyzer used nil importer.
fn AnalyzeFile(mut f: &ast::AST, mut importer: Importer, flags: Flag): (&SymTab, []build::Log) {
	let mut files: [1]&ast::AST = [f]
	mut _files := unsafe::Slice(&files[0], len(files), len(files))
	mut pkg, mut errors := AnalyzePackage(_files, importer, flags)
	if len(errors) > 0 {
		ret nil, errors
	}
	// Select first table, because package has only one file.
	// We give just one file.
	mut table := pkg.Files[0]
	ret table, nil
}