// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/token"

// Variable.
struct Var {
	Scope:      &Scope
	Token:      &token::Token
	Ident:      str
	Binded:     bool
	Constant:   bool
	Mutable:    bool
	Public:     bool
	Used:       bool
	Statically: bool
	Reference:  bool
	Checked:    bool
	TypeSym:    &TypeSym
	ValueSym:   &ValueSym
	Refers:     &ReferenceStack
	Directives: []&ast::Directive

	// The -2 means this variable is not one of the return variables.
	// The -1 means this variable is just the single return variable one.
	// The 0..n means this variable is the nth variable of the return variables.
	RetOrder: int = -2

	// This variable depended to these variables for initialization expression.
	// Nil if not global variable.
	Depends: []&Var

	// See developer reference (13).
	GroupIndex: int    // Index of variable in the group, if variable is grouped.
	Group:      []&Var // All variables of group in define order, if variable is grouped.
	Iota:       bool   // The enumerable iota variable used in the expression.
}

impl Var {
	// Reports whether variable is initialized explicitly.
	fn IsInitialized(self): bool {
		ret self.ValueSym != nil && self.ValueSym.Expr != nil
	}

	// Reports whether variable is type inferred.
	fn IsTypeInferred(self): bool {
		ret self.TypeSym == nil || self.TypeSym.Decl == nil
	}

	// Reports whether variable is enum field.
	fn IsEnumField(self): bool {
		// See developer reference (14).
		ret len(self.Group) > 0 && self.Group[len(self.Group)-1] == nil
	}

	// Reports whether variable is untyped constant.
	// For true result, variable should type inferred and not include casting.
	fn untypedConstant(self): bool {
		ret self.IsTypeInferred() && self.ValueSym.Value.untyped
	}

	// Reports whether s exist in Depends collection.
	fn depended(self, v: &Var): bool {
		for _, dv in self.Depends {
			if dv == v {
				ret true
			}
		}
		ret false
	}
}