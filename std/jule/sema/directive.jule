// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/build"
use "std/jule/directive"
use "std/jule/log"
use "std/jule/token"

struct directiveChecker {
	s: &sema
	d: &[]&ast::Directive
	o: any
}

impl directiveChecker {
	fn checkCdef(mut self, &d: &ast::Directive) {
		match type self.o {
		| &Func:
			if self.o.(&Func).Bind {
				break
			}
			fall
		|:
			self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
		}

		if len(d.Args) > 0 {
			self.s.pushErr(d.Args[0], log::InvalidSyntax)
		}
	}

	fn checkTypedef(mut self, &d: &ast::Directive) {
		match type self.o {
		| &Struct:
			if self.o.(&Struct).Bind {
				break
			}
			fall
		|:
			self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
		}

		if len(d.Args) > 0 {
			self.s.pushErr(d.Args[0], log::InvalidSyntax)
		}
	}

	fn checkNamespace(mut self, mut &d: &ast::Directive) {
		match type self.o {
		| &Struct:
			if !self.o.(&Struct).Bind {
				self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
			}
		| &Func:
			if !self.o.(&Func).Bind {
				self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
			}
		| &Var:
			if !self.o.(&Var).Bind {
				self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
			}
		|:
			self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
		}

		if len(d.Args) == 0 {
			self.s.pushErr(d.Tag, log::MissingExpr)
			ret
		} else if len(d.Args) > 1 {
			self.s.pushErr(d.Args[1], log::ArgumentOverflow, d.Tag.Kind)
		}

		arg := d.Args[0]
		if arg.Id != token::Lit {
			self.s.pushErr(arg, log::InvalidSyntax)
			ret
		}

		if arg.Kind[0] != '"' {
			self.s.pushErr(arg, log::InvalidSyntax)
			ret
		}

		// remove quotes
		d.Args[0].Kind = arg.Kind[1 : len(arg.Kind)-1]

		// Push relevant directives.
		match type self.o {
		| &Struct:
			if findDirective(*self.d, directive::Typedef) == nil {
				mut typedef := &ast::Directive{
					Tag: new(token::Token, *d.Tag),
				}
				typedef.Tag.Kind = directive::Typedef
				*self.d = append(*self.d, typedef)
			}
		}
	}

	fn checkTest(mut self, &d: &ast::Directive) {
		match type self.o {
		| &Func:
			f := self.o.(&Func)
			if !f.Bind && !f.IsInit() && !f.IsEntryPoint() {
				break
			}
			if f.IsMethod() {
				self.s.pushErr(f.Token, log::TestMethod)
				break
			}
			fall
		|:
			self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
		}

		if len(d.Args) > 0 {
			self.s.pushErr(d.Args[0], log::InvalidSyntax)
		}
	}

	fn checkExport(mut self, mut &d: &ast::Directive) {
		match type self.o {
		| &Func:
			f := self.o.(&Func)
			if f.Bind || f.IsInit() || f.IsEntryPoint() || f.IsMethod() || len(f.Generics) > 0 {
				self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
				ret
			}
		| &Var:
			v := self.o.(&Var)
			if v.Bind || v.Constant {
				self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
			}
		|:
			self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
			ret
		}
		if len(d.Args) > 1 {
			self.s.pushErr(d.Args[1], log::ArgumentOverflow, d.Tag.Kind)
			ret
		}
		if len(d.Args) < 1 {
			self.s.pushErr(d.Tag, log::MissingExpr)
			ret
		}
		arg := d.Args[0]
		if arg.Id != token::Lit {
			self.s.pushErr(arg, log::InvalidSyntax)
			ret
		}
		if arg.Kind[0] != '"' {
			self.s.pushErr(arg, log::InvalidSyntax)
			ret
		}
		// remove quotes
		d.Args[0].Kind = arg.Kind[1 : len(arg.Kind)-1]
	}

	fn checkDirective(mut self, mut &d: &ast::Directive) {
		match d.Tag.Kind {
		| directive::Cdef:
			self.checkCdef(d)
		| directive::Typedef:
			self.checkTypedef(d)
		| directive::Namespace:
			self.checkNamespace(d)
		| directive::Test:
			self.checkTest(d)
		| directive::Export:
			self.checkExport(d)
		| directive::Build
		| directive::Pass:
			self.s.pushErr(d.Tag, log::UnsupportedDirective, d.Tag.Kind)
		|:
			self.s.pushErr(d.Tag, log::InvalidLabel, d.Tag.Kind)
		}
	}

	fn check(mut self) {
		for (_, mut d) in *self.d {
			self.checkDirective(d)
		}
	}
}