// Copyright 2023 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/build"
use "std/jule/directive"
use "std/jule/log"
use "std/jule/token"

struct directiveChecker {
	s: &sema
	d: &[]&ast::Directive
	o: any
}

impl directiveChecker {
	fn checkCdef(mut *self, d: &ast::Directive) {
		match type self.o {
		| &Func:
			if self.o.(&Func).Extern {
				break
			}
			fall
		|:
			self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
		}

		if len(d.Args) > 0 {
			self.s.pushErr(d.Args[0], "invalid syntax")
		}
	}

	fn checkTypedef(mut *self, d: &ast::Directive) {
		match type self.o {
		| &Struct:
			if self.o.(&Struct).Extern {
				break
			}
			fall
		|:
			self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
		}

		if len(d.Args) > 0 {
			self.s.pushErr(d.Args[0], "invalid syntax")
		}
	}

	fn checkNamespace(mut *self, mut d: &ast::Directive) {
		match type self.o {
		| &Struct:
			if !self.o.(&Struct).Extern {
				self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
			}
		| &Func:
			if !self.o.(&Func).Extern {
				self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
			}
		| &Var:
			if !self.o.(&Var).Extern {
				self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
			}
		|:
			self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
		}

		if len(d.Args) == 0 {
			self.s.pushErr(d.Tag, "missing expression")
			ret
		} else if len(d.Args) > 1 {
			self.s.pushErr(d.Args[1], `passed more argument than expected to directive #namespace`)
			self.s.pushSuggestion(`use like; #namespace "namespace"`)
		}

		arg := d.Args[0]
		if arg.ID != token::STR || arg.Kind[0] != '"' {
			self.s.pushErr(arg, "invalid syntax")
			ret
		}

		// remove quotes
		d.Args[0].Kind = arg.Kind[1 : len(arg.Kind)-1]

		// Push relevant directives.
		match type self.o {
		| &Struct:
			if findDirective(*self.d, directive::Typedef) == nil {
				mut typedef := &ast::Directive{
					Tag: new(token::Token, *d.Tag),
				}
				typedef.Tag.Kind = directive::Typedef
				*self.d = append(*self.d, typedef)
			}
		}
	}

	fn checkTest(mut *self, d: &ast::Directive) {
		match type self.o {
		| &Func:
			f := self.o.(&Func)
			if !f.Extern && !f.IsInit() && !f.IsEntryPoint() {
				break
			}
			if f.IsMethod() {
				self.s.pushErr(f.Token, "methods cannot be test function")
				break
			}
			fall
		|:
			self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
		}

		if len(d.Args) > 0 {
			self.s.pushErr(d.Args[0], "invalid syntax")
		}
	}

	fn checkExport(mut *self, mut d: &ast::Directive) {
		match type self.o {
		| &Func:
			f := self.o.(&Func)
			if f.Extern || f.IsInit() || f.IsEntryPoint() || f.IsMethod() || len(f.Generics) > 0 {
				self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
				ret
			}
		| &Var:
			v := self.o.(&Var)
			if v.Extern || v.Constant {
				self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
			}
		|:
			self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
			ret
		}
		if len(d.Args) > 1 {
			self.s.pushErr(d.Args[1], `passed more argument than expected to directive #export`)
			self.s.pushSuggestion(`use like; #export "name"`)
			ret
		}
		if len(d.Args) < 1 {
			self.s.pushErr(d.Tag, "missing expression")
			ret
		}
		arg := d.Args[0]
		if arg.ID != token::STR || arg.Kind[0] != '"' {
			self.s.pushErr(arg, "invalid syntax")
			ret
		}
		// remove quotes
		d.Args[0].Kind = arg.Kind[1 : len(arg.Kind)-1]
	}

	fn checkDisable(mut *self, mut d: &ast::Directive) {
		match type self.o {
		| &Func:
			f := self.o.(&Func)
			if f.Extern {
				self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
				ret
			}
		|:
			self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
			ret
		}
		if len(d.Args) == 0 {
			self.s.pushErr(d.Tag, "the #disable directive requires at least one argument")
			ret
		}
		mut args := map[str]bool{
			"boundary": false,
			"nilptr": false,
		}
		for _, arg in d.Args {
			r, ok := args[arg.Kind]
			if r {
				self.s.pushErr(arg, "argument is already passed for the #disable directive: "+arg.Kind)
			} else if !ok {
				self.s.pushErr(arg, "invalid argument for the #disable directive: "+arg.Kind)
			}
			args[arg.Kind] = true
		}
	}

	fn checkDirective(mut *self, mut d: &ast::Directive) {
		match d.Tag.Kind {
		| directive::Cdef:
			self.checkCdef(d)
		| directive::Typedef:
			self.checkTypedef(d)
		| directive::Namespace:
			self.checkNamespace(d)
		| directive::Test:
			self.checkTest(d)
		| directive::Export:
			self.checkExport(d)
		| directive::Build
		| directive::Pass:
			self.s.pushErr(d.Tag, "declaration does not supports directive: "+d.Tag.Kind)
		| directive::Disable:
			self.checkDisable(d)
		|:
			self.s.pushErr(d.Tag, "invalid label: "+d.Tag.Kind)
		}
	}

	fn check(mut *self) {
		for (_, mut d) in *self.d {
			self.checkDirective(d)
		}
	}
}