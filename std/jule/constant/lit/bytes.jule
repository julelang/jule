// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/conv"
use "std/internal/byteslite"
use "std/unicode/utf8"
use "std/unsafe"

// An error for literal parsing.
struct Error {
	Offset: int
	Text:   str
}

// Reports whether rune is byte actually.
// In other words, whether rune is ASCII.
fn IsAscii(r: rune): bool {
	const MaxAscii = 1<<7 - 1
	ret r <= MaxAscii
}

// Returns rune from literal, literal includes quotes.
// Allows escape sequences.
// Assumes lit is syntactically correct.
fn ToRune(mut lit: str): (rune, errors: []Error) {
	mut r := rune(0)
	lit = lit[1 : len(lit)-1]
	if lit[0] == '\\' && len(lit) > 1 {
		mut i := 0
		r, _ = runeFromEsqSeq(lit, i, errors)
	} else {
		r, _ = utf8::DecodeRuneStr(lit)
	}
	ret r, errors
}

// Returns the first rune from ltieral. Quotes of the literal must be removed.
// Allows escape sequences.
// Checks the literal syntactically and semantically for the first rune.
fn GetRune(mut lit: str): (r: rune, length: int, errors: []Error) {
	if lit[0] == '\\' && len(lit) > 1 {
		ok := checkEsqSeq(lit, errors)
		if !ok {
			ret 0, 0, errors
		}
		r, _ = runeFromEsqSeq(lit, length, errors)
	} else {
		r, length = utf8::DecodeRuneStr(lit)
	}
	ret
}

// Returns raw-string value string from literal, literal includes quotes.
// Assumes lit is syntactically correct.
fn ToRawStr(lit: str): str {
	if len(lit) == 2 {
		ret ""
	}
	ret lit[1 : len(lit)-1]
}

// Returns string value string from literal, literal includes quotes.
// Allows escape sequences.
// Assumes lit is syntactically correct.
fn ToStr(mut lit: str): (str, errors: []Error) {
	if len(lit) == 2 {
		ret "", nil
	}
	// If no escape sequence used, return the string directly with no quotes.
	escapeseq := byteslite::IndexByteStr(lit, '\\') >= 0
	if !escapeseq {
		ret lit[1 : len(lit)-1], nil
	}

	// String is not pure.
	// Handle unicode characters and escape sequences.
	lit = lit[1 : len(lit)-1]
	mut s := make([]byte, 0, len(lit))
	mut i := 0
	for i < len(lit) {
		b := lit[i]
		if b == '\\' {
			strEsqSeq(s, lit, i, errors)
		} else {
			r, size := utf8::DecodeRuneStr(lit[i:])
			i += size
			s = utf8::AppendRune(s, r)
		}
	}
	ret unsafe::StrFromBytes(s), errors
}

// Checks the first escape sequence is syntacticaly correct.
// Assumes the s starts with the escape sequence rune `\`.
fn checkEsqSeq(s: str, mut &errors: []Error): (ok: bool) {
	if len(s) < 2 {
		errors = append(errors, Error{
			Offset: 0,
			Text: "invalid escape sequence, only used the backslash, expected sequence",
		})
		ret false
	}
	match s[1] {
	| '\\' | '\'' | '"' | 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v':
		ret true
	| 'u':
		if len(s) < 6 {
			errors = append(errors, Error{
				Offset: 0,
				Text: "invalid escape sequence, expected \"\\uhhhh\"",
			})
			ret false
		}
		for _, r in s[2:6] {
			if !isHex(r) {
				errors = append(errors, Error{
					Offset: 0,
					Text: "invalid escape sequence, expected \"\\uhhhh\"",
				})
				ret false
			}
		}
		ret true
	| 'U':
		if len(s) < 10 {
			errors = append(errors, Error{
				Offset: 0,
				Text: "invalid escape sequence, expected \"\\Uhhhhhhhh\"",
			})
			ret false
		}
		for _, r in s[2:10] {
			if !isHex(r) {
				errors = append(errors, Error{
					Offset: 0,
					Text: "invalid escape sequence, expected \"\\Uhhhhhhhh\"",
				})
				ret false
			}
		}
		ret true
	| 'x':
		if len(s) < 4 {
			errors = append(errors, Error{
				Offset: 0,
				Text: "invalid escape sequence, expected \"\\xhh\"",
			})
			ret false
		}
		for _, r in s[2:4] {
			if !isHex(r) {
				errors = append(errors, Error{
					Offset: 0,
					Text: "invalid escape sequence, expected \"\\xhh\"",
				})
				ret false
			}
		}
		ret true
	|:
		if !isOctal(rune(s[1])) {
			errors = append(errors, Error{
				Offset: 0,
				Text: "invalid escape sequence, found \"\\"+str(s[1])+"\"",
			})
			ret false
		}
		if len(s) < 4 {
			errors = append(errors, Error{
				Offset: 0,
				Text: "invalid escape sequence, expected \"\\nnn\"",
			})
			ret false
		}
		for _, r in s[1:4] {
			if !isOctal(r) {
				errors = append(errors, Error{
					Offset: 0,
					Text: "invalid escape sequence, expected \"\\nnn\"",
				})
				ret false
			}
		}
		ret true
	}
}

fn tryBtoaCommonEsq(s: str): (seq: byte, ok: bool) {
	if len(s) < 2 || s[0] != '\\' {
		ret
	}
	match s[1] {
	| '\\':
		seq = '\\'
	| '\'':
		seq = '\''
	| '"':
		seq = '"'
	| 'a':
		seq = '\a'
	| 'b':
		seq = '\b'
	| 'f':
		seq = '\f'
	| 'n':
		seq = '\n'
	| 'r':
		seq = '\r'
	| 't':
		seq = '\t'
	| 'v':
		seq = '\v'
	}
	ok = seq != 0
	ret
}

// Decodes the first escape sequence rune from ltieral s, i is the start position of the decoding.
// Advances the i by size of decoded rune.
// Assumes literal is syntactically correct.
fn runeFromEsqSeq(s: str, mut &i: int, mut &errors: []Error): (r: rune, isByte: bool) {
	b, ok := tryBtoaCommonEsq(s[i:])
	i++ // Skip escape sequence solidus.
	if ok {
		i++ // Skip sequence specifier.
		ret rune(b), true
	}

	match s[i] {
	| 'u':
		const SeqLen = 5
		ru64 := conv::ParseUint(s[i+1:i+SeqLen], 16, 64)!
		r = rune(ru64)
		i += SeqLen
	| 'U':
		const SeqLen = 9
		ru64 := conv::ParseUint(s[i+1:i+SeqLen], 16, 64)!
		r = rune(ru64)
		i += SeqLen
	| 'x':
		isByte = true
		const SeqLen = 3
		seq := s[i+1 : i+SeqLen]
		hex := conv::ParseUint(seq, 16, 64)!
		if hex > 255 {
			// This should be impossible case, but play safe.
			errors = append(errors, Error{
				Offset: i,
				Text: "hexadecimal escape value "+conv::FormatUint(hex, 10)+" > 255",
			})
		}
		i += SeqLen
		r = rune(hex)
	|:
		isByte = true
		const SeqLen = 3
		seq := s[i : i+SeqLen]
		octal := conv::ParseUint(seq, 8, 64)!
		if octal > 255 {
			errors = append(errors, Error{
				Offset: i,
				Text: "octal escape value "+conv::FormatUint(octal, 10)+" > 255",
			})
		}
		i += SeqLen
		r = rune(octal)
	}
	ret
}

// Decodes the escape sequence rune from ltieral s, i is the start position of the decoding.
// Advances the i by size of decoded rune.
// Assumes literal is syntactically correct.
// The decoded rune will be appended to buf.
fn strEsqSeq(mut &buf: []byte, s: str, mut &i: int, mut &errors: []Error) {
	r, isByte := runeFromEsqSeq(s, i, errors)
	if isByte {
		buf = append(buf, byte(r))
		ret
	}
	// Unicode code point.
	buf = utf8::AppendRune(buf, r)
}

// Reports whether rune is octal sequence.
fn isOctal(r: rune): bool { ret '0' <= r && r <= '7' }

// Reports whether rune is hexadecimal sequence.
fn isHex(r: rune): bool {
	match {
	| '0' <= r && r <= '9':
		ret true
	| 'a' <= r && r <= 'f':
		ret true
	| 'A' <= r && r <= 'F':
		ret true
	|:
		ret false
	}
}