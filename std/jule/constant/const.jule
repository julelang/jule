// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Constant data.
// Use Const.new_nil function istead of Const{} for nil literal.
struct Const {
    data: any
}

impl Const {
    // Returns new constant value instance from 64-bit signed integer.
    static fn NewI64(x: i64): &Const { ret &Const{data: x} }

    // Returns new constant value instance from 64-bit unsigned integer.
    static fn NewU64(x: u64): &Const { ret &Const{data: x} }

    // Returns new constant value instance from boolean.
    static fn NewBool(x: bool): &Const { ret &Const{data: x} }

    // Returns new constant value instance from string.
    static fn NewStr(x: str): &Const { ret &Const{data: x} }

    // Returns new constant value instance from 64-bit floating-point.
    static fn NewF64(x: f64): &Const { ret &Const{data: x} }

    // Returns new constant value instance with nil.
    static fn NewNil(): &Const { ret &Const{data: nil} }
}

impl Const {
    //
    // Read
    //

    // Reads 64-bit signed integer data.
    // Returns 0 if data is not 64-bit signed integer.
    fn ReadI64(self): i64 {
        if !self.IsI64() {
            ret 0
        }
        ret i64(self.data)
    }

    // Reads 64-bit unsigned integer data.
    // Returns 0 if data is not 64-bit unsigned integer.
    fn ReadU64(self): u64 {
        if !self.IsU64() {
            ret 0
        }
        ret u64(self.data)
    }

    // Reads boolean data.
    // Returns false if data is not boolean.
    fn ReadBool(self): bool {
        if !self.IsBool() {
            ret false
        }
        ret bool(self.data)
    }

    // Reads string data.
    // Returns empty string if data is not string.
    fn ReadStr(self): str {
        if !self.IsStr() {
            ret ""
        }
        ret str(self.data)
    }

    // Reads 64-bit floating-point data.
    // Returns 0 if data is not 64-bit floating-point.
    fn ReadF64(self): f64 {
        if !self.IsF64() {
            ret 0
        }
        ret f64(self.data)
    }

    //
    // Castings
    //

    // Reads data as 64-bit signed integer.
    // Returns 0 if data is string, bool or which is not numeric.
    fn AsI64(self): i64 {
        match type self.data {
        | i64:
            ret i64(self.data)
        | u64:
            ret i64(u64(self.data))
        | f64:
            ret i64(f64(self.data))
        |:
            ret 0
        }
    }

    // Reads data as 64-bit unsigned integer.
    // Returns 0 if data is string, bool or which is not numeric.
    fn AsU64(self): u64 {
        match type self.data {
        | u64:
            ret u64(self.data)
        | i64:
            ret u64(i64(self.data))
        | f64:
            ret u64(f64(self.data))
        |:
            ret 0
        }
    }

    // Reads data as 64-bit floating-point.
    // Returns 0 if data is string, bool or which is not numeric.
    fn AsF64(self): f64 {
        match type self.data {
        | f64:
            ret f64(self.data)
        | i64:
            ret f64(i64(self.data))
        | u64:
            ret f64(u64(self.data))
        |:
            ret 0
        }
    }

    //
    // Set
    //

    // Sets constant value from 64-bit signed integer.
    fn SetI64(mut self, x: i64) { self.data = x }

    // Sets constant value from 64-bit unsigned integer.
    fn SetU64(mut self, x: u64) { self.data = x }

    // Sets constant value from boolean.
    fn SetBool(mut self, x: bool) { self.data = x }

    // Sets constant value from string.
    fn SetStr(mut self, x: str) { self.data = x }

    // Sets constant value from 64-bit floating-point.
    fn SetF64(mut self, x: f64) { self.data = x }

    // Sets constant value to nil.
    fn SetNil(mut self) { self.data = nil }

    //
    // Types
    //

    // Reports whether data is 64-bit signed integer.
    fn IsI64(self): bool {
        match type self.data {
        | i64:
            ret true
        |:
            ret false
        }
    }

    // Reports whether data is 64-bit unsigned integer.
    fn IsU64(self): bool {
        match type self.data {
        | u64:
            ret true
        |:
            ret false
        }
    }

    // Reports whether data is boolean.
    fn IsBool(self): bool {
        match type self.data {
        | bool:
            ret true
        |:
            ret false
        }
    }

    // Reports whether data is string.
    fn IsStr(self): bool {
        match type self.data {
        | str:
            ret true
        |:
            ret false
        }
    }

    // Reports whether data is 64-bit floating-point.
    fn IsF64(self): bool {
        match type self.data {
        | f64:
            ret true
        |:
            ret false
        }
    }

    // Reports whether data is nil.
    fn IsNil(self): bool { ret self.data == nil }

    // Reports whether self and x has same type.
    fn AreSameTypes(self, x: Const): bool {
        match {
        | self.IsI64() == x.IsI64():
            ret true
        | self.IsU64() == x.IsU64():
            ret true
        | self.IsF64() == x.IsF64():
            ret true
        | self.IsBool() == x.IsBool():
            ret true
        | self.IsStr() == x.IsStr():
            ret true
        | self.IsNil() == x.IsNil():
            ret true
        |:
            ret false
        }
    }

    //
    // Logical
    //

    // Reports whether self and x are true.
    // Returns false if type is not supported.
    fn And(self, x: Const): bool {
        match {
        | self.IsBool() && x.IsBool():
            ret self.ReadBool() && x.ReadBool()
        |:
            ret false
        }
    }

    // Reports whether self or x is true.
    // Returns false if type is not supported.
    fn Or(self, x: Const): bool {
        match {
        | self.IsBool() && x.IsBool():
            ret self.ReadBool() || x.ReadBool()
        |:
            ret false
        }
    }

    // Reports whether self and x are equals.
    // Returns false if type is not supported.
    fn Eq(self, x: Const): bool {
        match {
        | self.IsNil():
            ret x.IsNil()
        | self.IsBool():
            ret x.IsBool() && self.ReadBool() == x.ReadBool()
        | self.IsStr():
            ret x.IsStr() && self.ReadStr() == x.ReadStr()
        | self.IsI64():
            ret self.ReadI64() == x.AsI64()
        | self.IsU64():
            ret self.ReadU64() == x.AsU64()
        | self.IsF64():
            ret self.ReadF64() == x.AsF64()
        |:
            ret false
        }
    }

    // Reports whether self less than x.
    // Returns false if type is unsupported by operation.
    //
    // Supported types are:
    //  - strings
    //  - 64-bit signed integer
    //  - 64-bit unsigned integer
    //  - 64-bit floating-point
    fn Lt(self, x: Const): bool {
        match {
        | self.IsStr():
            ret x.IsStr() && self.ReadStr() < x.ReadStr()
        | self.IsI64():
            ret self.ReadI64() < x.AsI64()
        | self.IsU64():
            ret self.ReadU64() < x.AsU64()
        | self.IsF64():
            ret self.ReadF64() < x.AsF64()
        |:
            ret false
        }
    }

    // Reports whether self less than or equals to x.
    // Returns false if type is unsupported by operation.
    //
    // Supported types are:
    //  - strings
    //  - 64-bit signed integer
    //  - 64-bit unsigned integer
    //  - 64-bit floating-point
    fn LtEq(self, x: Const): bool {
        match {
        | self.IsStr():
            ret x.IsStr() && self.ReadStr() <= x.ReadStr()
        | self.IsI64():
            ret self.ReadI64() <= x.AsI64()
        | self.IsU64():
            ret self.ReadU64() <= x.AsU64()
        | self.IsF64():
            ret self.ReadF64() <= x.AsF64()
        |:
            ret false
        }
    }

    // Reports whether self greater than x.
    // Returns false if type is unsupported by operation.
    //
    // Supported types are:
    //  - strings
    //  - 64-bit signed integer
    //  - 64-bit unsigned integer
    //  - 64-bit floating-point
    fn Gt(self, x: Const): bool {
        match {
        | self.IsStr():
            ret x.IsStr() && self.ReadStr() > x.ReadStr()
        | self.IsI64():
            ret self.ReadI64() > x.AsI64()
        | self.IsU64():
            ret self.ReadU64() > x.AsU64()
        | self.IsF64():
            ret self.ReadF64() > x.AsF64()
        |:
            ret false
        }
    }

    // Reports whether self greater than or equals to x.
    // Returns false if type is unsupported by operation.
    //
    // Supported types are:
    //  - strings
    //  - 64-bit signed integer
    //  - 64-bit unsigned integer
    //  - 64-bit floating-point
    fn GtEq(self, x: Const): bool {
        match {
        | self.IsStr():
            ret x.IsStr() && self.ReadStr() >= x.ReadStr()
        | self.IsI64():
            ret self.ReadI64() >= x.AsI64()
        | self.IsU64():
            ret self.ReadU64() >= x.AsU64()
        | self.IsF64():
            ret self.ReadF64() >= x.AsF64()
        |:
            ret false
        }
    }

    //
    // Ops
    //

    // Adds x's value to itself value.
    // Reports whether operation is success.
    fn Add(mut self, x: Const): bool {
        match {
        | self.IsStr():
            if !x.IsStr() {
                ret false
            }
            self.SetStr(self.ReadStr() + x.ReadStr())
        | self.IsF64():
            self.SetF64(self.ReadF64() + x.AsF64())
        | self.IsI64():
            if x.IsF64() {
                self.SetF64(self.AsF64() + x.ReadF64())
            } else {
                self.SetI64(self.ReadI64() + x.AsI64())
            }
        | self.IsU64():
            if x.IsF64() {
                self.SetF64(self.AsF64() + x.ReadF64())
            } else {
                self.SetU64(self.ReadU64() + x.AsU64())
            }
        |:
            ret false
        }
        ret true
    }

    // Subs x's value from itself value.
    // Reports whether operation is success.
    fn Sub(mut self, x: Const): bool {
        match {
        | self.IsF64():
            self.SetF64(self.ReadF64() - x.AsF64())
        | self.IsI64():
            if x.IsF64() {
                self.SetF64(self.AsF64() - x.ReadF64())
            } else {
                self.SetI64(self.ReadI64() - x.AsI64())
            }
        | self.IsU64():
            if x.IsF64() {
                self.SetF64(self.AsF64() - x.ReadF64())
            } else {
                self.SetU64(self.ReadU64() - x.AsU64())
            }
        |:
            ret false
        }
        ret true
    }

    // Multiplies x's value to c's value.
    // Reports whether operation is success.
    fn Mul(mut self, x: Const): bool {
        match {
        | self.IsF64():
            self.SetF64(self.ReadF64() * x.AsF64())
        | self.IsI64():
            if x.IsF64() {
                self.SetF64(self.AsF64() * x.ReadF64())
            } else {
                self.SetI64(self.ReadI64() * x.AsI64())
            }
        | self.IsU64():
            if x.IsF64() {
                self.SetF64(self.AsF64() * x.ReadF64())
            } else {
                self.SetU64(self.ReadU64() * x.AsU64())
            }
        |:
            ret false
        }
        ret true
    }

    // Divides itself value to x's value.
    // Reports whether operation is success.
    // Reports false if divided-by-zero.
    //
    // NOTICE
    //  This operation makes constant value is floating-point.
    fn Div(mut self, x: Const): bool {
        match {
        | self.IsF64():
            let l = x.AsF64()
            if l == 0 {
                ret false
            }
            self.SetF64(self.ReadF64() / l)
        | self.IsI64():
            let l = x.AsF64()
            if l == 0 {
                ret false
            }
            self.SetF64(self.AsF64() / l)
        | self.IsU64():
            let l = x.AsF64()
            if l == 0 {
                ret false
            }
            self.SetF64(self.AsF64() / l)
        |:
            ret false
        }
        ret true
    }

    // Mods itself value to x's value.
    // Reports whether operation is success.
    // Reports false if divided-by-zero.
    fn Mod(mut self, x: Const): bool {
        match {
        | self.IsI64():
            let l = x.AsI64()
            if l == 0 {
                ret false
            }
            self.SetI64(self.ReadI64() % l)
        | self.IsU64():
            let l = x.AsU64()
            if l == 0 {
                ret false
            }
            self.SetU64(self.ReadU64() % l)
        |:
            ret false
        }
        ret true
    }

    // Bitwise and itself value to x's value.
    // Reports whether operation is success.
    fn BitwiseAnd(mut self, x: Const): bool {
        match {
        | self.IsI64():
            self.SetI64(self.ReadI64() & x.AsI64())
        | self.IsU64():
            self.SetU64(self.ReadU64() & x.AsU64())
        |:
            ret false
        }
        ret true
    }

    // Bitwise or itself value to x's value.
    // Reports whether operation is success.
    fn BitwiseOr(mut self, x: Const): bool {
        match {
        | self.IsI64():
            self.SetI64(self.ReadI64() | x.AsI64())
        | self.IsU64():
            self.SetU64(self.ReadU64() | x.AsU64())
        |:
            ret false
        }
        ret true
    }

    // Bitwise xor itself value to x's value.
    // Reports whether operation is success.
    fn Xor(mut self, x: Const): bool {
        match {
        | self.IsI64():
            self.SetI64(self.ReadI64() ^ x.AsI64())
        | self.IsU64():
            self.SetU64(self.ReadU64() ^ x.AsU64())
        |:
            ret false
        }
        ret true
    }

    // Left shifts itself value to x's value.
    // Reports whether operation is success.
    fn Lshift(mut self, x: Const): bool {
        match {
        | self.IsI64():
            let l = self.AsI64()
            if l < 0 {
                let r = x.AsU64()
                self.SetI64(l << r)
            } else {
                lshiftUnsig(self, x)
            }
        | self.IsU64():
            lshiftUnsig(self, x)
        |:
            ret false
        }
        ret true
    }

    // Right shifts itself value to x's value.
    // Reports whether operation is success.
    fn Rshift(mut self, x: Const): bool {
        match {
        | self.IsI64():
            let l = self.AsI64()
            if l < 0 {
                let r = x.AsU64()
                self.SetI64(l >> r)
            } else {
                rshiftUnsig(self, x)
            }
        | self.IsU64():
            rshiftUnsig(self, x)
        |:
            ret false
        }
        ret true
    }
}

fn lshiftUnsig(mut &c: Const, &x: Const) {
    let l = c.AsU64()
    let r = x.AsU64()
    c.SetU64(l << r)
}

fn rshiftUnsig(mut &c: Const, &x: Const) {
    let l = c.AsU64()
    let r = x.AsU64()
    c.SetU64(l >> r)
}