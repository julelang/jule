// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/strings"
use "std/unicode"
use "std/unicode/utf8"

// Punctuations.
static Puncts: [...]rune = [
	'!',
	'#',
	'$',
	',',
	'.',
	'\'',
	'"',
	':',
	';',
	'<',
	'>',
	'=',
	'?',
	'-',
	'+',
	'*',
	'(',
	')',
	'[',
	']',
	'{',
	'}',
	'%',
	'&',
	'/',
	'\\',
	'@',
	'^',
	'_',
	'`',
	'|',
	'~',
	'Â¦',
]

// Space characters.
static Spaces: [...]rune = [
	' ',
	'\t',
	'\v',
	'\r',
	'\n',
]

// Kind list of unary operators.
static UnaryOps: [...]int = [
	Minus,
	Plus,
	Caret,
	Excl,
	Star,
	Amper,
]

// Kind list of binary operators.
static BinOps: [...]int = [
	Plus,
	Minus,
	Star,
	Solidus,
	Percent,
	Amper,
	Vline,
	Caret,
	Shl,
	Shr,
	Lt,
	Gt,
	LtEq,
	GtEq,
	DblAmper,
	DblVline,
	DblEq,
	NotEq,
]

// Kind list of weak operators.
// These operators are weak, can used as part of expression.
static WeakOps: [...]int = [
	TripleDot,
	Colon,
]

// List of postfix operators.
static PostfixOps: [...]int = [
	DblPlus,
	DblMinus,
]

// List of assign operators.
static AssignOps: [...]int = [
	Eq,
	PlusEq,
	MinusEq,
	SolidusEq,
	StarEq,
	PercentEq,
	ShrEq,
	ShlEq,
	VlineEq,
	AmperEq,
	CaretEq,
]

// Token identities.
const (
	Illegal = iota
	Name
	Ret
	Semicolon
	Lit
	Comma
	Const
	Type
	Colon
	For
	Break
	Cont
	In
	If
	Else
	Comment
	Use
	Dot
	Goto
	DblColon
	Enum
	Struct
	Co
	Match
	Self
	Trait
	Impl
	Chan
	Cpp
	Fall
	Fn
	Let
	Unsafe
	Mut
	Defer
	Static
	Hash
	Error
	Map
	ColonEq
	TripleDot
	PlusEq
	MinusEq
	StarEq
	SolidusEq
	PercentEq
	ShlEq
	ShrEq
	CaretEq
	AmperEq
	VlineEq
	DblEq
	NotEq
	GtEq
	LtEq
	DblAmper
	DblVline
	Shl
	Shr
	DblPlus
	DblMinus
	Plus
	Minus
	Star
	Solidus
	Percent
	Amper
	Vline
	Caret
	Excl
	Lt
	Gt
	Eq
	LBrace
	RBrace
	LParent
	RParent
	LBracket
	RBracket
	RArrow
	Select
)

// Token is lexer token.
struct Token {
	File:   &Fileset
	Row:    int
	Column: int
	Kind:   str
	Id:     int
}

impl Token {
	// Returns operator precedence of token.
	// Returns 0 if token is not operator or
	// invalid operator for operator precedence.
	//
	// Accepts assignment tokens (like equals [=]) as precedenced operator
	// to handle expression assignments.
	fn Prec(self): byte {
		match self.Id {
		| Star
		| Percent
		| Solidus
		| Shr
		| Shl
		| Amper:
			ret 5
		| Plus
		| Minus
		| Vline
		| Caret:
			ret 4
		| DblEq
		| NotEq
		| Eq
		| Lt
		| LtEq
		| Gt
		| GtEq:
			ret 3
		| DblAmper:
			ret 2
		| DblVline:
			ret 1
		|:
			ret 0
		}
	}
}

// Reports whether kind is unary operator.
fn IsUnaryOp(id: int): bool {
	for _, op in UnaryOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether kind is binary operator.
fn IsBinOp(id: int): bool {
	for _, op in BinOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether kind is weak operator.
fn IsWeakOp(id: int): bool {
	for _, op in WeakOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether kind is string literal.
fn IsStr(k: str): bool { ret k != "" && (k[0] == '"' || IsRawStr(k)) }

// Reports whether kind is raw string literal.
fn IsRawStr(k: str): bool { ret k != "" && k[0] == '`' }

// Reports whether kind is rune literal.
// Literal value can be byte or rune.
fn IsRune(k: str): bool { ret k != "" && k[0] == '\'' }

// Reports whether kind is nil literal.
fn IsNil(k: str): bool { ret k == "nil" }

// Reports whether kind is boolean literal.
fn IsBool(k: str): bool { ret k == "true" || k == "false" }

// Reports whether kind is float.
fn IsFloat(k: str): bool {
	if strings::HasPrefix(k, "0x") {
		ret strings::ContainsAny(k, ".pP")
	}
	ret strings::ContainsAny(k, ".eE")
}

// Reports whether kind is numeric.
fn IsNum(k: str): bool {
	if k == "" {
		ret false
	}

	b := k[0]
	ret b == '.' || ('0' <= b && b <= '9')
}

// Reports whether kind is literal.
fn IsLit(k: str): bool {
	ret IsNum(k) || IsStr(k) || IsRune(k) || IsNil(k) || IsBool(k)
}

// Reports whether rune is punctuation.
fn IsPunct(r: rune): bool {
	for _, cr in Puncts {
		if r == cr {
			ret true
		}
	}
	ret false
}

// Reports whether rune is whitespace.
fn IsSpace(r: rune): bool {
	for _, cr in Spaces {
		if r == cr {
			ret true
		}
	}
	ret false
}

// Reports whether rune is letter.
fn IsLetter(r: rune): bool {
	ret unicode::IsLetter(r)
}

// Reports whether first rune of string is allowed
// to first rune for identifier.
fn IsNameRune(s: str): bool {
	if s == "" {
		ret false
	}
	if s[0] != '_' {
		r, _ := utf8::DecodeRuneStr(s)
		if !IsLetter(r) {
			ret false
		}
	}
	ret true
}

// Reports whether s is keyword.
fn IsKeyword(s: str): bool {
	for _, op in keywords {
		if s == op.kind {
			ret true
		}
	}
	ret false
}

// Reports whether rune is decimal sequence.
fn IsDecimal(r: rune): bool { ret '0' <= r && r <= '9' }

// Reports whether rune is binary sequence.
fn IsBinary(r: rune): bool { ret r == '0' || r == '1' }

// Reports whether rune is octal sequence.
fn IsOctal(r: rune): bool { ret '0' <= r && r <= '7' }

// Reports whether rune is hexadecimal sequence.
fn IsHex(r: rune): bool {
	match {
	| '0' <= r && r <= '9':
		ret true
	| 'a' <= r && r <= 'f':
		ret true
	| 'A' <= r && r <= 'F':
		ret true
	|:
		ret false
	}
}

// Reports given token id is allow for
// assignment left-expression or not.
fn IsAssign(id: int): bool {
	ret (id == Name ||
		id == Cpp ||
		id == Let ||
		id == Mut ||
		id == Self ||
		id == LParent ||
		id == Star ||
		id == Amper)
}

// Reports whether operator kind is postfix operator.
fn IsPostfixOp(id: int): bool {
	for _, op in PostfixOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether operator kind is assignment operator.
fn IsAssignOp(id: int): bool {
	if IsPostfixOp(id) {
		ret true
	}
	for _, op in AssignOps {
		if id == op {
			ret true
		}
	}
	ret false
}