// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/strings"
use "std/unicode"
use "std/unicode/utf8"

// Punctuations.
static Puncts: [...]rune = [
	'!',
	'#',
	'$',
	',',
	'.',
	'\'',
	'"',
	':',
	';',
	'<',
	'>',
	'=',
	'?',
	'-',
	'+',
	'*',
	'(',
	')',
	'[',
	']',
	'{',
	'}',
	'%',
	'&',
	'/',
	'\\',
	'@',
	'^',
	'_',
	'`',
	'|',
	'~',
	'Â¦',
]

// Space characters.
static Spaces: [...]rune = [
	' ',
	'\t',
	'\v',
	'\r',
	'\n',
]

// Kind list of unary operators.
static UnaryOps: [...]Id = [
	Id.Minus,
	Id.Plus,
	Id.Caret,
	Id.Excl,
	Id.Star,
	Id.Amper,
]

// Kind list of binary operators.
static BinOps: [...]Id = [
	Id.Plus,
	Id.Minus,
	Id.Star,
	Id.Solidus,
	Id.Percent,
	Id.Amper,
	Id.Vline,
	Id.Caret,
	Id.Shl,
	Id.Shr,
	Id.Lt,
	Id.Gt,
	Id.LtEq,
	Id.GtEq,
	Id.DblAmper,
	Id.DblVline,
	Id.Eqs,
	Id.NotEq,
]

// Kind list of weak operators.
// These operators are weak, can used as part of expression.
static WeakOps: [...]Id = [
	Id.TripleDot,
	Id.Colon,
]

// List of postfix operators.
static PostfixOps: [...]Id = [
	Id.DblPlus,
	Id.DblMinus,
]

// List of assign operators.
static AssignOps: [...]Id = [
	Id.Eq,
	Id.PlusEq,
	Id.MinusEq,
	Id.SolidusEq,
	Id.StarEq,
	Id.PercentEq,
	Id.ShrEq,
	Id.ShlEq,
	Id.VlineEq,
	Id.AmperEq,
	Id.CaretEq,
]

// Special identifiers.
enum Ident: str {
	Ignore: "_",         // Ignore
	Anon: "<anonymous>", // Anonymous
}

// Token identities.
enum Id: uint {
	NA,
	Ident,
	Ret,
	Semicolon,
	Lit,
	Comma,
	Const,
	Type,
	Colon,
	For,
	Break,
	Cont,
	In,
	If,
	Else,
	Comment,
	Use,
	Dot,
	Goto,
	DblColon,
	Enum,
	Struct,
	Co,
	Match,
	Self,
	Trait,
	Impl,
	Cpp,
	Fall,
	Fn,
	Let,
	Unsafe,
	Mut,
	Defer,
	Static,
	Hash,
	Error,
	Map,
	ColonEq,
	TripleDot,
	PlusEq,
	MinusEq,
	StarEq,
	SolidusEq,
	PercentEq,
	ShlEq,
	ShrEq,
	CaretEq,
	AmperEq,
	VlineEq,
	Eqs,
	NotEq,
	GtEq,
	LtEq,
	DblAmper,
	DblVline,
	Shl,
	Shr,
	DblPlus,
	DblMinus,
	Plus,
	Minus,
	Star,
	Solidus,
	Percent,
	Amper,
	Vline,
	Caret,
	Excl,
	Lt,
	Gt,
	Eq,
	LBrace,
	RBrace,
	LParent,
	RParent,
	LBracket,
	RBracket,
}

// Token kinds.
enum Kind: str {
	DblColon: "::",
	Colon: ":",
	Semicolon: ";",
	Comma: ",",
	TripleDot: "...",
	Dot: ".",
	PlusEq: "+=",
	MinusEq: "-=",
	StarEq: "*=",
	SolidusEq: "/=",
	PercentEq: "%=",
	ShlEq: "<<=",
	ShrEq: ">>=",
	CaretEq: "^=",
	AmperEq: "&=",
	VlineEq: "|=",
	Eqs: "==",
	NotEq: "!=",
	GtEq: ">=",
	LtEq: "<=",
	DblAmper: "&&",
	DblVline: "||",
	Shl: "<<",
	Shr: ">>",
	DblPlus: "++",
	DblMinus: "--",
	Plus: "+",
	Minus: "-",
	Star: "*",
	Solidus: "/",
	Percent: "%",
	Amper: "&",
	Vline: "|",
	Caret: "^",
	Excl: "!",
	Lt: "<",
	Gt: ">",
	Eq: "=",
	ColonEq: ":=",
	LnComment: "//",
	RangLComment: "/*",
	RangRComment: "*/",
	LParent: "(",
	RParent: ")",
	LBracket: "[",
	RBracket: "]",
	LBrace: "{",
	RBrace: "}",
	Hash: "#",
	Const: "const",
	Ret: "ret",
	Type: "type",
	For: "for",
	Break: "break",
	Cont: "continue",
	In: "in",
	If: "if",
	Else: "else",
	Use: "use",
	Goto: "goto",
	Enum: "enum",
	Struct: "struct",
	Co: "co",
	Match: "match",
	Self: "self",
	Trait: "trait",
	Impl: "impl",
	Cpp: "cpp",
	Fall: "fall",
	Fn: "fn",
	Let: "let",
	Unsafe: "unsafe",
	Mut: "mut",
	Defer: "defer",
	Static: "static",
	Error: "error",
	Map: "map",
}

// Token is lexer token.
struct Token {
	File:   &Fileset
	Row:    int
	Column: int
	Kind:   str
	Id:     Id
}

impl Token {
	// Returns operator precedence of token.
	// Returns 0 if token is not operator or
	// invalid operator for operator precedence.
	//
	// Accepts assignment tokens (like equals [=]) as precedenced operator
	// to handle expression assignments.
	fn Prec(self): byte {
		match self.Id {
		| Id.Star
		| Id.Percent
		| Id.Solidus
		| Id.Shr
		| Id.Shl
		| Id.Amper:
			ret 5
		| Id.Plus
		| Id.Minus
		| Id.Vline
		| Id.Caret:
			ret 4
		| Id.Eqs
		| Id.NotEq
		| Id.Eq
		| Id.Lt
		| Id.LtEq
		| Id.Gt
		| Id.GtEq:
			ret 3
		| Id.DblAmper:
			ret 2
		| Id.DblVline:
			ret 1
		|:
			ret 0
		}
	}
}

// Reports whether kind is unary operator.
fn IsUnaryOp(id: Id): bool {
	for _, op in UnaryOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether kind is binary operator.
fn IsBinOp(id: Id): bool {
	for _, op in BinOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether kind is weak operator.
fn IsWeakOp(id: Id): bool {
	for _, op in WeakOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether kind is string literal.
fn IsStr(k: str): bool { ret k != "" && (k[0] == '"' || IsRawStr(k)) }

// Reports whether kind is raw string literal.
fn IsRawStr(k: str): bool { ret k != "" && k[0] == '`' }

// Reports whether kind is rune literal.
// Literal value can be byte or rune.
fn IsRune(k: str): bool { ret k != "" && k[0] == '\'' }

// Reports whether kind is nil literal.
fn IsNil(k: str): bool { ret k == "nil" }

// Reports whether kind is boolean literal.
fn IsBool(k: str): bool { ret k == "true" || k == "false" }

// Reports whether kind is float.
fn IsFloat(k: str): bool {
	if strings::HasPrefix(k, "0x") {
		ret strings::ContainsAny(k, ".pP")
	}
	ret strings::ContainsAny(k, ".eE")
}

// Reports whether kind is numeric.
fn IsNum(k: str): bool {
	if k == "" {
		ret false
	}

	b := k[0]
	ret b == '.' || ('0' <= b && b <= '9')
}

// Reports whether kind is literal.
fn IsLit(k: str): bool {
	ret IsNum(k) || IsStr(k) || IsRune(k) || IsNil(k) || IsBool(k)
}

// Reports whether identifier is ignore.
fn IsIgnoreIdent(ident: str): bool { ret ident == Ident.Ignore }

// Reports whether identifier is anonymous.
fn IsAnonIdent(ident: str): bool { ret ident == Ident.Anon }

// Reports whether rune is punctuation.
fn IsPunct(r: rune): bool {
	for _, cr in Puncts {
		if r == cr {
			ret true
		}
	}
	ret false
}

// Reports wheter byte is whitespace.
fn IsSpace(r: rune): bool {
	for _, cr in Spaces {
		if r == cr {
			ret true
		}
	}
	ret false
}

// Reports whether rune is letter.
fn IsLetter(r: rune): bool {
	ret unicode::IsLetter(r)
}

// Reports whether first rune of string is allowed
// to first rune for identifier.
fn IsIdentRune(s: str): bool {
	if s == "" {
		ret false
	}
	if s[0] != '_' {
		r, _ := utf8::DecodeRuneStr(s)
		if !IsLetter(r) {
			ret false
		}
	}
	ret true
}

// Reports whether s is keyword.
fn IsKeyword(s: str): bool {
	for _, op in keywords {
		if s == op.kind {
			ret true
		}
	}
	ret false
}

// Reports whether byte is decimal sequence.
fn IsDecimal(b: byte): bool { ret '0' <= b && b <= '9' }

// Reports whether byte is binary sequence.
fn IsBinary(b: byte): bool { ret b == '0' || b == '1' }

// Reports whether byte is octal sequence.
fn IsOctal(b: byte): bool { ret '0' <= b && b <= '7' }

// Reports whether byte is hexadecimal sequence.
fn IsHex(b: byte): bool {
	match {
	| '0' <= b && b <= '9':
		ret true
	| 'a' <= b && b <= 'f':
		ret true
	| 'A' <= b && b <= 'F':
		ret true
	|:
		ret false
	}
}

// Reports given token id is allow for
// assignment left-expression or not.
fn IsAssign(id: Id): bool {
	ret (id == Id.Ident ||
		id == Id.Cpp ||
		id == Id.Let ||
		id == Id.Mut ||
		id == Id.Self ||
		id == Id.LParent ||
		id == Id.Star ||
		id == Id.Amper)
}

// Reports whether operator kind is postfix operator.
fn IsPostfixOp(id: Id): bool {
	for _, op in PostfixOps {
		if id == op {
			ret true
		}
	}
	ret false
}

// Reports whether operator kind is assignment operator.
fn IsAssignOp(id: Id): bool {
	if IsPostfixOp(id) {
		ret true
	}
	for _, op in AssignOps {
		if id == op {
			ret true
		}
	}
	ret false
}