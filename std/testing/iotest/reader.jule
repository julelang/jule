// Copyright 2025 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.25.0/src/testing/iotest/reader.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
//
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

use "std/io"

// Returns a Reader that implements
// each non-empty Read by reading one byte from r.
fn OneByteReader(mut r: io::Reader): io::Reader { ret &oneByteReader{r} }

struct oneByteReader {
	r: io::Reader
}

impl io::Reader for oneByteReader {
	async fn Read(mut *self, mut p: []byte)!: int {
		if len(p) == 0 {
			ret 0
		}
		ret self.r.Read(p[0:1]).await?
	}
}

// Returns a Reader that implements Read
// by reading half as many requested bytes from r.
fn HalfReader(mut r: io::Reader): io::Reader { ret &halfReader{r} }

struct halfReader {
	r: io::Reader
}

impl io::Reader for halfReader {
	async fn Read(mut *self, mut p: []byte)!: int {
		ret self.r.Read(p[0 : (len(p)+1)/2]).await?
	}
}

// Changes the way errors are handled by a Reader. Normally, a
// Reader returns an error from the first Read call after the
// last piece of data is read. DataErrReader wraps a Reader and changes its
// behavior so the final error is returned along with the final data, instead
// of in the first call after the final data.
fn DataErrReader(mut r: io::Reader): io::Reader { ret &dataErrReader{r, nil, make([]byte, 1024)} }

struct dataErrReader {
	r:      io::Reader
	unread: []byte
	data:   []byte
}

impl io::Reader for dataErrReader {
	async fn Read(mut *self, mut p: []byte)!: (n: int) {
		// loop because first call needs two reads:
		// one to get data and a second to look for an error.
		n = io::EOF
		for {
			if len(self.unread) == 0 {
				mut n1 := self.r.Read(self.data).await?
				if n1 == io::EOF {
					self.unread = self.data[0:0]
					ret
				}
				self.unread = self.data[0:n1]
			}
			if n > 0 {
				break
			}
			n = copy(p, self.unread)
			self.unread = self.unread[n:]
		}
		ret
	}
}