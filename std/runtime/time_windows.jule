// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/sys"

let mut qpcfreq: i64 = 0

// Returns system-wide realtime clock in seconds and nanoseconds.
// The nanoseconds is not a second yet, always in range (0, 999999999).
fn timeNow(): (sec: i64, nsec: i64) {
	mut ft := sys::Filetime{}
	sys::GetSystemTimeAsFileTime(&ft)
	t := ft.Nanoseconds()
	sec = t / _Second
	nsec = t % _Second
	ret
}

fn nanotime(): i64 {
	ticks := sys::QueryPerformanceCounter() else {
		panic("runtime: nanotime failed")
	}
	sec := ticks / qpcfreq
	rem := ticks - sec*qpcfreq
	ret sec*1e9 + rem*1e9/qpcfreq
}

fn timeinit() {
	qpcfreq = sys::QueryPerformanceFrequency() else {
		panic("runtime: QPC frequency failed")
	}
}