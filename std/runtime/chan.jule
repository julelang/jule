// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Queue implementation using type T for channels that use type T.
// Provides read and write in FIFO order. Automatically instantiated by pchan.
// Designed for channels and should be used with caution. Not thread-safe by itself.
// Works with a capacity of N for buffered channels, and allocated spaces are reused,
// with each addition not resulting in a new allocation.
struct chanQueue[T] {
	// Preallocated buffer for the queue. It performs GC for memory safety.
	// Always len(buf) == cap(buf).
	buf: []T

	// Pointer to the first node of the buf.
	// Used for the direct access to the memory, to avoid bound checking cost.
	data: *T

	// Read and write offsets of the queue. The read offset always points to the
	// next node to be read and the write offset always points to the next free
	// node to be used for enqueuing.
	r: int
	w: int
}

impl chanQueue {
	// Creates a chanQueue and allocates nodes according to cap.
	// If cap >= 1, it also sets the last node. If cap >= 1,
	// the channel is created as buffered, and if cap == 0,
	// it is created as unbuffered channel.
	static fn new(cap: int): chanQueue[T] {
		mut q := chanQueue[T]{}
		if cap >= 1 {
			q.buf = make([]T, cap)
		} else {
			q.buf = make([]T, 1)
		}
		q.data = unsafe { (*sliceBase[T])(&q.buf).slice }
		ret q
	}

	// Removes the first node from the queue and returns the data.
	// Assumes that there is always data in the queue.
	fn dequeue(mut self): T {
		// Copy the data before resetting to default.
		mut data := unsafe { self.data[self.r] }
		unsafe {
			// Remove the data from the node. Since this is a data type that performs GC,
			// it will continue to exist until it is removed from the node.
			// Remove its reference from the queue to allow it to be freed if possible.
			let mut def: T
			self.data[self.r] = def
		}
		// Advance the read order.
		self.r++
		if self.r == len(self.buf) {
			self.r = 0
		}
		ret data
	}

	// Adds the data to the end of the queue.
	// If the channel is unbuffered, it always assumes the current data has been read.
	// If the channel is buffered, it always assumes there is free node to enqueue.
	fn enqueue(mut self, mut &data: T) {
		// Write data to the next available node in the queue.
		unsafe { self.data[self.w] = data }
		// Advance the write order.
		self.w++
		if self.w == len(self.buf) {
			self.w = 0
		}
	}
}

// Channel state flags.
const chanClosed = 0b01
const chanBuffered = 0b10

// Shared section of channel implementation.
// Regardless of the generic types, each channel initially contains these fields.
// To abstract away from the generic types and access the internal data in a
// shared, type-independent manner, may used with Unsafe Jule.
// It should not be copied.
struct hchan {
	lock:  fmutex
	cap:   int
	len:   int
	state: u32
}

// The channel implementation of the language. The fields are
// structured according to hchan. The generic type represents the
// data type of the channel. Instances required at compile-time are
// automatically instantiated by the compiler. Any channel algorithms that
// require generic types should be defined under this structure.
// Type-independent shared algorithms, like hchan, should be defined
// outside of this structure. A pchan should not be copied after being used.
// The compiler creates channels in the background using the [pchan[T].new]
// static method. Behind the scenes, each channel is treated as a smart pointer.
struct pchan[T] {
	lock:  fmutex
	cap:   int
	len:   int
	state: u32
	queue: chanQueue[T]
}

impl pchan {
	// Creates a new channel.
	// If cap >= 1, it is initialized as a buffered channel,
	// otherwise, it is initialized as an unbuffered channel.
	// A negative value for cap will cause a panic.
	static fn new(cap: int): &pchan[T] {
		mut ch := new(pchan[T])
		if cap < 0 {
			panic("runtime: invalid channel buffer size, it was <0")
		}
		ch.cap = cap
		ch.queue = chanQueue[T].new(ch.cap)
		if ch.cap > 0 {
			ch.state |= chanBuffered
		}
		ret ch
	}

	// Returns the channel as a &hchan for general sharing of pchan[T].
	// The returned &hchan does not exhibit GC behavior and can be used until
	// the self pointer is freed, after which it should not be used.
	fn hchan(mut self): &hchan {
		ret unsafe { (&hchan)((*hchan)(&self)) }
	}

	// Closes the channel.
	fn close(mut self) {
		self.lock.lock()
		self.state |= chanClosed
		self.lock.unlock()
	}

	// Sends the data to the channel.
	// If owned is true, assumes lock is already owned by the current caller.
	fn send(mut self, mut data: T, owned: bool) {
		if !owned {
			self.lock.lock()
		}
		if self.state&chanBuffered == chanBuffered {
			// If the capacity is completely consumed, wait until space becomes available.
			// When some data is received, there will be space in the queue to write.
			chanpark(self.hchan(), self.len, self.cap, reasonSend)
			if self.state&chanClosed == chanClosed {
				panic("runtime: send on closed channel")
			}
			self.queue.enqueue(data)
			self.len++
		} else {
			one := 1
			// If there is data waiting to be received, wait until it is received.
			chanpark(self.hchan(), self.len, one, reasonSend)
			if self.state&chanClosed == chanClosed {
				panic("runtime: send on closed channel")
			}
			self.queue.enqueue(data)
			self.len++
			// Wait until the length becomes zero.
			// This means the data has been received.
			chanpark(self.hchan(), self.len, one, reasonSend)
		}
		self.lock.unlock()
	}

	// Receives the data from the channel.
	// The |ok| is the reference that points to boolean and
	// it reports whether data dequeued and received successfully.
	// If owned is true, assumes lock is already owned by the current caller.
	fn recv(mut self, mut &ok: bool, owned: bool): T {
		// References are safe, but this method may used by the compiler unsafely.
		// And we have to support nil references for such a low-level use.
		// So, If the |ok| reference not implemented as is nil raw pointer
		// by the compiler, set it. Otherwise, do not touch it. Be safe.
		//
		// To determine whether |ok| is nil raw pointer behind the scene,
		// take raw pointer and compare it with nil. Since taking raw pointer of
		// the references will result as actual pointer of them, we can determine
		// whether the reference's raw pointer is nil.
		if !owned {
			self.lock.lock()
		}
		zero := 0
		// If there is no data waiting to be received in the queue, wait until there is.
		chanpark(self.hchan(), zero, self.len, reasonRecv)
		// chanpark returns when the condition met or channel has been closed.
		// An unbuffered channel cannot have a queue, so its length always varies
		// between 0 and 1. If there is no data to receive, the only possibility is
		// that the channel has been closed. If there is data in the queue,
		// a buffered channel should be able to receive this data, whether
		// the channel is closed or open. In this case, we can determine whether
		// the channel is closed and whether there is any data by simply checking
		// its length. If the length has reached zero, it means the channel is
		// closed and there is no data left in the queue.
		if self.len == 0 {
			// Since the channel is no longer fully functional, set the buffer
			// to nil. This drops references to the relevant allocations without
			// waiting for the channel to go out of scope, making it easier
			// to gain used memory back efficiently. Helps to reduce memory consumption.
			self.queue.buf = nil
			self.queue.data = nil
			self.lock.unlock()
			if &ok != nil {
				ok = false
			}
			let mut def: T
			ret def
		}
		mut data := self.queue.dequeue()
		self.len--
		self.lock.unlock()
		if &ok != nil {
			ok = true
		}
		ret data
	}
}

// The channel halts execution until the required condition is met.
// ch represents the channel, n is the field data used for comparison,
// and c indicates the value that n should be smaller than.
// This function must be called with the channel lock held by this thread.
// Even if the condition is not met, it will return if the channel is in a closed state.
fn chanpark(ch: &hchan, &n: int, &c: int, mut reason: u32) {
	reason |= reasonStrict
	for {
		if ch.state&chanClosed == chanClosed || n < c {
			break
		}
		// The lock is acquired. This means no changes can occur in the channel.
		// We must release the lock and immediately switch to a different thread.
		yield(uintptr(&ch.lock), reason)
		reason &= ^reasonStrict
		// After the thread wakes up, we must acquire the lock
		// before reaching the critical section again.
		ch.lock.lock()
	}
}

// Reports whether channel can receive data.
// Will locks the mutex, but will not release.
fn chanCanRecv(&ch: hchan): (r: bool) {
	ch.lock.lock()
	r = ch.len > 0
	ret
}

// Reports whether channel can send data.
// Will locks the mutex, but will not release.
fn chanCanSend(&ch: hchan): (r: bool) {
	ch.lock.lock()
	if ch.state&chanBuffered == chanBuffered {
		r = ch.len < ch.cap
	} else {
		r = ch.len == 0
	}
	ret
}

// Candidate lookup threshold for non-empty select statements before yield CPU.
const selectThreshold = 5

// Select statement implementation for blocking and non-blocking select.
// If the block is true, behavior is blocking select, otherwise unblocking select.
// The chans should point to an array with a size equal to totalChans.
// All receive cases should came first, then send cases. The recvChans should be
// equal to length of the leading receive cases in the chans.
// If any case selected, returns the index of selected candidate case by chans.
// Otherwise returns -1 which means no selected case. It only appears for non-blocking selects.
// The channel's mutex will be locked already for the selected case.
// For empty select statement, chans should be nil.
unsafe fn chanSelect(chans: *&hchan, totalChans: int, recvChans: int, block: bool): int {
	// Empty or blocking-select statement.
	if block || chans == nil {
		mut t := getCurrentThread()
		threadMutex.lock()
		// Add special case for empty select.
		if chans == nil {
			threadCases |= threadSC_EmptySelect
		}
		// Set thread state as suspended with no reason.
		// We do not need to frame analysis for this thread.
		// If we enable the frame analysis for this thread, deadlock analysis caught
		// any deadlock so slow because we will put this thread into deep sleep.
		// So do not enable frame analysis, use zero frame count. So we can caught
		// deadlock immediately if this thread is the only thread. In other cases,
		// other threads will caught any deadlock if occurs.
		t.state |= threadSuspended | reasonSelect
		if chans == nil {
			t.state |= reasonSelectEmpty
		}
		t.frame = 0
		// Check deadlock before sleep and release mutex.
		checkDeadlock(0, reasonSelect)
		threadMutex.unlock()
		// Empty select statement. Yield CPU indefinitely.
		if chans == nil {
			// Put thread into sleep for a hour.
			// We do not need to yield CPU, this thread will never continue to run.
			// It always yields CPU, so put it into deep sleep.
			// Other threads can caught deadlocks, if any, after this stage.
			// If this thread is the single thread, we already checked deadlocks.
			// So put this thread into deep sleep indefinitely, do not waste CPU with fast cycles.
			for {
				sleep(1 * _Hour)
			}
			panic("unreachable")
		}
	}
	mut it := 0 // Iteration count.
	// We have channels for cases, evaluate and choice a candidate.
	mut candidates := make([]&hchan, 0, totalChans)
Select:
	mut ch := *(&chans)
	mut tch := totalChans
	mut rch := recvChans
	// Determine receive candidates.
	tch -= rch
	for rch > 0; rch, ch = rch-1, ch+1 {
		if *ch != nil {
			if chanCanRecv(**ch) {
				candidates = append(candidates, *ch)
			} else {
				// Release the mutex, this channel is not a candidate.
				(*ch).lock.unlock()
			}
		}
	}
	// Determine send candidates.
	for tch > 0; tch, ch = tch-1, ch+1 {
		if *ch != nil {
			if chanCanSend(**ch) {
				candidates = append(candidates, *ch)
			} else {
				// Release the mutex, this channel is not a candidate.
				(*ch).lock.unlock()
			}
		}
	}

	// No candidate found.
	if len(candidates) == 0 {
		// This is a blocking-select but we have not any candidate.
		// So select until we have at least 1 eligible channel.
		if block {
			// Increase iteration count and check the value.
			// If we iterated many times to find a candidate,
			// stop wasting CPU cycles with fast iterations.
			// Check deadlock risk and then yield the CPU.
			it++
			if it < selectThreshold {
				threadMutex.lock()
				checkDeadlock(0, reasonNA)
				threadMutex.unlock()
				osyield()
				it = 0 // Reset iteration count for the next threshold.
			}
			// Try again to find a candidate and select it.
			goto Select
		}
		// Non-blocking select, return as -1.
		ret -1
	}
	// Select a candidate.
	mut i := 0
	if len(candidates) > 1 {
		// There is more than one candidate channels.
		// Select candidate randomly.
		i = int(rand() % u64(len(candidates)))
	}
	candidateChan := candidates[i]

	// Find selected candidate in chans.
	// Because selected candidate index may not be equal to chanmap offset of candidate.
	// So map the selected candidate index to chanmap index for correct selection.
	// Respond candidate offset by chanmap, not candidatemap.
	ch = *(&chans)
	i = 0
	for *ch != candidateChan {
		i++
		ch++
	}

	// Release all mutexes of candidates except selected one.
	// Other candidates can be free now.
	for _, c in candidates {
		if c != nil && c != candidateChan {
			(*c).lock.unlock()
		}
	}

	ret i
}