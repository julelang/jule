// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/sys"

extern let CreateThread: *unsafe
extern let GetCurrentThreadId: *unsafe
extern let SwitchToThread: *unsafe

// Wrapper for operating system thread.
struct osthread {
	id:     _DWORD
	handle: *unsafe
}

impl osthread {
	// Reports whether the threads are equal.
	fn equal(*self, other: _DWORD): bool {
		ret self.id == other
	}
}

// A low level API function for threads.
// It doesn't provide much abstraction.
// It just creates and detaches a thread using API.
// Reports whether the thread created successfully.
// The created thread is a native-thread.
// Assumes the thread-mutex is already locked and will be released before return.
// The |func| parameter should point to the valid function for operating system thread API.
// The |args| parameter may be nil, passed to the function when call.
// The thread data, should be fit into the threadData struct.
// So, the head fields of the thread data should be matched fields of the threadData.
unsafe fn threadSpawn(mut proc: &coroproc, func: *unsafe, mut args: *unsafe): bool {
	mut t := pushNewThread()
	t.proc = proc
	t.os.handle = sys::Addrcall[*unsafe](uintptr(extern.CreateThread),
		(*unsafe)(nil), int(0), func, args, _DWORD(0), &t.os.id)
	if t.os.handle == nil {
		threadMutex.unlock()
		ret false
	}
	threadMutex.unlock()
	sys::CloseHandle(sys::Handle(t.os.handle))!
	ret true
}

fn currentThreadID(): _DWORD {
	ret sys::Addrcall[_DWORD](uintptr(extern.GetCurrentThreadId))
}

fn osyield() {
	sys::Addrcall(uintptr(extern.SwitchToThread))
}

fn _threadinit() {
	// Set numcpu.
	{
		let mut sysinfo: systeminfo
		_GetSystemInfo(&sysinfo)
		numcpu = int(sysinfo.dwnumberofprocessors)
		if numcpu == 0 {
			numcpu = 1
		}
	}
	// Push the main thread to threads.
	// See the documentation of the pushNewThread function.
	// The |threads| should be initialized here, because compiler will not do it.
	// We do not have to lock the mutex for threads, no risk for concurrency.
	mut t := newThread()
	t.os.id = currentThreadID()
	threadHead = t
	threadTail = t
}