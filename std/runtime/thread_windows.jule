// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use integ "std/jule/integrated"
use "std/sys"

cpp unsafe fn CreateThread(*unsafe, int, *unsafe, *unsafe, int, *unsafe): *unsafe
cpp fn GetCurrentThreadId(): _DWORD
cpp fn SwitchToThread(): bool

// Wrapper for operating system thread.
struct osthread {
	id:     _DWORD
	handle: *unsafe
}

impl osthread {
	// Reports whether the threads are equal.
	fn equal(self, other: _DWORD): bool {
		ret self.id == other
	}
}

// Common head fields for a thread data.
struct threadData {
	handle: **unsafe
}

// A low level API function for threads.
// It doesn't provide much abstraction.
// It just creates and detaches a thread using API.
// Reports whether the thread created successfully.
// The created thread is a native-thread.
// The |func| parameter should point to the valid function for operating system thread API.
// The |args| parameter may be nil and should point to the thread data.
// The thread data, should be fit into the threadData struct.
// So, the head fields of the thread data should be matched fields of the threadData.
#export "__jule_coSpawn"
unsafe fn coSpawn(func: *unsafe, mut args: *unsafe): bool {
	mut t := pushNewThread()
	(*threadData)(args).handle = &t.os.handle
	t.os.handle = cpp.CreateThread(
		nil,
		0,
		integ::Emit[*unsafe]("(unsigned long(*)(void*))({})", func),
		args,
		0,
		integ::Emit[*unsafe]("(LPDWORD)({})", &t.os.id))
	if t.os.handle == nil {
		ret false
	}
	threadMutex.unlock()
	sys::CloseHandle(sys::Handle(t.os.handle))
	ret true
}

fn currentThreadID(): _DWORD {
	ret cpp.GetCurrentThreadId()
}

fn osyield() {
	cpp.SwitchToThread()
}

// Reserved function for hard initialization.
// This function will be called at earlies stages of the program;
// Before initialization of global variables, and calling other initializer functions.
// This function must initialize critical data of the typical Jule program.
fn __init() {
	// Set numcpu.
	{
		let info: cpp.SYSTEM_INFO
		unsafe { cpp.GetSystemInfo(&info) }
		numcpu = int(info.dwNumberOfProcessors)
	}
	// Push the main thread to threads.
	// See the documentation of the pushNewThread function.
	// The |threads| should be initialized here, because compiler will not do it.
	// We do not have to lock the mutex for threads, no risk for concurrency.
	mut t := newThread()
	t.os.id = currentThreadID()
	threads = t
}