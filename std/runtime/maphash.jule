// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/comptime"
use integ "std/jule/integrated"

const (
	// Size of a pointer in bytes - mem::SizeOf(uintptr(0)) but as an ideal constant.
	// It is also the size of the machine's native word size (that is, 4 on 32-bit systems, 8 on 64-bit).
	_ptrSize = 4 << (^uintptr(0) >> 63)

	c0 = uintptr((8-_ptrSize)/4*2860486313 + (_ptrSize-4)/4*33054211828000289)
	c1 = uintptr((8-_ptrSize)/4*3267000013 + (_ptrSize-4)/4*23344194077549503)
)

// NOTE: Because NaN != NaN, a map can contain any
// number of (mostly useless) entries keyed with NaNs.
// To avoid long hash chains, we assign a random number
// as the hash value for a NaN.

fn f32hash(f: f32, seed: uintptr): uintptr {
	match {
	| f == 0:
		ret c1 * (c0 ^ seed) // +0, -0
	| f != f:
		ret c1 * (c0 ^ seed ^ uintptr(rand())) // any kind of NaN
	|:
		ret unsafe { memhash(&f, seed, 4) }
	}
}

fn f64hash(f: f64, seed: uintptr): uintptr {
	match {
	| f == 0:
		ret c1 * (c0 ^ seed) // +0, -0
	| f != f:
		ret c1 * (c0 ^ seed ^ uintptr(rand())) // any kind of NaN
	|:
		ret unsafe { memhash(&f, seed, 8) }
	}
}

fn boolhash(b: bool, seed: uintptr): uintptr {
	if b {
		ret seed + 1
	}
	ret seed
}

fn structhash[S](s: S, mut seed: uintptr): uintptr {
	const v = comptime::ValueOf(s)
	const for i, field in v.Type().Decl().Fields() {
		const match {
		| field.Name() != "_":
			seed = maphash(v.FieldByIndex(i).Unwrap(), seed)
		}
	}
	ret seed
}

fn arrayhash[A](a: A, mut seed: uintptr): uintptr {
	for _, e in a {
		seed = maphash(e, seed)
	}
	ret seed
}

fn strhash(s: str, seed: uintptr): uintptr {
	unsafe {
		sp := (*strBase)(&s)
		ret memhash(sp.slice, uintptr(sp.len), seed)
	}
}

fn i32hash(i: u32, seed: uintptr): uintptr {
	ret unsafe { memhash32(&i, seed) }
}

fn i64hash(i: u64, seed: uintptr): uintptr {
	ret unsafe { memhash64(&i, seed) }
}

// Common hash function for dynamic types with underlying smart pointer type.
// It should be same as pointer hashing.
//
// Exported as private non-usual API to escape from deadcode optimizations.
#export "__jule_priv_dynhash"
fn dynhash(ptr: *&uintptr, seed: uintptr): uintptr {
	ret unsafe { memhash((*unsafe)(uintptr(*ptr)), _ptrSize, seed) }
}

fn maphash[K](k: K, seed: uintptr): uintptr {
	const t = comptime::TypeOf(K)
	const match {
	| t.CanNil():
		if k == nil {
			ret seed
		}
	}
	const match t.Kind() {
	| comptime::Int | comptime::I8 | comptime::I16 | comptime::I32 | comptime::I64
	| comptime::Uint | comptime::U8 | comptime::U16 | comptime::U32 | comptime::U64:
		const match t.Bits() {
		| 64:
			ret i64hash(u64(k), seed)
		|:
			ret i32hash(u32(k), seed)
		}
	| comptime::Enum:
		comptime::TypeAlias(enumSource, t.Elem())
		ret maphash(enumSource(k), seed)
	| comptime::F32:
		ret f32hash(f32(k), seed)
	| comptime::F64:
		ret f64hash(f64(k), seed)
	| comptime::Trait | comptime::Any | comptime::TypeEnum:
		// If hash function pointer equals to nil, underlying type is not hashable.
		if unsafe { integ::Emit[*unsafe]("{}.type->hash", k) } == nil {
			panic("runtime: dynamic type used as map key with not-hashable/not-comparable underlying type")
		}
		ret unsafe { integ::Emit[uintptr]("{}.type->hash(&{}.data, {})", k, k, seed) }
	| comptime::Str:
		ret strhash(str(k), seed)
	| comptime::Ptr | comptime::SmartPtr:
		ret unsafe { memhash((*unsafe)(uintptr(k)), _ptrSize, seed) }
	| comptime::Bool:
		ret boolhash(bool(k), seed)
	| comptime::Array:
		ret arrayhash(k, seed)
	| comptime::Struct:
		ret structhash(k, seed)
	| comptime::Chan:
		ret unsafe { memhash((*unsafe)(uintptr(*(*&hchan)(&k))), _ptrSize, seed) }
	}
	panic("unreachable")
}

// Used to seed the hash function.
static mut hashkey: [4]uintptr = []

fn hashinit() {
	for i in hashkey {
		hashkey[i] = uintptr(rand())
	}
}