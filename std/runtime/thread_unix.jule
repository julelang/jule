// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/sys"

extern use "<pthread.h>"

extern let pthread_create: *unsafe
extern let pthread_equal: *unsafe
extern let pthread_detach: *unsafe
extern let pthread_self: *unsafe

#typedef
extern struct pthread_t{}

// Wrapper for operating system thread.
struct osthread {
	handle: extern.pthread_t
}

impl osthread {
	// Reports whether the threads are equal.
	fn equal(*self, other: extern.pthread_t): bool {
		ret sys::Addrcall[i32](uintptr(extern.pthread_equal), self.handle, other) != 0
	}
}

// A low level API function for threads.
// It doesn't provide much abstraction.
// It just creates and detaches a thread using API.
// Reports whether the thread created successfully.
// The created thread is a native-thread.
// Assumes the thread-mutex is already locked and will be released before return.
// The |func| parameter should point to the valid function for operating system thread API.
// The |args| parameter may be nil, passed to the function when call.
// The thread data, should be fit into the threadData struct.
// So, the head fields of the thread data should be matched fields of the threadData.
unsafe fn threadSpawn(func: *unsafe, mut args: *unsafe): bool {
	let mut handle: extern.pthread_t
	if sys::Addrcall[i32](uintptr(extern.pthread_create), &handle, (*unsafe)(nil), func, args) != 0 {
		ret false
	}
	sys::Addrcall(uintptr(extern.pthread_detach), handle)
	ret true
}

fn currentThreadID(): extern.pthread_t {
	ret sys::Addrcall[extern.pthread_t](uintptr(extern.pthread_self))
}

fn osyield() {
	sys::SchedYield() else {}
}

fn _threadinit() {
	setnumcpu()
	// Push the main thread to threads.
	// See the documentation of the pushNewThread function.
	// The |threads| should be initialized here, because compiler will not do it.
	// We do not have to lock the mutex for threads, no risk for concurrency.
	mut m := newThread(roleProgram)
	mainm = m
}