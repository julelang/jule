// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use integ "std/jule/integrated"

cpp use "<pthread.h>"
cpp use "<sched.h>"

cpp unsafe fn pthread_create(*cpp.pthread_t, *unsafe, *unsafe, *unsafe): int
cpp fn pthread_equal(cpp.pthread_t, cpp.pthread_t): bool
cpp fn pthread_detach(cpp.pthread_t): int
cpp fn pthread_self(): cpp.pthread_t
cpp fn sched_yield(): int

#typedef
cpp struct pthread_t{}

// Wrapper for operating system thread.
struct osthread {
	handle: cpp.pthread_t
}

impl osthread {
	// Reports whether the threads are equal.
	fn equal(self, other: cpp.pthread_t): bool {
		ret cpp.pthread_equal(self.handle, other)
	}
}

// Common head fields for a thread data.
struct threadData {
	handle: *cpp.pthread_t
}

// A low level API function for threads.
// It doesn't provide much abstraction.
// It just creates and detaches a thread using API.
// Reports whether the thread created successfully.
// The created thread is a native-thread.
// The |func| parameter should point to the valid function for operating system thread API.
// The |args| parameter may be nil and should point to the thread data.
// The thread data, should be fit into the threadData struct.
// So, the head fields of the thread data should be matched fields of the threadData.
#export "__jule_coSpawn"
unsafe fn coSpawn(func: *unsafe, mut args: *unsafe): bool {
	mut t := pushNewThread()
	(*threadData)(args).handle = &t.os.handle
	if cpp.pthread_create(&t.os.handle, nil, integ::Emit[*unsafe]("(void*(*)(void*))({})", func), args) != 0 {
		ret false
	}
	threadMutex.unlock()
	cpp.pthread_detach(t.os.handle)
	ret true
}

fn currentThreadID(): cpp.pthread_t {
	ret cpp.pthread_self()
}

fn osyield() {
	cpp.sched_yield()
}

// Reserved function for hard initialization.
// This function will be called at earlies stages of the program;
// Before initialization of global variables, and calling other initializer functions.
// This function must initialize critical data of the typical Jule program.
fn __init() {
	// Set numcpu.
	numcpu = unsafe { int(cpp.sysconf(_SC_NPROCESSORS_ONLN)) }
	// Push the main thread to threads.
	// See the documentation of the pushNewThread function.
	// The |threads| should be initialized here, because compiler will not do it.
	// We do not have to lock the mutex for threads, no risk for concurrency.
	mut t := newThread()
	t.os.handle = currentThreadID()
	threads = t
}