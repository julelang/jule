// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/0df681248862a34ff1233cdc4cf0b036e4761652/src/slices/zsortanyfunc.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
//
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

// Sorts data[a:b] using insertion sort.
#disable boundary
fn insertionSortFunc[S: ~[]E, E](mut data: S, a: int, b: int, cmp: fn(a: E, b: E): int) {
	mut i := a + 1
	for i < b; i++ {
		mut j := i
		for j > a && (cmp(data[j], data[j-1]) < 0); j-- {
			data[j], data[j-1] = data[j-1], data[j]
		}
	}
}

// Implements the heap property on data[lo:hi].
// first is an offset into the array where the root of the heap lies.
#disable boundary
fn siftDownFunc[S: ~[]E, E](mut data: S, lo: int, hi: int, first: int, cmp: fn(a: E, b: E): int) {
	mut root := lo
	for {
		mut child := 2*root + 1
		if child >= hi {
			break
		}
		if child+1 < hi && (cmp(data[first+child], data[first+child+1]) < 0) {
			child++
		}
		if !(cmp(data[first+root], data[first+child]) < 0) {
			ret
		}
		data[first+root], data[first+child] = data[first+child], data[first+root]
		root = child
	}
}

#disable boundary
fn heapSortFunc[S: ~[]E, E](mut data: S, a: int, b: int, cmp: fn(a: E, b: E): int) {
	first := a
	lo := 0
	hi := b - a

	// Build heap with greatest element at top.
	mut i := (hi - 1) >> 1
	for i >= 0; i-- {
		siftDownFunc(data, i, hi, first, cmp)
	}

	// Pop elements, largest first, into end of data.
	i = hi - 1
	for i >= 0; i-- {
		data[first], data[first+i] = data[first+i], data[first]
		siftDownFunc(data, lo, i, first, cmp)
	}
}

// Scatters some elements around in an attempt to break some patterns
// that might cause imbalanced partitions in quicksort.
#disable boundary
fn breakPatternsFunc[S: ~[]E, E](mut data: S, a: int, b: int, cmp: fn(a: E, b: E): int) {
	length := b - a
	if length >= 8 {
		mut random := xorshift(length)
		modulus := nextPowerOfTwo(length)

		mut idx := a + (length>>2)<<1 - 1
		for idx <= a+(length>>2)<<1+1; idx++ {
			mut other := int(uint(xorshiftNext(&random)) & (modulus - 1))
			if other >= length {
				other -= length
			}
			data[idx], data[a+other] = data[a+other], data[idx]
		}
	}
}

// Returns x,y where data[x] <= data[y], where x,y=a,b or x,y=b,a.
#disable boundary
fn order2Func[S: ~[]E, E](data: S, a: int, b: int, mut &swaps: *int, cmp: fn(a: E, b: E): int): (int, int) {
	if cmp(data[b], data[a]) < 0 {
		*swaps++
		ret b, a
	}
	ret a, b
}

// Returns x where data[x] is the median of data[a],data[b],data[c], where x is a, b, or c.
fn medianFunc[S: ~[]E, E](data: S, mut a: int, mut b: int, mut c: int, mut &swaps: *int, cmp: fn(a: E, b: E): int): int {
	a, b = order2Func(data, a, b, swaps, cmp)
	b, c = order2Func(data, b, c, swaps, cmp)
	a, b = order2Func(data, a, b, swaps, cmp)
	ret b
}

// Finds the median of data[a - 1], data[a], data[a + 1] and stores the index into a.
fn medianAdjacentFunc[S: ~[]E, E](mut data: S, a: int, mut &swaps: *int, cmp: fn(a: E, b: E): int): int {
	ret medianFunc(data, a-1, a, a+1, swaps, cmp)
}

// Chooses a pivot in data[a:b].
//
// [0,8): chooses a static pivot.
// [8,shortestNinther): uses the simple median-of-three method.
// [shortestNinther,âˆž): uses the Tukey ninther method.
fn choosePivotFunc[S: ~[]E, E](mut data: S, a: int, b: int, cmp: fn(a: E, b: E): int): (pivot: int, hint: sortedHint) {
	const shortestNinther = 50
	const maxSwaps = 4 * 3

	l := b - a

	mut swaps := 0
	mut i := a + l>>2*1
	mut j := a + l>>2*2
	mut k := a + l>>2*3

	if l >= 8 {
		if l >= shortestNinther {
			// Tukey ninther method, the idea came from Rust's implementation.
			i = medianAdjacentFunc(data, i, &swaps, cmp)
			j = medianAdjacentFunc(data, j, &swaps, cmp)
			k = medianAdjacentFunc(data, k, &swaps, cmp)
		}
		// Find the median among i, j, k and stores it into j.
		j = medianFunc(data, i, j, k, &swaps, cmp)
	}

	match swaps {
	| 0:
		ret j, sortedHint.Increasing
	| maxSwaps:
		ret j, sortedHint.Decreasing
	|:
		ret j, sortedHint.Unknown
	}
}

#disable boundary
fn reverseRangeFunc[S: ~[]E, E](mut data: S, a: int, b: int, cmp: fn(a: E, b: E): int) {
	mut i := a
	mut j := b - 1
	for i < j {
		data[i], data[j] = data[j], data[i]
		i++
		j--
	}
}

// Partially sorts a slice, returns true if the slice is sorted at the end.
#disable boundary
fn partialInsertionSortFunc[S: ~[]E, E](mut data: S, a: int, b: int, cmp: fn(a: E, b: E): int): bool {
	const maxSteps = 5          // maximum number of adjacent out-of-order pairs that will get shifted
	const shortestShifting = 50 // don't shift any elements on short arrays

	mut i := a + 1
	mut j := 0
	for j < maxSteps; j++ {
		for i < b && !(cmp(data[i], data[i-1]) < 0) {
			i++
		}

		if i == b {
			ret true
		}

		if b-a < shortestShifting {
			ret false
		}

		data[i], data[i-1] = data[i-1], data[i]

		// Shift the smaller one to the left.
		if i-a >= 2 {
			j = i - 1
			for j >= 1; j-- {
				if !(cmp(data[j], data[j-1]) < 0) {
					break
				}
				data[j], data[j-1] = data[j-1], data[j]
			}
		}
		// Shift the greater one to the right.
		if b-i >= 2 {
			j = i + 1
			for j < b; j++ {
				if !(cmp(data[j], data[j-1]) < 0) {
					break
				}
				data[j], data[j-1] = data[j-1], data[j]
			}
		}
	}
	ret false
}

// Partitions data[a:b] into elements equal to data[pivot] followed by elements greater than data[pivot].
// It assumed that data[a:b] does not contain elements smaller than the data[pivot].
#disable boundary
fn partitionEqualFunc[S: ~[]E, E](mut data: S, a: int, b: int, pivot: int, cmp: fn(a: E, b: E): int): (newpivot: int) {
	data[a], data[pivot] = data[pivot], data[a]
	mut i, mut j := a+1, b-1 // i and j are inclusive of the elements remaining to be partitioned

	for {
		for i <= j && !(cmp(data[a], data[i]) < 0) {
			i++
		}
		for i <= j && (cmp(data[a], data[j]) < 0) {
			j--
		}
		if i > j {
			break
		}
		data[i], data[j] = data[j], data[i]
		i++
		j--
	}
	ret i
}

// Does one quicksort partition.
// Let p = data[pivot]
// Moves elements in data[a:b] around, so that data[i]<p and data[j]>=p for i<newpivot and j>newpivot.
// On return, data[newpivot] = p
#disable boundary
fn partitionFunc[S: ~[]E, E](mut data: S, a: int, b: int, pivot: int, cmp: fn(a: E, b: E): int): (newpivot: int, alreadyPartitioned: bool) {
	data[a], data[pivot] = data[pivot], data[a]
	mut i, mut j := a+1, b-1 // i and j are inclusive of the elements remaining to be partitioned

	for i <= j && (cmp(data[i], data[a]) < 0) {
		i++
	}
	for i <= j && !(cmp(data[j], data[a]) < 0) {
		j--
	}
	if i > j {
		data[j], data[a] = data[a], data[j]
		ret j, true
	}
	data[i], data[j] = data[j], data[i]
	i++
	j--

	for {
		for i <= j && (cmp(data[i], data[a]) < 0) {
			i++
		}
		for i <= j && !(cmp(data[j], data[a]) < 0) {
			j--
		}
		if i > j {
			break
		}
		data[i], data[j] = data[j], data[i]
		i++
		j--
	}
	data[j], data[a] = data[a], data[j]
	ret j, false
}

// Sorts data[a:b].
// The algorithm based on pattern-defeating quicksort(pdqsort), but without the optimizations from BlockQuicksort.
// pdqsort paper: https://arxiv.org/pdf/2106.05123.pdf
// C++ implementation: https://github.com/orlp/pdqsort
// Rust implementation: https://docs.rs/pdqsort/latest/pdqsort/
// limit is the number of allowed bad (very unbalanced) pivots before falling back to heapsort.
#disable boundary
fn pdqsortFunc[S: ~[]E, E](mut data: S, mut a: int, mut b: int, mut limit: int, cmp: fn(a: E, b: E): int) {
	const maxInsertion = 12

	mut wasBalanced := true    // whether the last partitioning was reasonably balanced
	mut wasPartitioned := true // whether the slice was already partitioned

	for {
		length := b - a

		if length <= maxInsertion {
			insertionSortFunc(data, a, b, cmp)
			ret
		}

		// Fall back to heapsort if too many bad choices were made.
		if limit == 0 {
			heapSortFunc(data, a, b, cmp)
			ret
		}

		// If the last partitioning was imbalanced, we need to breaking patterns.
		if !wasBalanced {
			breakPatternsFunc(data, a, b, cmp)
			limit--
		}

		mut pivot, mut hint := choosePivotFunc(data, a, b, cmp)
		if hint == sortedHint.Decreasing {
			reverseRangeFunc(data, a, b, cmp)
			// The chosen pivot was pivot-a elements after the start of the array.
			// After reversing it is pivot-a elements before the end of the array.
			// The idea came from Rust's implementation.
			pivot = (b - 1) - (pivot - a)
			hint = sortedHint.Increasing
		}

		// The slice is likely already sorted.
		if wasBalanced && wasPartitioned && hint == sortedHint.Increasing {
			if partialInsertionSortFunc(data, a, b, cmp) {
				ret
			}
		}

		// Probably the slice contains many duplicate elements, partition the slice into
		// elements equal to and elements greater than the pivot.
		if a > 0 && !(cmp(data[a-1], data[pivot]) < 0) {
			mid := partitionEqualFunc(data, a, b, pivot, cmp)
			a = mid
			continue
		}

		mid, alreadyPartitioned := partitionFunc(data, a, b, pivot, cmp)
		wasPartitioned = alreadyPartitioned

		leftLen, rightLen := mid-a, b-mid
		balanceThreshold := length >> 3
		if leftLen < rightLen {
			wasBalanced = leftLen >= balanceThreshold
			pdqsortFunc(data, a, mid, limit, cmp)
			a = mid + 1
		} else {
			wasBalanced = rightLen >= balanceThreshold
			pdqsortFunc(data, mid+1, b, limit, cmp)
			b = mid
		}
	}
}

#disable boundary
fn swapRangeFunc[E](mut data: []E, a: int, b: int, n: int, cmp: fn(a: E, b: E): int) {
	mut i := 0
	for i < n; i++ {
		data[a+i], data[b+i] = data[b+i], data[a+i]
	}
}

// Rotates two consecutive blocks u = data[a:m] and v = data[m:b] in data:
// Data of the form 'x u v y' is changed to 'x v u y'.
// rotate performs at most b-a many calls to data.Swap,
// and it assumes non-degenerate arguments: a < m && m < b.
fn rotateFunc[E](mut data: []E, a: int, m: int, b: int, cmp: fn(a: E, b: E): int) {
	mut i := m - a
	mut j := b - m

	for i != j {
		if i > j {
			swapRangeFunc(data, m-i, m, j, cmp)
			i -= j
		} else {
			swapRangeFunc(data, m-i, m+j-i, i, cmp)
			j -= i
		}
	}
	// i == j
	swapRangeFunc(data, m-i, m, i, cmp)
}

// Merges the two sorted subsequences data[a:m] and data[m:b] using
// the SymMerge algorithm from Pok-Son Kim and Arne Kutzner, "Stable Minimum
// Storage Merging by Symmetric Comparisons", in Susanne Albers and Tomasz
// Radzik, editors, Algorithms - ESA 2004, volume 3221 of Lecture Notes in
// Computer Science, pages 714-723. Springer, 2004.
//
// Let M = m-a and N = b-n. Wolog M < N.
// The recursion depth is bound by ceil(log(N+M)).
// The algorithm needs O(M*log(N/M + 1)) calls to data.Less.
// The algorithm needs O((M+N)*log(M)) calls to data.Swap.
//
// The paper gives O((M+N)*log(M)) as the number of assignments assuming a
// rotation algorithm which uses O(M+N+gcd(M+N)) assignments. The argumentation
// in the paper carries through for Swap operations, especially as the block
// swapping rotate uses only O(M+N) Swaps.
//
// symMerge assumes non-degenerate arguments: a < m && m < b.
// Having the caller check this condition eliminates many leaf recursion calls,
// which improves performance.
#disable boundary
fn symMergeFunc[E](mut data: []E, a: int, m: int, b: int, cmp: fn(a: E, b: E): int) {
	// Avoid unnecessary recursions of symMerge
	// by direct insertion of data[a] into data[m:b]
	// if data[a:m] only contains one element.
	if m-a == 1 {
		// Use binary search to find the lowest index i
		// such that data[i] >= data[a] for m <= i < b.
		// Exit the search loop with i == b in case no such index exists.
		mut i := m
		mut j := b
		for i < j {
			h := int(uint(i+j) >> 1)
			if cmp(data[h], data[a]) < 0 {
				i = h + 1
			} else {
				j = h
			}
		}
		// Swap values until data[a] reaches the position before i.
		mut k := a
		for k < i-1; k++ {
			data[k], data[k+1] = data[k+1], data[k]
		}
		ret
	}

	// Avoid unnecessary recursions of symMerge
	// by direct insertion of data[m] into data[a:m]
	// if data[m:b] only contains one element.
	if b-m == 1 {
		// Use binary search to find the lowest index i
		// such that data[i] > data[m] for a <= i < m.
		// Exit the search loop with i == m in case no such index exists.
		mut i := a
		mut j := m
		for i < j {
			h := int(uint(i+j) >> 1)
			if !(cmp(data[m], data[h]) < 0) {
				i = h + 1
			} else {
				j = h
			}
		}
		// Swap values until data[m] reaches the position i.
		mut k := m
		for k > i; k-- {
			data[k], data[k-1] = data[k-1], data[k]
		}
		ret
	}

	mid := int(uint(a+b) >> 1)
	n := mid + m
	let mut start: int
	let mut r: int
	if m > mid {
		start = n - b
		r = mid
	} else {
		start = a
		r = m
	}
	p := n - 1

	for start < r {
		c := int(uint(start+r) >> 1)
		if !(cmp(data[p-c], data[c]) < 0) {
			start = c + 1
		} else {
			r = c
		}
	}

	end := n - start
	if start < m && m < end {
		rotateFunc(data, start, m, end, cmp)
	}
	if a < start && start < mid {
		symMergeFunc(data, a, start, mid, cmp)
	}
	if mid < end && end < b {
		symMergeFunc(data, mid, end, b, cmp)
	}
}

fn stableFunc[E](mut data: []E, n: int, cmp: fn(a: E, b: E): int) {
	mut blockSize := 20 // must be > 0
	mut a, mut b := 0, blockSize
	for b <= n {
		insertionSortFunc(data, a, b, cmp)
		a = b
		b += blockSize
	}
	insertionSortFunc(data, a, n, cmp)

	for blockSize < n {
		a, b = 0, 2*blockSize
		for b <= n {
			symMergeFunc(data, a, a+blockSize, b, cmp)
			a = b
			b += 2 * blockSize
		}
		m := a + blockSize
		if m < n {
			symMergeFunc(data, a, m, n, cmp)
		}
		blockSize *= 2
	}
}