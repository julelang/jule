// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/runtime"
use "std/sync"
use "std/sync/atomic"
use "std/testing"

#test
fn testCond(t: &testing::T) {
	cond := sync::Cond.New(new(sync::Mutex))
	mut ready := new(bool)
	mut wg := &sync::WaitGroup{}
	wg.Add(1)
	co fn() {
		cond.Lock()
		for !*ready {
			cond.Wait()
		}
		cond.Unlock()
		wg.Done()
	}()
	wg.Add(1)
	co fn() {
		cond.Lock()
		*ready = true
		cond.Signal()
		cond.Unlock()
		wg.Done()
	}()
	wg.Wait()
	if !*ready {
		t.Errorf("condition did not set correctly")
	}
}