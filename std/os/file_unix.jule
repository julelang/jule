// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/poll"
use integ "std/jule/integrated"
use "std/sys"

const devNull = "/dev/null"

fn openFile(path: str, flag: int, perm: FileMode)!: &File {
	if path == "" {
		error(sys::ENOENT)
	}
	s := integ::StrToBytes(path)
	handle := unsafe { sys::Open(&s[0], flag|sys::O_CLOEXEC, int(syscallMode(perm))) }
	if handle == -1 {
		error(sys::GetLastErrno())
	}
	ret newFile(u64(handle), poll::File) else { error(error) }
}

fn newFile(h: u64, mut flags: int)!: &File {
	mut fd := &poll::FD{
		File: u64(h),
		Flags: flags,
		IsStream: true,
		ZeroReadIsEOF: true,
	}
	fd.Init() else { error(error) }
	ret &File{fd: fd}
}

fn remove(path: str)! {
	s := integ::StrToBytes(path)
	if unsafe { sys::Unlink(&s[0]) } != 0 {
		error(sys::GetLastErrno())
	}
}