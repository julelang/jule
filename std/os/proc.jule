// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use integ "std/jule/integrated"
use "std/os/filepath"
use "std/os/internal"
use "std/runtime"
use "std/sys"

// Causes the current program to exit with the given status code.
// Conventionally, code zero indicates success, non-zero an error.
fn Exit(code: int) { sys::Exit(code) }

// Returns executable path.
// Returns empty string if any error occurs.
fn Executable(): str {
	ret runtime::executable()
}

// Returns command-line arguments.
// Starts with the program name.
fn Args(): []str {
	ret runtime::args()
}

// Returns environment variables.
fn Env(): []str {
	ret runtime::env()
}

// Returns an absolute path name of the current working
// directory of the calling process.
fn Getwd()!: str {
	wd := internal::Getwd()
	if wd == "" {
		error(getLastOsError())
	}
	ret wd
}

// Changes the current working directory to the given directory.
fn Chdir(path: str)! {
	if path == "" {
		error(Error.NotDir)
	}
	ok := internal::Chdir(path)
	if !ok {
		error(getLastOsError())
	}
	// Update the working directory cache.
	const match runtime::OS {
	| "windows":
		abs := filepath::IsAbs(path)
		wdmu.lock()
		if abs {
			wd = path
		} else {
			wd = ""
		}
		wdmu.unlock()
	}
}

// Retrieves the value of the environment variable named by the key.
// It returns the value, which will be empty if the variable is not present.
// To distinguish between an empty value and an unset value, use [LookupEnv].
fn Getenv(key: str): str {
	v, _ := LookupEnv(key)
	ret v
}

// Retrieves the value of the environment variable named by the key.
// If the variable is present in the environment the value (which may be empty)
// is returned and the boolean is false. Otherwise the returned value will be
// empty and the boolean will be true.
fn LookupEnv(key: str): (val: str, unset: bool) {
	ret lookupenv(key)
}

// Sets the value of the environment variable named by the key.
// Reports whether it successful.
fn Setenv(key: str, val: str): bool {
	ret setenv(key, val)
}