// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/conv"
use "std/internal/fastbytes"
use integ "std/jule/integrated"
use "std/sys"

cpp use "<signal.h>"
cpp use "<sys/wait.h>"
cpp use "<unistd.h>"

cpp fn fork(): int
cpp unsafe fn waitpid(pid: int, mut status: *integ::Int, options: int): int
cpp fn WEXITSTATUS(status: int): int
cpp fn kill(pid: int, sig: int): int
cpp unsafe fn setenv(*integ::Char, *integ::Char, int): int
cpp fn fcntl(int, int): int
cpp unsafe fn pipe(mut *integ::Int): int

cpp let X_OK: int
cpp let F_GETFD: int
cpp let F_SETFD: int
cpp let FD_CLOEXEC: int

// Type of a process id.
type pid: int

const invalidPid = -1

struct cmdAttrs {
	pid: pid = invalidPid
}

fn strSliceToCstrSlice(&s: []str, mut &bargs: [][]byte): []*integ::Char {
	mut cs := make([]*integ::Char, len(s)+1)
	bargs = make([][]byte, len(s))
	for i, arg in s {
		mut barg := integ::StrToBytes(arg)
		bargs[i] = barg
		cs[i] = unsafe { (*integ::Char)(&barg[0]) }
	}
	cs[len(cs)-1] = nil
	ret cs
}

fn execvp(&file: str, &argv: []str): int {
	mut bargs := [][]byte(nil) // Avoid deallocation of converted args for safety.
	cargv := strSliceToCstrSlice(argv, bargs)
	s := integ::StrToBytes(file)
	unsafe {
		ret integ::Emit[int]("execvp({}, (char*const*){})", (*integ::Char)(&s[0]), &cargv[0])
	}
}

fn setenv(&envv: []str)! {
	const Overwrite = 1
	for _, env in envv {
		i := fastbytes::FindByteStr(env, '=')
		if i == -1 {
			error(CmdError.Env)
		}
		unsafe {
			key := integ::StrToBytes(env[:i])
			if len(env)-i == 1 {
				val := "\x00"
				if cpp.setenv((*integ::Char)(&key[0]), (*integ::Char)(&val[0]), Overwrite) == -1 {
					error(CmdError.Env)
				}
			} else {
				val := integ::StrToBytes(env[i+1:])
				if cpp.setenv((*integ::Char)(&key[0]), (*integ::Char)(&val[0]), Overwrite) == -1 {
					error(CmdError.Env)
				}
			}
		}
	}
}

impl Cmd {
	fn spawn(self)! {
		if self.attrs.pid != invalidPid {
			panic("process: command is already spawned")
		}
		path := self.path
		let pipe: [2]integ::Int
		if unsafe { cpp.pipe(&pipe[0]) } == -1 {
			error(CmdError.Spawn)
		}
		if sys::Fcntl(int(pipe[1]), cpp.F_SETFD, cpp.fcntl(int(pipe[1]), cpp.F_GETFD)|cpp.FD_CLOEXEC) == -1 {
			sys::Close(int(pipe[0]))
			sys::Close(int(pipe[1]))
			error(CmdError.Spawn)
		}
		self.attrs.pid = cpp.fork()
		match self.attrs.pid {
		| -1:
			error(CmdError.Spawn)
		| 0:
			sys::Close(int(pipe[0]))
			mut args := make([]str, 1, 1+len(self.Args))
			args[0] = path
			args = append(args, self.Args...)
			setenv(self.Env) else { error(error) }
			execvp(path, args)
			code := conv::Itoa(sys::GetLastErrno())
			unsafe { sys::Write(int(pipe[1]), &code[0], uint(len(code))) }
			sys::Close(int(pipe[1]))
			Exit(1)
		|:
			sys::Close(int(pipe[1]))
			let mut buf: [16]byte
			n := unsafe { sys::Read(int(pipe[0]), &buf[0], uint(len(buf))) }
			sys::Close(int(pipe[0]))
			if n != 0 {
				self.attrs.pid = invalidPid
				code, _ := unsafe { conv::AtoiBp(&buf[0], n) }
				error(cmdErrorFromCode(code))
			}
		}
	}

	fn kill(self)! {
		if self.attrs.pid == invalidPid {
			panic("process: command is not spawned")
		}
		if cpp.kill(self.attrs.pid, 1) != 0 {
			error(getLastCmdError())
		}
		self.attrs.pid = invalidPid
	}

	fn wait(self)!: int {
		if self.attrs.pid == invalidPid {
			panic("process: command is not spawned")
		}
		mut stat := 0
		unsafe {
			if cpp.waitpid(self.attrs.pid, (*integ::Int)(&stat), 0) == -1 {
				error(getLastCmdError())
			}
		}
		self.attrs.pid = invalidPid
		ret cpp.WEXITSTATUS(stat)
	}
}

fn cmdErrorFromCode(code: int): CmdError {
	match code {
	| sys::EACCES:
		ret CmdError.Denied
	| sys::ENOENT:
		ret CmdError.NotExist
	|:
		ret CmdError.Other
	}
}

// Returns last Cmd error by errno.
fn getLastCmdError(): CmdError {
	ret cmdErrorFromCode(sys::GetLastErrno())
}