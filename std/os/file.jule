// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/poll"
use "std/io"
use "std/sys"
use "std/unsafe"

// The name of the operating system's “null device.”
// On Unix-like systems, it is "/dev/null"; on Windows, "NUL".
const DevNull = devNull

// Flags to OpenFile wrapping those of the underlying system. Not all
// flags may be implemented on a given system.
const (
	O_RDONLY = sys::O_RDONLY // Open the file read-only
	O_WRONLY = sys::O_WRONLY // Open the file write-only
	O_RDWR   = sys::O_RDWR   // Open the file read-write
	O_APPEND = sys::O_APPEND // Append data to the file when writing
	O_CREATE = sys::O_CREAT  // Create a new file if none exists
	O_EXCL   = sys::O_EXCL   // Used with O_CREATE, file must not exist
	O_SYNC   = sys::O_SYNC   // Open for synchronous I/O
	O_TRUNC  = sys::O_TRUNC  // Truncate regular writable file when opened
)

// The file stream handle.
//
// It works like a wrapper when it comes to console handle like
// stdin, stdout or stderr. Read and write functions are supported
// for console handlers. The rest of the functions are not supported
// and not checked, it is undefined behavior.
//
// There may be system call differences and performance differences
// for console handlers depending on the operating system.
// For example, Windows has an overhead for UTF-16 processing.
struct File {
	fd: &poll::FD
}

impl io::Reader for File {}
impl io::ReadCloser for File {}
impl io::Writer for File {}
impl io::WriteCloser for File {}
impl io::ReadWriter for File {}
impl io::ReadWriteCloser for File {}
impl io::ReadWriteSeeker for File {}
impl io::ReadSeeker for File {}
impl io::WriteSeeker for File {}
impl io::Seeker for File {}
impl io::StrWriter for File {}

// Opens the named file for reading. If successful, methods on
// the returned file can be used for reading; the associated file
// descriptor has mode O_RDONLY.
fn Open(path: str)!: &File {
	ret OpenFile(path, O_RDONLY, 0)?
}

// Opens file stream with named file, specified flag
// (O_RDWR, O_TRUNC etc.) and perm. If named file does
// not exist and O_CREATE flag is passed, will created with
// mode perm (before umask). If successful, returns File reference with handle
// to file stream and the reference can used for I/O operations.
fn OpenFile(path: str, flag: int, perm: FileMode)!: &File {
	ret openFile(path, flag, perm)?
}

// Removes named file.
fn Remove(path: str)! {
	remove(path)?
}

// Creates or truncates the named file. If the file already exists,
// it is truncated. If the file does not exist, it is created with mode 0666
// (before umask). If successful, methods on the returned File can
// be used for I/O; the associated file descriptor has mode O_RDWR.
// Calls internally `File.Open` and forwards any exceptional.
fn Create(path: str)!: &File {
	ret OpenFile(path, O_RDWR|O_CREATE|O_TRUNC, 0666)?
}

// Reads bytes of file.
// First, learns byte-size of file.
// Then reads bytes and returns buffer.
async fn ReadFile(path: str)!: []byte {
	mut f := Open(path)?
	info := Stat(path)?

	let mut size: int
	size64 := info.Size()
	if i64(int(size64)) == size64 {
		size = int(size64)
	}
	size++ // one byte for final read at EOF

	// Following information adopted from Go:
	//
	// If a file claims a small size, read at least 512 bytes.
	// In particular, files in Linux's /proc claim size 0 but
	// then do not work right if read in small pieces,
	// so an initial read of 1 byte would not work correctly.
	if size < 512 {
		size = 512
	}

	mut data := make([]byte, 0, size)
	for {
		n := f.Read(data[len(data):cap(data)]).await?
		if n == io::EOF {
			f.Close().await?
			ret data
		}
		data = data[:len(data)+n]
		if len(data) >= cap(data) {
			mut d := append(data[:cap(data)], 0)
			data = d[:len(data)]
		}
	}
}

// Sync variant of [ReadFile].
// It is provided for blocking operations and must be used carefully
// in an async runtime; it blocks the thread, not the coroutine.
fn ReadFileSync(path: str)!: []byte {
	mut f := Open(path)?
	info := Stat(path)?

	let mut size: int
	size64 := info.Size()
	if i64(int(size64)) == size64 {
		size = int(size64)
	}
	size++ // one byte for final read at EOF

	// Following information adopted from Go:
	//
	// If a file claims a small size, read at least 512 bytes.
	// In particular, files in Linux's /proc claim size 0 but
	// then do not work right if read in small pieces,
	// so an initial read of 1 byte would not work correctly.
	if size < 512 {
		size = 512
	}

	mut data := make([]byte, 0, size)
	for {
		n := f.ReadSync(data[len(data):cap(data)])?
		if n == io::EOF {
			f.CloseSync()?
			ret data
		}
		data = data[:len(data)+n]
		if len(data) >= cap(data) {
			mut d := append(data[:cap(data)], 0)
			data = d[:len(data)]
		}
	}
}

// Writes data to the named file, creating it if necessary.
// If the file does not exist, creates it with permissions perm (before umask);
// otherwise truncates it before writing, without changing permissions.
// Since requires multiple system calls to complete, a failure mid-operation
// can leave the file in a partially written state.
// Calls internally `File.Open`, `File.Write`, `File.Close` and forwards any exceptional.
async fn WriteFile(path: str, data: []byte, perm: FileMode)! {
	mut f := OpenFile(path, O_WRONLY|O_CREATE|O_TRUNC, perm)?
	f.Write(data).await?
	f.Close().await?
}

// Sync variant of [WriteFile].
// It is provided for blocking operations and must be used carefully
// in an async runtime; it blocks the thread, not the coroutine.
fn WriteFileSync(path: str, data: []byte, perm: FileMode)! {
	mut f := OpenFile(path, O_WRONLY|O_CREATE|O_TRUNC, perm)?
	f.WriteSync(data)?
	f.CloseSync()?
}

impl File {
	// Writes bytes to handle and returns written byte count.
	// The number of bytes written can never exceed the length of the buf.
	async fn Write(mut *self, buf: []byte)!: (n: int) {
		ret self.fd.Write(buf).await?
	}

	// Sync variant of [Write].
	// It is provided for blocking operations and must be used carefully
	// in an async runtime; it blocks the thread, not the coroutine.
	fn WriteSync(mut *self, buf: []byte)!: (n: int) {
		ret self.fd.WriteSync(buf)?
	}

	// Like Write, but writes the contents of string s rather than a slice of bytes.
	async fn WriteStr(mut *self, s: str)!: (n: int) {
		ret self.Write(unsafe::StrBytes(s)).await?
	}

	// Read bytes to buffer from handle and returns read byte count.
	// The number of bytes read can never exceed the length of the buf.
	// If the buf is larger than the number of bytes that can be read,
	// the buffer will not cause an overflow. Offset will be shifted
	// by the number of bytes read.
	async fn Read(mut *self, mut buf: []byte)!: (n: int) {
		ret self.fd.Read(buf).await?
	}

	// Sync variant of [Write].
	// It is provided for blocking operations and must be used carefully
	// in an async runtime; it blocks the thread, not the coroutine.
	fn ReadSync(mut *self, mut buf: []byte)!: (n: int) {
		ret self.fd.ReadSync(buf)?
	}

	// Sets offset to next Read/Write operation and returns the new offset.
	// whence: 0 (io::SeekStart) means, relative to the whence of the file, 1 (io::SeekCurrent)
	// means relative to the current offset, and 2 (io::SeekEnd) means relative to end.
	async fn Seek(mut *self, offset: i64, whence: int)!: i64 {
		ret self.fd.Seek(offset, whence).await?
	}

	// Commits the current contents of the file to stable storage.
	// Typically, this means flushing the file system's in-memory copy
	// of recently written data to disk.
	fn Sync(mut *self)! {
		self.fd.Fsync()?
	}

	// Changes the size of the file.
	// It does not change the I/O offset.
	fn Truncate(mut *self, size: i64)! {
		self.fd.Ftruncate(size)?
	}

	// Closes file handle.
	async fn Close(mut *self)! {
		self.fd.Close().await?
	}

	// Sync variant of [Close].
	// It is provided for blocking operations and must be used carefully
	// in an async runtime; it blocks the thread, not the coroutine.
	fn CloseSync(mut *self)! {
		self.fd.CloseSync()?
	}
}