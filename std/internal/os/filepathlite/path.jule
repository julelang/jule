// Copyright 2023-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.20.1/src/path/filepath/path.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
//
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

use "std/runtime"
use "std/strings"

// A LazyBuff is a lazily constructed path buffer.
// It supports append, reading previously appended bytes,
// and retrieving the final string. It does not allocate a buffer
// to hold the output until that output diverges from s.
struct lazyBuff {
	path:       str
	buff:       []byte
	w:          int
	volAndPath: str
	volLen:     int
}

impl lazyBuff {
	fn index(mut *self, i: int): byte {
		if self.buff != nil {
			ret self.buff[i]
		}
		ret self.path[i]
	}

	fn append(mut *self, c: byte) {
		if self.buff == nil {
			if self.w < len(self.path) && self.path[self.w] == c {
				self.w++
				ret
			}
			self.buff = make([]byte, len(self.path))
			copy(self.buff, self.path[:self.w])
		}
		self.buff[self.w] = c
		self.w++
	}

	fn string(mut *self): str {
		if self.buff == nil {
			ret self.volAndPath[:self.volLen+self.w]
		}
		ret self.volAndPath[:self.volLen] + str(self.buff[:self.w])
	}
}

// Returns the shortest path name equivalent to path
// by purely lexical processing. It applies the following rules
// iteratively until no further processing can be done:
//
//  1. Replace multiple SEPARATOR elements with a single one.
//  2. Eliminate each . path name element (the current directory).
//  3. Eliminate each inner .. path name element (the parent directory)
//     along with the non-.. element that precedes it.
//  4. Eliminate .. elements that begin a rooted path:
//     that is, replace "/.." by "/" at the beginning of a path,
//     assuming SEPARATOR is '/'.
//
// The returned path ends in a slash only if it represents a root directory,
// such as "/" on Unix or `C:\` on Windows.
//
// Finally, any occurrences of slash are replaced by SEPARATOR.
//
// If the result of this process is an empty string, clean
// returns the string ".".
//
// On Windows, clean does not modify the volume name other than to replace
// occurrences of "/" with `\`.
// For example, clean("//host/share/../x") returns `\\host\share\x`.
//
// See also Rob Pike, “Lexical File Names in Plan 9 or Getting Dot-Dot Right”
// https://9p.io/sys/doc/lexnames.html
fn Clean(mut path: str): str {
	originalPath := path
	volLen := VolumeNameLen(path)
	path = path[volLen:]
	if path == "" {
		if volLen > 1 && IsPathSeparator(originalPath[0]) && IsPathSeparator(originalPath[1]) {
			// should be UNC
			ret FromSlash(originalPath)
		}
		ret originalPath + "."
	}
	rooted := IsPathSeparator(path[0])

	// Invariants:
	//  reading from path; r is index of next byte to process.
	//  writing to buf; w is index of next byte to write.
	//  dotdot is index in buf where .. must stop, either because
	//      it is the leading slash or it is a leading ../../.. prefix.
	n := len(path)
	mut out := lazyBuff{
		path: path,
		volAndPath: originalPath,
		volLen: volLen,
	}
	mut r, mut dotdot := 0, 0
	if rooted {
		out.append(Separator)
		r, dotdot = 1, 1
	}

	for r < n {
		match {
		| IsPathSeparator(path[r]):
			// empty path element
			r++
		| path[r] == '.' && (r+1 == n || IsPathSeparator(path[r+1])):
			// . element
			r++
		| path[r] == '.' && path[r+1] == '.' && (r+2 == n || IsPathSeparator(path[r+2])):
			// .. element: remove to last separator
			r += 2
			match {
			| out.w > dotdot:
				// can backtrack
				out.w--
				for out.w > dotdot && !IsPathSeparator(out.index(out.w)) {
					out.w--
				}
			| !rooted:
				// cannot backtrack, but not rooted, so append .. element.
				if out.w > 0 {
					out.append(Separator)
				}
				out.append('.')
				out.append('.')
				dotdot = out.w
			}
		|:
			// real path element.
			// add slash if needed
			if rooted && out.w != 1 || !rooted && out.w != 0 {
				out.append(Separator)
			}
			// If a ':' appears in the path element at the start of a Windows path,
			// insert a .\ at the beginning to avoid converting relative paths
			// like a/../c: into c:.
			if runtime::OS == "windows" && out.w == 0 && out.volLen == 0 && r != 0 {
				mut i := r
				for i < n && !IsPathSeparator(path[i]); i++ {
					if path[i] == ':' {
						out.append('.')
						out.append(Separator)
						break
					}
				}
			}
			// copy element
			for r < n && !IsPathSeparator(path[r]); r++ {
				out.append(path[r])
			}
		}
	}

	// Turn empty string into "."
	if out.w == 0 {
		out.append('.')
	}
	ret FromSlash(out.string())
}

// Returns the result of replacing each separator character
// in path with a slash ('/') character. Multiple separators are
// replaced by multiple slashes.
fn ToSlash(path: str): str {
	if Separator == '/' {
		ret path
	}
	ret strings::ReplaceAll(path, str(Separator), "/")
}

// Returns the result of replacing each slash ('/') character
// in path with a separator character. Multiple slashes are replaced
// by multiple separators.
fn FromSlash(path: str): str {
	if Separator == '/' {
		ret path
	}
	ret strings::ReplaceAll(path, "/", str(Separator))
}

// Joins any number of path elements into a single path,
// separating them with an OS specific SEPARATOR. Empty elements
// are ignored. The result is cleaned. However, if the argument
// list is empty or all its elements are empty, join returns
// an empty string.
// On Windows, the result will only be a UNC path if the first
// non-empty element is a UNC path.
fn Join(elem: ...str): str { ret join(elem...) }

// Returns the file name extension used by path.
// The extension is the suffix beginning at the final dot
// in the final element of path; it is empty if there is no dot.
fn Ext(path: str): str {
	mut i := len(path) - 1
	for i >= 0 && !IsPathSeparator(path[i]); i-- {
		if path[i] == '.' {
			ret path[i:]
		}
	}
	ret ""
}

// Returns an absolute representation of path.
// If the path is not absolute it will be joined with the current
// working directory to turn it into an absolute path. The absolute
// path name for a given file is not guaranteed to be unique.
// abs calls clean on the result.
fn Abs(path: str)!: str { ret abs(path)? }

// Returns the last element of path.
// Trailing path separators are removed before extracting the last element.
// If the path is empty, base returns ".".
// If the path consists entirely of separators, base returns a single separator.
fn Base(mut path: str): str {
	if path == "" {
		ret "."
	}
	// Strip trailing slashes.
	for len(path) > 0 && IsPathSeparator(path[len(path)-1]) {
		path = path[0 : len(path)-1]
	}
	// Throw away volume name
	path = path[len(VolumeName(path)):]
	// Find the last element
	mut i := len(path) - 1
	for i >= 0 && !IsPathSeparator(path[i]) {
		i--
	}
	if i >= 0 {
		path = path[i+1:]
	}
	// If empty now, it had only slashes.
	if path == "" {
		ret str(Separator)
	}
	ret path
}

// Returns all but the last element of path, typically the path's directory.
// After dropping the final element, dir calls clean on the path and trailing
// slashes are removed.
// If the path is empty, dir returns ".".
// If the path consists entirely of separators, dir returns a single separator.
// The returned path does not end in a separator unless it is the root directory.
fn Dir(path: str): str {
	vol := VolumeName(path)
	mut i := len(path) - 1
	for i >= len(vol) && !IsPathSeparator(path[i]) {
		i--
	}
	dir := Clean(path[len(vol) : i+1])
	if dir == "." && len(vol) > 2 {
		// must be UNC
		ret vol
	}
	ret vol + dir
}

// Returns leading volume name.
// Given "C:\foo\bar" it returns "C:" on Windows.
// Given "\\host\share\foo" it returns "\\host\share".
// On other platforms it returns empty string.
fn VolumeName(path: str): str {
	ret FromSlash(path[:VolumeNameLen(path)])
}

// Splits a list of paths joined by the OS-specific [ListSeparator],
// usually found in PATH environment variable.
// Unlike strings::Split, it returns an empty slice when passed an empty string.
fn SplitList(path: str): []str {
	ret splitList(path)
}