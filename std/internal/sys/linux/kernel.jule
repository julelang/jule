// Copyright 2026 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/sys"

// Returns major and minor kernel version numbers
// parsed from the sys::Uname's Release field, or (0, 0) if
// the version can't be obtained or parsed.
fn KernelVersion(): (major: int, minor: int) {
	let mut uname: sys::Utsname
	sys::Uname(&uname) else { ret }

	let mut values: [2]int
	let mut value: int
	let mut vi: int
	for _, c in uname.Release {
		if '0' <= c && c <= '9' {
			value = (value * 10) + int(c-'0')
		} else {
			// Note that we're assuming N.N.N here.
			// If we see anything else, we are likely to mis-parse it.
			values[vi] = value
			vi++
			if vi >= len(values) {
				break
			}
			value = 0
		}
	}

	ret values[0], values[1]
}