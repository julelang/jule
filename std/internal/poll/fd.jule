// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/errors"
use "std/io"
use "std/mem"
use "std/sys"

// Throw when a file descriptor is used after it has been closed.
let mut ErrFileClosing = errors::New("use of closed file")

// FD flags. Kind flags should not be combined like File|Console|Socket.
const (
	File         = 1 << iota // Standard file descriptor.
	Socket                   // Socket file descriptor, not connectionless, such as TCP.
	SocketNoConn             // Like Socket kind, but connectionless, such as UDP.
	Console                  // Console file descriptor.
	Pipe                     // Pipe.
)

impl FD {
	// Returns new FD by file and kind.
	fn New(file: u64, flags: int, pollable: bool)!: &FD {
		mut f := &FD{
			File: file,
			Flags: flags,
		}
		f.Init(pollable)?
		ret f
	}

	// Reports whether the FD kind is file.
	fn isFile(*self): bool {
		ret self.Flags&File == File ||
			self.Flags&Pipe == Pipe
	}

	// Returns EOF when fd is available for reading end of file.
	// Otherwise returns n.
	fn statEOF(*self, n: int): int {
		if n == 0 && self.ZeroReadIsEOF {
			ret io::EOF
		}
		ret n
	}

	// Like Read, but wraps the recvfrom syscall.
	async fn ReadV4(mut *self, mut buf: []byte, addr: sys::RawSockaddrInet4)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.ReadV(buf, uintptr(&addr), sys::Socklen(addrLen)).await?
	}

	// Like Read, but wraps the recvfrom syscall.
	async fn ReadV6(mut *self, mut buf: []byte, addr: sys::RawSockaddrInet6)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.ReadV(buf, uintptr(&addr), sys::Socklen(addrLen)).await?
	}

	// Like Write, but wraps the sendto syscall.
	async fn WriteV4(mut *self, buf: []byte, addr: sys::RawSockaddrInet4)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.WriteV(buf, uintptr(&addr), sys::Socklen(addrLen)).await?
	}

	// Like Write, but wraps the sendo syscall.
	async fn WriteV6(mut *self, buf: []byte, addr: sys::RawSockaddrInet6)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.WriteV(buf, uintptr(&addr), sys::Socklen(addrLen)).await?
	}

	// Like Read, but wraps the recvfrom syscall.
	fn ReadV4Sync(mut *self, mut buf: []byte, addr: sys::RawSockaddrInet4)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.ReadVSync(buf, uintptr(&addr), sys::Socklen(addrLen))?
	}

	// Like Read, but wraps the recvfrom syscall.
	fn ReadV6Sync(mut *self, mut buf: []byte, addr: sys::RawSockaddrInet6)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.ReadVSync(buf, uintptr(&addr), sys::Socklen(addrLen))?
	}

	// Like Write, but wraps the sendto syscall.
	fn WriteV4Sync(mut *self, buf: []byte, addr: sys::RawSockaddrInet4)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.WriteVSync(buf, uintptr(&addr), sys::Socklen(addrLen))?
	}

	// Like Write, but wraps the sendo syscall.
	fn WriteV6Sync(mut *self, buf: []byte, addr: sys::RawSockaddrInet6)!: (n: int) {
		addrLen := mem::SizeOf(addr)
		ret self.WriteVSync(buf, uintptr(&addr), sys::Socklen(addrLen))?
	}

	// Reports whether treating the file descriptor as async is the correct
	// approach. If the fd has the potential to exhibit non-blocking behavior,
	// it should be handled with async API. Behavior in sync API is undefined.
	// However, this is not a definitive guarantee that the fd is non-blocking.
	fn ShouldAsync(*self): bool {
		ret self.pollable
	}
}