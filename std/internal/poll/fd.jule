// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/mem"
use "std/sys"

// FD flags. Kind flags should not be combined like File|Console|Socket.
const File = 1 << 0         // Standard file descriptor.
const Socket = 1 << 1       // Socket file descriptor, not connectionless, such as TCP.
const SocketNoConn = 1 << 2 // Like Socket kind, but connectionless, such as UDP.
const Console = 1 << 3      // Console file descriptor.
const Pipe = 1 << 4         // Pipe.

impl FD {
	// Returns new FD by file and kind.
	static fn New(file: u64, flags: int)!: &FD {
		mut f := &FD{
			File: file,
			Flags: flags,
		}
		f.Init() else { error(error) }
		ret f
	}

	// Like Read, but wraps the recvfrom syscall.
	fn ReadV4(mut self, mut buf: []byte, addr: sys::SockaddrIn): (n: int, ok: bool) {
		addrLen := mem::SizeOf(addr)
		ret unsafe { self.ReadV(buf, (*sys::Sockaddr)(&addr), addrLen) }
	}

	// Like Read, but wraps the recvfrom syscall.
	fn ReadV6(mut self, mut buf: []byte, addr: sys::SockaddrIn6): (n: int, ok: bool) {
		addrLen := mem::SizeOf(addr)
		ret unsafe { self.ReadV(buf, (*sys::Sockaddr)(&addr), addrLen) }
	}

	// Like Write, but wraps the sendto syscall.
	fn WriteV4(mut self, buf: []byte, addr: sys::SockaddrIn): (n: int, ok: bool) {
		addrLen := mem::SizeOf(addr)
		ret unsafe { self.WriteV(buf, (*sys::Sockaddr)(&addr), addrLen) }
	}

	// Like Write, but wraps the sendo syscall.
	fn WriteV6(mut self, buf: []byte, addr: sys::SockaddrIn6): (n: int, ok: bool) {
		addrLen := mem::SizeOf(addr)
		ret unsafe { self.WriteV(buf, (*sys::Sockaddr)(&addr), addrLen) }
	}
}