// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Reports whether x is less than y.
// For floating-point types, a NaN is considered less than any non-NaN,
// and -0.0 is not less than (is equal to) 0.0.
fn Less[T: ordered](x: T, y: T): bool {
	const match type T {
	| f32 | f64:
		if isNaN(x) && !isNaN(y) {
			ret true
		}
	}
	ret x < y
}

// Returns
//
//	-1 if x is less than y,
//	 0 if x equals y,
//	+1 if x is greater than y.
//
// For floating-point types, a NaN is considered less than any non-NaN,
// a NaN is considered equal to a NaN, and -0.0 is equal to 0.0.
fn Compare[T: ordered](x: T, y: T): int {
	xNaN := isNaN(x)
	yNaN := isNaN(y)
	if xNaN {
		if yNaN {
			ret 0
		}
		ret -1
	}
	if yNaN {
		ret +1
	}
	if x < y {
		ret -1
	}
	if x > y {
		ret +1
	}
	ret 0
}

// isNaN reports whether x is a NaN without requiring the math package.
// This will always return false if T is not floating-point.
fn isNaN[T](x: T): bool {
	ret x != x
}