// Copyright 2024 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/ubuf"

// Returns the value true if the ASCII character with the given array
// position can be represented inside a JSON string without any further
// escaping.
//
// All values are true except for the ASCII control characters (0-31), the
// double quote ("), and the backslash character ("\").
fn isSafe(b: byte): bool {
	ret b != '"' && b != '\\' && b > 31
}

// Returns the value true if the ASCII character with the given
// array position can be safely represented inside a JSON string, embedded
// inside of HTML <script> tags, without any additional escaping.
//
// All values are true except for the ASCII control characters (0-31), the
// double quote ("), the backslash character ("\"), HTML opening and closing
// tags ("<" and ">"), and the ampersand ("&").
fn isHTMLSafe(b: byte): bool {
	ret b != '"' && b != '&' && b != '<' && b != '>' && b != '\\' && b > 31
}

fn isNeedHTMLEscape(src: []byte): bool {
	b := ubuf::Ubuf{}
	b.SetData(src)
	for i, c in src {
		if c == '<' || c == '>' || c == '&' {
			ret true
		}
		// Convert U+2028 and U+2029 (E2 80 A8 and E2 80 A9).
		if c == 0xE2 && i+2 < b.Len() && b.Get(i+1) == 0x80 && b.Get(i+2)&^1 == 0xA8 {
			ret true
		}
	}
	ret false
}

fn appendHTMLEscape(mut &dst: *buffer, src: []byte) {
	// The characters can only appear in string literals,
	// so just scan the string one byte at a time.
	b := ubuf::Ubuf{}
	b.SetData(src)
	mut start := 0
	for i, c in src {
		if c == '<' || c == '>' || c == '&' {
			dst.write(b.Slice2(start, i))
			dst.writeByte('\\')
			dst.writeByte('u')
			dst.writeByte('0')
			dst.writeByte('0')
			dst.writeByte(hex[c>>4])
			dst.writeByte(hex[c&0xF])
			start = i + 1
		}
		// Convert U+2028 and U+2029 (E2 80 A8 and E2 80 A9).
		if c == 0xE2 && i+2 < b.Len() && b.Get(i+1) == 0x80 && b.Get(i+2)&^1 == 0xA8 {
			dst.write(b.Slice2(start, i))
			dst.writeByte('\\')
			dst.writeByte('u')
			dst.writeByte('2')
			dst.writeByte('0')
			dst.writeByte('2')
			dst.writeByte(hex[b.Get(i+2)&0xF])
			start = i + len("\u2029")
		}
	}
	dst.write(b.Slice1(start))
}