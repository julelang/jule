// Copyright 2022-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.19/src/math/remainder.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
// 
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

// The original C code and the comment below are from
// FreeBSD's /usr/src/lib/msun/src/e_remainder.c and came
// with this notice. The Jule code is a simplified version of
// the original C.
//
// ====================================================
// Copyright (C) 1993 by Sun Microsystems, Inc. All rights reserved.
//
// Developed at SunPro, a Sun Microsystems, Inc. business.
// Permission to use, copy, modify, and distribute this
// software is freely granted, provided that this notice
// is preserved.
// ====================================================
//
// __ieee754_remainder(x,y)
// Return :
//      returns  x REM y  =  x - [x/y]*y  as if in infinite
//      precision arithmetic, where [x/y] is the (infinite bit)
//      integer nearest x/y (in half way cases, choose the even one).
// Method :
//      Based on Mod() returning  x - [x/y]chopped * y  exactly.

// Returns the IEEE 754 floating-point remainder of x/y.
//
// Special cases are:
//  Remainder(±Inf, y) = NaN
//  Remainder(NaN, y) = NaN
//  Remainder(x, 0) = NaN
//  Remainder(x, ±Inf) = x
//  Remainder(x, NaN) = NaN
fn Remainder(mut x: f64, mut y: f64): f64 {
    const Tiny = 4.45014771701440276618e-308 // 0x0020000000000000
    const HalfMax = f64.Max / 2

    // special cases
    match {
    | IsNaN(x) | IsNaN(y) | IsInf(x, 0) | y == 0:
        ret NaN()
    | IsInf(y, 0):
        ret x
    }
    let mut sign = false
    if x < 0 {
        x = -x
        sign = true
    }
    if y < 0 {
        y = -y
    }
    if x == y {
        if sign {
            let zero = 0.0
            ret -zero
        }
        ret 0
    }
    if y <= HalfMax {
        x = Mod(x, y + y) // now x < 2y
    }
    if y < Tiny {
        if x+x > y {
            x -= y
            if x+x >= y {
                x -= y
            }
        }
    } else {
        let yHalf = 0.5 * y
        if x > yHalf {
            x -= y
            if x >= yHalf {
                x -= y
            }
        }
    }
    if sign {
        x = -x
    }
    ret x
}