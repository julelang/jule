// Copyright 2022-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.19/src/math/atanh.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
// 
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

// The original C code, the long comment, and the constants
// below are from FreeBSD's /usr/src/lib/msun/src/e_atanh.c
// and came with this notice. The Jule code is a simplified
// version of the original C.
//
// ====================================================
// Copyright (C) 1993 by Sun Microsystems, Inc. All rights reserved.
//
// Developed at SunPro, a Sun Microsystems, Inc. business.
// Permission to use, copy, modify, and distribute this
// software is freely granted, provided that this notice
// is preserved.
// ====================================================
//
//
// __ieee754_atanh(x)
// Method :
//  1. Reduce x to positive by atanh(-x) = -atanh(x)
//  2. For x>=0.5
//              1              2x                          x
//  atanh(x) = --- * log(1 + -------) = 0.5 * log1p(2 * --------)
//              2             1 - x                      1 - x
//
//  For x<0.5
//  atanh(x) = 0.5*log1p(2x+2x*x/(1-x))
//
// Special cases:
//  atanh(x) is NaN if |x| > 1 with signal;
//  atanh(NaN) is that NaN with no signal;
//  atanh(+-1) is +-INF with signal.
//

// Returns the inverse hyperbolic tangent of x.
//
// Special cases are:
//  atanh(1) = +inf
//  atanh(±0) = ±0
//  atanh(-1) = -inf
//  atanh(x) = nan if x < -1 or x > 1
//  atanh(nan) = nan
pub fn atanh(mut x: f64): f64 {
    const NEAR_ZERO = 1.0 / (1 << 28) // 2**-28
    // special cases
    match {
    | x < -1 | x > 1 | is_nan(x):
        ret nan()
    | x == 1:
        ret inf(1)
    | x == -1:
        ret inf(-1)
    }
    let mut sign = false
    if x < 0 {
        x = -x
        sign = true
    }
    let mut temp: f64 = 0
    match {
    | x < NEAR_ZERO:
        temp = x
    | x < 0.5:
        temp = x + x
        temp = 0.5 * log1p(temp + temp * x / (1 - x))
    |:
        temp = 0.5 * log1p((x + x) / (1 - x))
    }
    if sign {
        temp = -temp
    }
    ret temp
}