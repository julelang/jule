// Copyright 2022 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.19/src/math/trig_reduce.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
// 
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

use std::math::bits::{add64, mul64, leading_zeros64}

// Is the maximum value of x where the reduction using PI/4
// in 3 f64 parts still gives accurate results. This threshold
// is set by y*C being representable as a f64 without error
// where y is given by y = floor(x * (4 / PI)) and C is the leading partial
// terms of 4/PI. Since the leading terms (PI4A and PI4B in sin.go) have 30
// and 32 trailing zero bits, y should have less than 30 significant bits.
//
//  y < 1<<30  -> floor(x*4/PI) < 1<<30 -> x < (1<<30 - 1) * PI/4
//
// So, conservatively we can take x < 1<<29.
// Above this threshold Payne-Hanek range reduction must be used.
const REDUCE_THRESHOLD = 1 << 29

// Implements Payne-Hanek range reduction by PI/4
// for x > 0. It returns the integer part mod 8 (j) and
// the fractional part (z) of x / (PI/4).
// The implementation is based on:
// "ARGUMENT REDUCTION FOR HUGE ARGUMENTS: Good to the Last Bit"
// K. C. Ng et al, March 24, 1992
// The simulated multi-precision calculation of x*B uses 64-bit integer arithmetic.
fn trig_reduce(mut x: f64): (j: u64, z: f64) {
    const PI4 = PI / 4
    if x < PI4 {
        ret 0, x
    }
    // Extract out the integer and exponent such that,
    // x = ix * 2 ** exp.
    let mut ix = f64_bits(x)
    let exp = int(ix>>SHIFT&MASK) - BIAS - SHIFT
    x = ix & ^(MASK << SHIFT)
    ix |= 1 << SHIFT
    // Use the exponent to extract the 3 appropriate uint64 digits from M_PI4,
    // B ~ (z0, z1, z2), such that the product leading digit has the exponent -61.
    // Note, exp >= -53 since x >= PI4 and exp < 971 for maximum f64.
    let (digit, bitshift) = uint(exp+61)/64, uint(exp+61)%64
    let z0 = (M_PI4[digit] << bitshift) | (M_PI4[digit+1] >> (64 - bitshift))
    let z1 = (M_PI4[digit+1] << bitshift) | (M_PI4[digit+2] >> (64 - bitshift))
    let z2 = (M_PI4[digit+2] << bitshift) | (M_PI4[digit+3] >> (64 - bitshift))
    // Multiply mantissa by the digits and extract the upper two digits (hi, lo).
    let (z2hi, _) = mul64(z2, ix)
    let (z1hi, z1lo) = mul64(z1, ix)
    let z0lo = z0 * ix
    let (lo, c) = add64(z1lo, z2hi, 0)
    let (mut hi, _) = add64(z0lo, z1hi, c)
    // The top 3 bits are j.
    j = hi >> 61
    // Extract the fraction and find its magnitude.
    hi = hi<<3 | lo>>61
    let lz = uint(leading_zeros64(hi))
    let e = u64(BIAS - (lz + 1))
    // Clear implicit mantissa bit and shift into place.
    hi = (hi << (lz + 1)) | (lo >> (64 - (lz + 1)))
    hi >>= 64 - SHIFT
    // Include the exponent and convert to a float.
    hi |= e << SHIFT
    z = f64_from_bits(hi)
    // Map zeros to origin.
    if j&1 == 1 {
        j++
        j &= 7
        z--
    }
    // Multiply the fractional part by pi/4.
    ret j, z * PI4
}

// Is the binary digits of 4/pi as a u64 array,
// that is, 4/pi = Sum M_PI4[i]*2^(-64*i)
// 19 64-bit digits and the leading one bit give 1217 bits
// of precision to handle the largest possible f64 exponent.
static M_PI4: [...]u64 = [
    0x0000000000000001,
    0x45f306dc9c882a53,
    0xf84eafa3ea69bb81,
    0xb6c52b3278872083,
    0xfca2c757bd778ac3,
    0x6e48dc74849ba5c0,
    0x0c925dd413a32439,
    0xfc3bd63962534e7d,
    0xd1046bea5d768909,
    0xd338e04d68befc82,
    0x7323ac7306a673e9,
    0x3908bf177bf25076,
    0x3ff12fffbc0b301f,
    0xde5e2316b414da3e,
    0xda6cfd9e4f96136e,
    0x9e8c7ecd3cbfd45a,
    0xea4f758fd7cbe2f6,
    0x7a0e73ef14a525d4,
    0xd7f6bf623f1aba10,
    0xac06608df8f6d757,
]
