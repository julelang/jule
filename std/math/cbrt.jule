// Copyright 2022-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.19/src/math/cbrt.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
// 
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

// The Jule code is a modified version of the original C code from
// http://www.netlib.org/fdlibm/s_cbrt.c and came with this notice.
//
// ====================================================
// Copyright (C) 1993 by Sun Microsystems, Inc. All rights reserved.
//
// Developed at SunSoft, a Sun Microsystems, Inc. business.
// Permission to use, copy, modify, and distribute this
// software is freely granted, provided that this notice
// is preserved.
// ====================================================

// Returns the cube root of x.
//
// Special cases are:
//  Cbrt(±0) = ±0
//  Cbrt(±Inf) = ±Inf
//  Cbrt(NaN) = NaN
fn Cbrt(mut x: f64): f64 {
    const B1 = 715094163                               // (682-0.03306235651)*2**20
    const B2 = 696219795                               // (664-0.03306235651)*2**20
    const C = 5.42857142857142815906e-01               // 19/35     = 0x3FE15F15F15F15F1
    const D = -7.05306122448979611050e-01              // -864/1225 = 0xBFE691DE2532C834
    const E = 1.41428571428571436819e+00               // 99/70     = 0x3FF6A0EA0EA0EA0F
    const F = 1.60714285714285720630e+00               // 45/28     = 0x3FF9B6DB6DB6DB6E
    const G = 3.57142857142857150787e-01               // 5/14      = 0x3FD6DB6DB6DB6DB7
    const SmallestNormal = 2.22507385850720138309e-308 // 2**-1022  = 0x0010000000000000

    // special cases
    if x == 0 || IsNaN(x) || IsInf(x, 0) {
        ret x
    }

    mut sign := false
    if x < 0 {
        x = -x
        sign = true
    }

    // rough cbrt to 5 bits
    mut t := F64FromBits(F64Bits(x) / 3 + B1 << 32)
    if x < SmallestNormal {
        // subnormal number
        t = f64(1 << 54) // set t= 2**54
        t *= x
        t = F64FromBits(F64Bits(t) / 3 + B2 << 32)
    }

    // new cbrt to 23 bits
    mut r := t * t / x
    mut s := C + r * t
    t *= G + F / (s + E + D / s)

    // chop to 22 bits, make larger than cbrt(x)
    t = F64FromBits(F64Bits(t) & (0xFFFFFFFFC << 28) + 1 << 30)

    // one step newton iteration to 53 bits with error less than 0.667ulps
    s = t * t // t*t is exact
    r = x / s
    w := t + t
    r = (r - t) / (w + r) // r-s is exact
    t = t + t * r

    // restore the sign bit
    if sign {
        t = -t
    }
    ret t
}