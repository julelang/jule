// Copyright 2022-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.19/src/math/fma.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
// 
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

use bits for std::math::bits

fn zero(x: u64): u64 {
    if x == 0 {
        ret 1
    }
    ret 0
    // branchless:
    // ret ((x>>1 | x&1) - 1) >> 63
}

fn nonzero(x: u64): u64 {
    if x != 0 {
        ret 1
    }
    ret 0
    // branchless:
    // ret 1 - ((x>>1|x&1)-1)>>63
}

fn shl(u1: u64, u2: u64, n: uint): (r1: u64, r2: u64) {
    r1 = u1 << n | u2 >> (64 - n) | u2 << (n - 64)
    r2 = u2 << n
    ret
}

fn shr(u1: u64, u2: u64, n: uint): (r1: u64, r2: u64) {
    r2 = u2 >> n | u1 << (64 - n)
    // Avoid overflow value of: u1>>(n-64)
    let shift = n - 64
    if shift < 64 {
        r2 |= u1 >> shift
    }
    r1 = u1 >> n
    ret
}

// Compresses the bottom n+1 bits of the two-word
// value into a single bit. the result is equal to the value
// shifted to the right by n, except the result's 0th bit is
// set to the bitwise OR of the bottom n+1 bits.
fn shrcompress(u1: u64, u2: u64, n: uint): (r1: u64, r2: u64) {
    match {
    | n == 0:
        ret u1, u2
    | n == 64:
        ret 0, u1 | nonzero(u2)
    | n >= 128:
        ret 0, nonzero(u1 | u2)
    | n < 64:
        r1, r2 = shr(u1, u2, n)
        r2 |= nonzero(u2 & (1 << n - 1))
    | n < 128:
        r1, r2 = shr(u1, u2, n)
        r2 |= nonzero(u1 & (1 << (n - 64) - 1) | u2)
    }
    ret
}

fn lz(u1: u64, u2: u64): (l: i32) {
    l = i32(bits::LeadingZeros64(u1))
    if l == 64 {
        l += i32(bits::LeadingZeros64(u2))
    }
    ret l
}

// Splits b into sign, biased exponent, and mantissa.
// It adds the implicit 1 bit to the mantissa for normal values,
// and normalizes subnormal values.
fn split(b: u64): (sign: u32, exp: i32, mantissa: u64) {
    sign = u32(b >> 63)
    exp = i32(b >> 52) & mask
    mantissa = b & fracMask

    if exp == 0 {
        // Normalize value if subnormal.
        let shift = uint(bits::LeadingZeros64(mantissa) - 11)
        mantissa <<= shift
        exp = 1 - i32(shift)
    } else {
        // Add implicit 1 bit
        mantissa |= 1 << 52
    }
    ret
}

// Returns x * y + z, computed with only one rounding.
// (That is, FMA returns the fused multiply-add of x, y, and z.)
fn FMA(x: f64, y: f64, z: f64): f64 {
    let (bx, by, bz) = F64Bits(x), F64Bits(y), F64Bits(z)

    // inf or NaN or zero involved. At most one rounding will occur.
    if x == 0.0 || y == 0.0 || z == 0.0 || bx&uvinf == uvinf || by&uvinf == uvinf {
        ret x * y + z
    }
    // Handle non-finite z separately. Evaluating x*y+z where
    // x and y are finite, but z is infinite, should always result in z.
    if bz&uvinf == uvinf {
        ret z
    }

    // Inputs are (sub)normal.
    // Split x, y, z into sign, exponent, mantissa.
    let (xs, xe, xm) = split(bx)
    let (ys, ye, ym) = split(by)
    let (mut zs, mut ze, zm) = split(bz)

    // Compute product p = x*y as sign, exponent, two-word mantissa.
    // Start with exponent. "is normal" bit isn't subtracted yet.
    let mut pe = xe + ye - bias + 1

    // pm1:pm2 is the double-word mantissa for the product p.
    // Shift left to leave top bit in product. Effectively
    // shifts the 106-bit product to the left by 21.
    let (mut pm1, mut pm2) = bits::Mul64(xm << 10, ym << 11)
    let (mut zm1, mut zm2) = zm << 10, u64(0)
    let mut ps = xs ^ ys // product sign

    // normalize to 62nd bit
    let is62zero = uint((^pm1 >> 62) & 1)
    pm1, pm2 = shl(pm1, pm2, is62zero)
    pe -= i32(is62zero)

    // Swap addition operands so |p| >= |z|
    if pe < ze || pe == ze && pm1 < zm1 {
        ps, pe, pm1, pm2, zs, ze, zm1, zm2 = zs, ze, zm1, zm2, ps, i32(pe), pm1, pm2
    }

    // Align significands
    zm1, zm2 = shrcompress(zm1, zm2, uint(pe - ze))

    // Compute resulting significands, normalizing if necessary.
    let mut m: u64 = 0
    let mut c: u64 = 0
    if ps == zs {
        // Adding (pm1:pm2) + (zm1:zm2)
        pm2, c = bits::Add64(pm2, zm2, 0)
        pm1, _ = bits::Add64(pm1, zm1, c)
        pe -= i32(^pm1 >> 63)
        pm1, m = shrcompress(pm1, pm2, uint(64 + pm1 >> 63))
    } else {
        // Subtracting (pm1:pm2) - (zm1:zm2)
        pm2, c = bits::Sub64(pm2, zm2, 0)
        pm1, _ = bits::Sub64(pm1, zm1, c)
        let nz = lz(pm1, pm2)
        pe -= nz
        m, pm2 = shl(pm1, pm2, uint(nz - 1))
        m |= nonzero(pm2)
    }

    // Round and break ties to even
    if pe > 1022+bias || pe == 1022+bias && (m+1<<9)>>63 == 1 {
        // rounded value overflows exponent range
        ret F64FromBits(u64(ps) << 63 | uvinf)
    }
    if pe < 0 {
        let n = uint(-pe)
        m = m >> n | nonzero(m & (1 << n - 1))
        pe = 0
    }
    m = ((m + 1 << 9) >> 10) & ^zero((m & (1 << 10 - 1)) ^ 1 << 9)
    pe &= -i32(nonzero(m))
    ret F64FromBits(u64(ps) << 63 + u64(pe) << 52 + m)
}