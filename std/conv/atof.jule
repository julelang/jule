// Copyright 2022-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.19/src/strconv/atof.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
// 
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

use "std/unsafe"
use "std/math"

const optimize = true

// Returns the length of the common
// prefix of s and prefix, with the character case of s ignored.
// The prefix argument must be all lower-case.
fn commonPrefixLenIgnoreCase(s: str, prefix: str): int {
	mut n := len(prefix)
	if n > len(s) {
		n = len(s)
	}
	mut i := 0
	for i < n; i++ {
		mut c := s[i]
		if 'A' <= c && c <= 'Z' {
			c += 'a' - 'A'
		}
		if c != prefix[i] {
			ret i
		}
	}
	ret n
}

// Returns the floating-point value for the special,
// possibly signed floating-point representations inf, infinity,
// and NaN. The result is ok if a prefix of s contains one
// of these representations and n is the length of that prefix.
// The character case is ignored.
fn special(s: str): (f: f64, n: int, ok: bool) {
	if len(s) == 0 {
		ret 0, 0, false
	}
	mut sign := 1
	mut nsign := 0
	match s[0] {
	| '+' | '-':
		if s[0] == '-' {
			sign = -1
		}
		nsign = 1
		unsafe {
			// Break immutabilty, do not change content, just slicing.
			mut sp := &s
			*sp = (*sp)[1:]
		}
		fall
	| 'i' | 'I':
		mut caseN := commonPrefixLenIgnoreCase(s, "infinity")
		// Anything longer than "inf" is ok, but if we
		// don't have "infinity", only consume "inf".
		if 3 < caseN && caseN < 8 {
			caseN = 3
		}
		if caseN == 3 || caseN == 8 {
			ret math::Inf(sign), nsign + n, true
		}
	| 'n' | 'N':
		if commonPrefixLenIgnoreCase(s, "NaN") == 3 {
			ret math::NaN(), 3, true
		}
	}
	ret 0, 0, false
}

// Reads a decimal or hexadecimal mantissa and exponent from a float
// string representation in s; the number may be followed by other characters.
// Reports the number of bytes consumed (i), and whether the number is valid (ok).
fn readFloat(&s: str): (mantissa: u64, exp: int, neg: bool, trunc: bool, hex: bool, i: int, ok: bool) {
	// optional sign
	if i >= len(s) {
		ret
	}
	match {
	| s[i] == '+':
		i++
	| s[i] == '-':
		neg = true
		i++
	}

	// digits
	mut base := u64(10)
	mut maxMantDigits := 19 // 10^19 fits in u64
	mut expChar := 'e'
	if i+2 < len(s) && s[i] == '0' && lower(s[i+1]) == 'x' {
		base = 16
		maxMantDigits = 16 // 16^16 fits in u64
		i += 2
		expChar = 'p'
		hex = true
	}
	mut sawdot := false
	mut sawdigits := false
	mut nd := 0
	mut ndMant := 0
	mut dp := 0
	for i < len(s); i++ {
		c := s[i]
		match {
		| c == '_':
			continue
		| c == '.':
			if sawdot {
				goto loop_end
			}
			sawdot = true
			dp = nd
			continue
		| '0' <= c && c <= '9':
			sawdigits = true
			if c == '0' && nd == 0 { // ignore leading zeros
				dp--
				continue
			}
			nd++
			if ndMant < maxMantDigits {
				mantissa *= base
				mantissa += u64(c - '0')
				ndMant++
			} else if c != '0' {
				trunc = true
			}
			continue
		| base == 16 && 'a' <= lower(c) && lower(c) <= 'f':
			sawdigits = true
			nd++
			if ndMant < maxMantDigits {
				mantissa *= 16
				mantissa += u64(lower(c) - 'a' + 10)
				ndMant++
			} else {
				trunc = true
			}
			continue
		}
		break
	}
loop_end:
	if !sawdigits {
		ret
	}
	if !sawdot {
		dp = nd
	}

	if base == 16 {
		dp *= 4
		ndMant *= 4
	}

	// optional exponent moves decimal point.
	// if we read a very large, very long number,
	// just be sure to move the decimal point by
	// a lot (say, 100000).  it doesn't matter if it's
	// not the exact number.
	if i < len(s) && lower(s[i]) == expChar {
		i++
		if i >= len(s) {
			ret
		}
		mut esign := 1
		if s[i] == '+' {
			i++
		} else if s[i] == '-' {
			i++
			esign = -1
		}
		if i >= len(s) || s[i] < '0' || s[i] > '9' {
			ret
		}
		mut e := 0
		for i < len(s) && ('0' <= s[i] && s[i] <= '9' || s[i] == '_'); i++ {
			if s[i] == '_' {
				continue
			}
			if e < 10000 {
				e = e*10 + int(s[i]) - '0'
			}
		}
		dp += e * esign
	} else if base == 16 {
		// Must have exponent.
		ret
	}

	if mantissa != 0 {
		exp = dp - ndMant
	}

	ok = true
	ret
}

// Decimal power of ten to binary power of two.
static powtab = [1, 3, 6, 9, 13, 16, 19, 23, 26]

impl decimal {
	fn set(mut self, s: []byte): (ok: bool) {
		mut i := 0
		self.neg = false
		self.trunc = false

		// optional sign
		if i >= len(s) {
			ret
		}
		match {
		| s[i] == '+':
			i++
		| s[i] == '-':
			self.neg = true
			i++
		}

		// digits
		mut sawdot := false
		mut sawdigits := false
		for i < len(s); i++ {
			match {
			| s[i] == '_':
				continue
			| s[i] == '.':
				if sawdot {
					ret
				}
				sawdot = true
				self.dp = self.nd
				continue
			| '0' <= s[i] && s[i] <= '9':
				sawdigits = true
				if s[i] == '0' && self.nd == 0 { // ignore leading zeros
					self.dp--
					continue
				}
				if self.nd < len(self.d) {
					self.d[self.nd] = s[i]
					self.nd++
				} else if s[i] != '0' {
					self.trunc = true
				}
				continue
			}
			break
		}
		if !sawdigits {
			ret
		}
		if !sawdot {
			self.dp = self.nd
		}

		// optional exponent moves decimal point.
		// if we read a very large, very long number,
		// just be sure to move the decimal point by
		// a lot (say, 100000).  it doesn't matter if it's
		// not the exact number.
		if i < len(s) && lower(s[i]) == 'e' {
			i++
			if i >= len(s) {
				ret
			}
			mut esign := 1
			if s[i] == '+' {
				i++
			} else if s[i] == '-' {
				i++
				esign = -1
			}
			if i >= len(s) || s[i] < '0' || s[i] > '9' {
				ret
			}
			mut e := 0
			for i < len(s) && ('0' <= s[i] && s[i] <= '9' || s[i] == '_'); i++ {
				if s[i] == '_' {
					continue
				}
				if e < 10000 {
					e = e*10 + int(s[i]) - '0'
				}
			}
			self.dp += e * esign
		}

		if i != len(s) {
			ret
		}

		ok = true
		ret
	}

	fn floatBits(mut self, &flt: floatInfo): (b: u64, overflow: bool) {
		mut exp := 0
		mut mant := u64(0)

		// Zero is always a special case.
		if self.nd == 0 {
			mant = 0
			exp = flt.bias
			goto out
		}

		// Obvious overflow/underflow.
		// These bounds are for 64-bit floats.
		// Will have to change if we want to support 80-bit floats in the future.
		if self.dp > 310 {
			goto overflow
		}
		if self.dp < -330 {
			// zero
			mant = 0
			exp = flt.bias
			goto out
		}

		// Scale by powers of two until in range [0.5, 1.0)
		exp = 0
		for self.dp > 0 {
			mut n := 0
			if self.dp >= len(powtab) {
				n = 27
			} else {
				n = powtab[self.dp]
			}
			self.shift(-n)
			exp += n
		}
		for self.dp < 0 || self.dp == 0 && self.d[0] < '5' {
			mut n := 0
			if -self.dp >= len(powtab) {
				n = 27
			} else {
				n = powtab[-self.dp]
			}
			self.shift(n)
			exp -= n
		}

		// Our range is [0.5,1) but floating point range is [1,2).
		exp--

		// Minimum representable exponent is flt.bias+1.
		// If the exponent is smaller, move it up and
		// adjust d accordingly.
		if exp < flt.bias+1 {
			n := flt.bias + 1 - exp
			self.shift(-n)
			exp += n
		}

		if exp-flt.bias >= int(1<<flt.expbits-1) {
			goto overflow
		}

		// Extract 1+flt.mantbits bits.
		self.shift(int(1 + flt.mantbits))
		mant = self.roundedInteger()

		// Rounding might have added a bit; shift down.
		if mant == 2<<flt.mantbits {
			mant >>= 1
			exp++
			if exp-flt.bias >= int(1<<flt.expbits-1) {
				goto overflow
			}
		}

		// Denormalized?
		if mant&(1<<flt.mantbits) == 0 {
			exp = flt.bias
		}
		goto out

	overflow:
		// Â±inf
		mant = 0
		exp = int(uint(1)<<flt.expbits - 1 + uint(flt.bias))
		overflow = true

	out:
		// Assemble bits.
		mut bits := mant & (1<<flt.mantbits - 1)
		bits |= u64((exp-flt.bias)&int(1<<flt.expbits-1)) << flt.mantbits
		if self.neg {
			bits |= 1 << flt.mantbits << flt.expbits
		}
		ret bits, overflow
	}
}

// Exact powers of 10.
static f64pow10: []f64 = [
	1e0, 1e1, 1e2, 1e3, 1e4, 1e5, 1e6, 1e7, 1e8, 1e9,
	1e10, 1e11, 1e12, 1e13, 1e14, 1e15, 1e16, 1e17, 1e18, 1e19,
	1e20, 1e21, 1e22,
]
static f32pow10: []f32 = [1e0, 1e1, 1e2, 1e3, 1e4, 1e5, 1e6, 1e7, 1e8, 1e9, 1e10]

// If possible to convert decimal representation to 64-bit float f exactly,
// entirely in floating-point math, do so, avoiding the expense of decimal_to_float_bits.
// Three common cases:
//  value is exact integer
//  value is exact integer * exact power of ten
//  value is exact integer / exact power of ten
//
// These all produce potentially inexact but correctly rounded answers.
fn atof64Exact(mantissa: u64, mut exp: int, neg: bool): (f: f64, ok: bool) {
	if mantissa>>f64info.mantbits != 0 {
		ret
	}
	f = f64(mantissa)
	if neg {
		f = -f
	}
	match {
	| exp == 0:
		// an integer.
		ret f, true
		// Exact integers are <= 10^15.
		// Exact powers of ten are <= 10^22.
	| exp > 0 && exp <= 15+22: // int * 10^k
		// If exponent is big but number of digits is not,
		// can move a few zeros into the integer part.
		if exp > 22 {
			f *= f64pow10[exp-22]
			exp = 22
		}
		if f > 1e15 || f < -1e15 {
			// the exponent was really too large.
			ret
		}
		ret f * f64pow10[exp], true
	| exp < 0 && exp >= -22: // int / 10^k
		ret f / f64pow10[-exp], true
	}
	ret
}

// If possible to convert decimal representation to 64-bit float f exactly,
// entirely in floating-point math, do so, avoiding the expense of decimalToFloatBits.
// Three common cases:
//  value is exact integer
//  value is exact integer * exact power of ten
//  value is exact integer / exact power of ten
// These all produce potentially inexact but correctly rounded answers.
fn atof64exact(mantissa: u64, mut exp: int, neg: bool): (f: f64, ok: bool) {
	if mantissa>>f64info.mantbits != 0 {
		ret
	}
	f = f64(mantissa)
	if neg {
		f = -f
	}
	match {
	| exp == 0:
		// an integer.
		ret f, true
		// Exact integers are <= 10^15.
		// Exact powers of ten are <= 10^22.
	| exp > 0 && exp <= 15+22: // int * 10^k
		// If exponent is big but number of digits is not,
		// can move a few zeros into the integer part.
		if exp > 22 {
			f *= f64pow10[exp-22]
			exp = 22
		}
		if f > 1e15 || f < -1e15 {
			// the exponent was really too large.
			ret
		}
		ret f * f64pow10[exp], true
	| exp < 0 && exp >= -22: // int / 10^k
		ret f / f64pow10[-exp], true
	}
	ret
}

// If possible to compute mantissa*10^exp to 32-bit float f exactly,
// entirely in floating-point math, do so, avoiding the machinery above.
fn atof32exact(mantissa: u64, mut exp: int, mut neg: bool): (f: f32, ok: bool) {
	if mantissa>>f32info.mantbits != 0 {
		ret
	}
	f = f32(mantissa)
	if neg {
		f = -f
	}
	match {
	| exp == 0:
		ret f, true
		// Exact integers are <= 10^7.
		// Exact powers of ten are <= 10^10.
	| exp > 0 && exp <= 7+10: // int * 10^k
		// If exponent is big but number of digits is not,
		// can move a few zeros into the integer part.
		if exp > 10 {
			f *= f32pow10[exp-10]
			exp = 10
		}
		if f > 1e7 || f < -1e7 {
			// the exponent was really too large.
			ret
		}
		ret f * f32pow10[exp], true
	| exp < 0 && exp >= -10: // int / 10^k
		ret f / f32pow10[-exp], true
	}
	ret
}

// Converts the hex floating-point string s
// to a rounded f32 or f64 value (depending on flt==&f32_info or flt==&f64_info)
// and returns it as a f64.
// The string s has already been parsed into a mantissa, exponent, and sign (neg==true for negative).
// If trunc is true, trailing non-zero bits have been omitted from the mantissa.
fn atofHex(&flt: floatInfo, mut mantissa: u64, mut exp: int, neg: bool, trunc: bool): (f64, ConvError) {
	maxExp := int(1<<flt.expbits) + flt.bias - 2
	minExp := flt.bias + 1
	exp += int(flt.mantbits) // mantissa now implicitly divided by 2^mantbits.

	// Shift mantissa and exponent to bring representation into float range.
	// Eventually we want a mantissa with a leading 1-bit followed by mantbits other bits.
	// For rounding, we need two more, where the bottom bit represents
	// whether that bit or any later bit was non-zero.
	// (If the mantissa has already lost non-zero bits, trunc is true,
	// and we OR in a 1 below after shifting left appropriately.)
	for mantissa != 0 && mantissa>>(flt.mantbits+2) == 0 {
		mantissa <<= 1
		exp--
	}
	if trunc {
		mantissa |= 1
	}
	for mantissa>>(1+flt.mantbits+2) != 0 {
		mantissa = mantissa>>1 | mantissa&1
		exp++
	}

	// If exponent is too negative,
	// denormalize in hopes of making it representable.
	// (The -2 is for the rounding bits.)
	for mantissa > 1 && exp < minExp-2 {
		mantissa = mantissa>>1 | mantissa&1
		exp++
	}

	// Round using two bottom bits.
	mut round := mantissa & 3
	mantissa >>= 2
	round |= mantissa & 1 // round to even (round up if mantissa is odd)
	exp += 2
	if round == 3 {
		mantissa++
		if mantissa == 1<<(1+flt.mantbits) {
			mantissa >>= 1
			exp++
		}
	}

	if mantissa>>flt.mantbits == 0 { // Denormal or zero.
		exp = flt.bias
	}
	mut err := ConvError.Ok
	if exp > maxExp { // infinity and range error
		mantissa = u64(1 << flt.mantbits)
		exp = maxExp + 1
		err = ConvError.OutOfRange
	}

	mut bits := mantissa & (1<<flt.mantbits - 1)
	bits |= u64((exp-flt.bias)&int(1<<flt.expbits-1)) << flt.mantbits
	if neg {
		bits |= 1 << flt.mantbits << flt.expbits
	}
	if flt == f32info {
		ret f64(math::F32FromBits(u32(bits))), err
	}
	ret math::F64FromBits(bits), err
}

fn atof32(s: str): (f: f32, n: int, err: ConvError) {
	{
		val, _n, ok := special(s)
		if ok {
			ret f32(val), _n, ConvError.Ok
		}
	}

	mantissa, exp, neg, trunc, hex, _n, mut ok := readFloat(s)
	n = _n
	if !ok {
		ret 0, n, ConvError.InvalidSyntax
	}

	if hex {
		mut ff := 0.
		ff, err = atofHex(f32info, mantissa, exp, neg, trunc)
		ret f32(ff), n, err
	}

	if optimize {
		// Try pure floating-point arithmetic conversion, and if that fails,
		// the Eisel-Lemire algorithm.
		if !trunc {
			f, ok = atof32exact(mantissa, exp, neg)
			if ok {
				ret f, n, ConvError.Ok
			}
		}
		f, ok = eiselLemire32(mantissa, exp, neg)
		if ok {
			if !trunc {
				ret f, n, ConvError.Ok
			}
			// Even if the mantissa was truncated, we may
			// have found the correct result. Confirm by
			// converting the upper mantissa bound.
			mut fUp := f32(0)
			fUp, ok = eiselLemire32(mantissa+1, exp, neg)
			if ok && f == fUp {
				ret f, n, ConvError.Ok
			}
		}
	}

	// Slow fallback.
	mut d := decimal{}
	if !d.set(unsafe::StrBytes(s)[:n]) {
		ret 0, n, ConvError.InvalidSyntax
	}
	b, ovf := d.floatBits(f32info)
	f = math::F32FromBits(u32(b))
	if ovf {
		err = ConvError.OutOfRange
	}
	ret f, n, err
}

fn atof64(s: str): (f: f64, n: int, err: ConvError) {
	{
		val, _n, ok := special(s)
		if ok {
			ret val, _n, ConvError.Ok
		}
	}

	mantissa, exp, neg, trunc, hex, _n, mut ok := readFloat(s)
	n = _n
	if !ok {
		ret 0, n, ConvError.InvalidSyntax
	}

	if hex {
		f, err = atofHex(f64info, mantissa, exp, neg, trunc)
		ret f, n, err
	}

	if optimize {
		// Try pure floating-point arithmetic conversion, and if that fails,
		// the Eisel-Lemire algorithm.
		if !trunc {
			f, ok = atof64exact(mantissa, exp, neg)
			if ok {
				ret f, n, ConvError.Ok
			}
		}
		f, ok = eiselLemire64(mantissa, exp, neg)
		if ok {
			if !trunc {
				ret f, n, ConvError.Ok
			}
			// Even if the mantissa was truncated, we may
			// have found the correct result. Confirm by
			// converting the upper mantissa bound.
			mut fUp := 0.
			fUp, ok = eiselLemire64(mantissa+1, exp, neg)
			if ok && f == fUp {
				ret f, n, ConvError.Ok
			}
		}
	}

	// Slow fallback.
	mut d := decimal{}
	if !d.set(unsafe::StrBytes(s)[:n]) {
		ret 0, n, ConvError.InvalidSyntax
	}
	b, ovf := d.floatBits(f64info)
	f = math::F64FromBits(b)
	if ovf {
		err = ConvError.OutOfRange
	}
	ret f, n, err
}

fn parseFloatPrefix(&s: str, bitSize: int): (f64, int, ConvError) {
	if bitSize == 32 {
		f, n, err := atof32(s)
		ret f64(f), n, err
	}
	ret atof64(s)
}

// Converts the string s to a floating-point number
// with the precision specified by bitSize: 32 for f32, or 64 for f64.
// When bitSize=32, the result still has type f64, but it will be
// convertible to f32 without changing its value.
//
// Accepts decimal and hexadecimal floating-point numbers
// as defined by the Jule syntax for [floating-point literals].
// If s is well-formed and near a valid floating-point number,
// returns the nearest floating-point number rounded
// using IEEE754 unbiased rounding.
// (Parsing a hexadecimal floating-point value only rounds when
// there are more bits in the hexadecimal representation than
// will fit in the mantissa.)
//
// The exceptional errors that have concrete type ConvError.
//
// If s is not syntactically well-formed, throws exception = ConvError.InvalidSyntax.
//
// If s is syntactically well-formed but is more than 1/2 ULP
// away from the largest floating point number of the given size,
// Exceptional = ConvError.OutOfRange.
//
// Recognizes the string "NaN", and the (possibly signed) strings "inf" and "infinity"
// as their respective special floating point values. It ignores case when matching.
fn ParseFloat(s: str, bitSize: int)!: f64 {
	f, n, err := parseFloatPrefix(s, bitSize)
	if n != len(s) && (err == ConvError.Ok || err != ConvError.InvalidSyntax) {
		error(ConvError.InvalidSyntax)
	}
	if err != ConvError.Ok {
		error(err)
	}
	ret f
}