// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "env"
use "obj"
use "obj/meta"
use "std/jule"
use "std/jule/ast"
use "std/jule/build"
use "std/jule/sema"

struct liveTable {
	vars:    []&sema::Var
	fns:     []&sema::FuncIns
	structs: []&sema::StructIns
	traits:  []&sema::Trait
}

struct ObjectDeadCode {
	live: liveTable
	ir:   &obj::IR
}

impl ObjectDeadCode {
	static fn new(mut &ir: &obj::IR): &ObjectDeadCode {
		ret &ObjectDeadCode{
			ir: ir,
		}
	}

	fn isLive[T](mut &self, &t: T): bool {
		let mut live: []T = nil
		const match type T {
		| &sema::FuncIns:
			live = self.live.fns
		| &sema::Var:
			live = self.live.vars
		| &sema::StructIns:
			live = self.live.structs
		| &sema::Trait:
			live = self.live.traits
		}
		for i in live {
			if live[i] == t {
				ret true
			}
		}
		ret false
	}

	fn pushLive[T](mut &self, mut t: T) {
		const match type T {
		| &sema::Var:
			self.live.vars = append(self.live.vars, t)
		| &sema::FuncIns:
			self.live.fns = append(self.live.fns, t)
		| &sema::StructIns:
			self.live.structs = append(self.live.structs, t)
		| &sema::Trait:
			self.live.traits = append(self.live.traits, t)
		}
	}

	fn pushStructAsLive(mut &self, mut &s: &sema::StructIns) {
		if self.isLive[&sema::StructIns](s) {
			ret
		}
		self.pushLive[&sema::StructIns](s)
		self.setReferencesAsLive(s.Refers)
		allLive := isAllMethodsLive(s.Decl)
		for (_, mut m) in s.Methods {
			for (_, mut ins) in m.Instances {
				if self.isLive[&sema::FuncIns](ins) {
					continue
				}

				// Push all methods as live if s is requires all methods should be live.
				if allLive {
					self.pushLive[&sema::FuncIns](ins)
					self.setReferencesAsLive(ins.Refers)
					continue
				}

				// Set trait implemented methods as live.
				// Push as live the method if implements a trait's method.
				// Other methods will be marked as live by referenced defines,
				// no need for special tracking algorithm to caught.
				if obj::IsTraitMethod(s, ins) {
					self.pushLive[&sema::FuncIns](ins)
					self.setReferencesAsLive(ins.Refers)
				}

				// Set reserved methods as live.
				// So, removing these methods may cause compilation problems,
				// or unexpected program bavior.
				const Binded = false

				mut _str := s.FindMethod("Str", Binded)
				if _str != nil && sema::FuncPattern.Str(_str) {
					mut fins := _str.Instances[0]
					self.pushLive[&sema::FuncIns](fins)
					self.setReferencesAsLive(fins.Refers)
				}
			}
		}
	}

	fn setReferencesAsLive(mut &self, mut &rs: &sema::ReferenceStack) {
		if rs == nil {
			ret
		}
		mut i := 0
		for i < rs.Len(); i++ {
			mut ref := rs.At(i)
			match type ref {
			| &sema::Trait:
				mut t := (&sema::Trait)(ref)
				if self.isLive[&sema::Trait](t) {
					continue
				}
				self.pushLive[&sema::Trait](t)
				for (_, mut ins) in t.Methods {
					for (_, mut mins) in ins.Instances {
						if self.isLive[&sema::FuncIns](mins) {
							continue
						}
						self.pushLive[&sema::FuncIns](mins)
						self.setReferencesAsLive(mins.Refers)
					}
				}
			| &sema::FuncIns:
				mut f := (&sema::FuncIns)(ref)
				if self.isLive[&sema::FuncIns](f) {
					break
				}
				if f.Owner != nil && !f.Decl.Statically {
					if !self.isLive[&sema::StructIns](f.Owner) {
						self.pushLive[&sema::StructIns](f.Owner)
						self.setReferencesAsLive(f.Owner.Refers)
					}
				}
				self.pushLive[&sema::FuncIns](f)
				self.setReferencesAsLive(f.Refers)
			| &sema::Var:
				mut v := (&sema::Var)(ref)
				if self.isLive[&sema::Var](v) {
					break
				}
				self.pushLive[&sema::Var](v)
				self.setReferencesAsLive(v.Refers)
			| &sema::StructIns:
				mut s := (&sema::StructIns)(ref)
				self.pushStructAsLive(s)
			}
		}
	}

	fn inits(mut &self, mut &pkg: &sema::Package) {
		for (_, mut file) in pkg.Files {
			for (_, mut f) in file.Funcs {
				if f.Ident == jule::InitFunc {
					mut ins := f.Instances[0]
					if !self.isLive[&sema::FuncIns](ins) {
						self.pushLive[&sema::FuncIns](ins)
					}
					self.setReferencesAsLive(ins.Refers)
				}
			}
		}
	}

	fn collectLivePackage(mut &self, mut &pkg: &sema::Package) {
		// Collect live references based on initializer functions.
		self.inits(pkg)

		// collect api defines as live
		obj::IterFiles(pkg, fn(mut &file: &sema::SymTab) {
			for (_, mut v) in file.Vars {
				if obj::HasDirective(v.Directives, build::Directive.Export) {
					if !self.isLive[&sema::Var](v) {
						self.pushLive[&sema::Var](v)
						self.setReferencesAsLive(v.Refers)
					}
				}
			}
			for (_, mut f) in file.Funcs {
				if obj::HasDirective(f.Directives, build::Directive.Export) {
					for (_, mut ins) in f.Instances {
						if !self.isLive[&sema::FuncIns](ins) {
							self.pushLive[&sema::FuncIns](ins)
							self.setReferencesAsLive(ins.Refers)
						}
					}
				}
			}
		})

		// Collect test functions and the testing::{T} struct if test compilation is enabled.
		if env::Test {
			if len(pkg.Files) > 0 && obj::IsStdPackage(pkg.Files[0].File.Path, "testing") {
				const Binded = false
				mut s := pkg.FindStruct("T", Binded)
				mut ins := s.Instances[0]
				self.pushStructAsLive(ins)
				for (_, mut m) in ins.Methods {
					for (_, mut mins) in m.Instances {
						if self.isLive[&sema::FuncIns](mins) {
							continue
						}
						self.pushLive[&sema::FuncIns](mins)
						self.setReferencesAsLive(mins.Refers)
					}
				}
			}
			for (_, mut file) in pkg.Files {
				for (_, mut f) in file.Funcs {
					if obj::HasDirective(f.Directives, build::Directive.Test) {
						mut ins := f.Instances[0]
						if !self.isLive[&sema::FuncIns](ins) {
							self.pushLive[&sema::FuncIns](ins)
						}
						self.setReferencesAsLive(ins.Refers)
					}
				}
			}
		}
	}

	fn collectLive(mut &self) {
		// Special cases.
		self.pushLive[&sema::FuncIns](meta::Program.Runtime.Init)
		self.setReferencesAsLive(meta::Program.Runtime.Init.Refers)
		self.pushLive[&sema::FuncIns](meta::Program.Runtime.CloseThread)
		self.setReferencesAsLive(meta::Program.Runtime.CloseThread.Refers)

		for (_, mut used) in self.ir.Used {
			if !used.Binded {
				self.collectLivePackage(used.Package)
			}
		}
		self.collectLivePackage(self.ir.Main)

		// Push live references based on entry point.
		mut main := self.ir.Main.FindFunc(jule::EntryPoint, false)
		if main != nil {
			mut ins := main.Instances[0]
			if !self.isLive[&sema::FuncIns](ins) {
				self.pushLive[&sema::FuncIns](ins)
			}
			self.setReferencesAsLive(ins.Refers)
		}

		// Finally analysis is done. We have to mark as live methods of the
		// structures which is implementes a live trait. Because a trait
		// may use the method, but following remove algorithm of the analysis
		// will may remove the trait methods because of zero references.
		// So push as live the methods of implemented live structures.
		for (_, mut t) in self.live.traits {
			for (_, mut s) in t.Implemented {
				for (_, mut ins) in s.Instances {
					if !self.isLive[&sema::StructIns](ins) {
						continue
					}
					for _, tm in t.Methods {
						const Static = false
						mut m := ins.FindMethod(tm.Ident, Static)
						for (_, mut mins) in m.Instances {
							if !self.isLive[&sema::FuncIns](mins) {
								self.pushLive[&sema::FuncIns](mins)
								self.setReferencesAsLive(mins.Refers)
							}
						}
					}
				}
			}
		}
	}

	fn removeDeadGlobals(mut &self, mut &vars: []&sema::Var) {
		mut i := 0
		for i < len(vars) {
			v := vars[i]
			if self.isLive[&sema::Var](v) {
				i++
				continue
			}
			vars = append(vars[:i], vars[i+1:]...)
		}
	}

	fn removeDeadFuncs(mut &self, mut &funcs: []&sema::Func) {
		mut i := 0
		for i < len(funcs) {
			mut f := funcs[i]
			mut j := 0
			for j < len(f.Instances) {
				ins := f.Instances[j]
				if self.isLive[&sema::FuncIns](ins) {
					j++
					continue
				}
				f.Instances = append(f.Instances[:j], f.Instances[j+1:]...)
			}
			if len(f.Instances) == 0 {
				funcs = append(funcs[:i], funcs[i+1:]...)
				continue
			}
			i++
		}
	}

	fn removeDeadStructs(mut &self, mut &structs: []&sema::Struct) {
		mut i := 0
		for i < len(structs) {
			mut s := structs[i]
			mut j := 0
			for j < len(s.Instances) {
				mut ins := s.Instances[j]
				self.removeDeadFuncs(ins.Methods)
				if len(ins.Methods) != 0 || self.isLive[&sema::StructIns](ins) {
					j++
					continue
				}
				s.Instances = append(s.Instances[:j], s.Instances[j+1:]...)
			}
			if len(s.Instances) == 0 {
				structs = append(structs[:i], structs[i+1:]...)
				continue
			}
			i++
		}
	}

	fn removeDeadTraits(mut &self, mut &traits: []&sema::Trait) {
		mut i := 0
		for i < len(traits) {
			mut t := traits[i]
			if !self.isLive[&sema::Trait](t) {
				traits = append(traits[:i], traits[i+1:]...)
				continue
			}
			mut j := 0
			for j < len(t.Implemented) {
				s := t.Implemented[j]
				if len(s.Instances) > 0 {
					j++
					continue
				}
				t.Implemented = append(t.Implemented[:j], t.Implemented[j+1:]...)
			}
			i++
		}
	}

	fn removeDeadStrictTypeAliases(mut &self, mut &aliases: []&sema::TypeAlias) {
		mut i := 0
		for i < len(aliases) {
			mut ta := aliases[i]
			if ta.Strict {
				mut s := (&sema::StructIns)(ta.TypeSym.Type.Kind).Decl
				mut j := 0
				for j < len(s.Instances) {
					mut ins := s.Instances[j]
					self.removeDeadFuncs(ins.Methods)
					if len(ins.Methods) != 0 || self.isLive[&sema::StructIns](ins) {
						j++
						continue
					}
					s.Instances = append(s.Instances[:j], s.Instances[j+1:]...)
				}
				if len(s.Instances) == 0 {
					aliases = append(aliases[:i], aliases[i+1:]...)
					continue
				}
			}
			i++
		}
	}

	fn removeDeadsFile(mut &self, mut &file: &sema::SymTab) {
		self.removeDeadFuncs(file.Funcs)
		self.removeDeadStructs(file.Structs)
		self.removeDeadTraits(file.Traits)
		self.removeDeadGlobals(file.Vars)
		self.removeDeadStrictTypeAliases(file.TypeAliases)
	}

	fn removeDeadsPackage(mut &self, mut &pkg: &sema::Package) {
		for (_, mut file) in pkg.Files {
			self.removeDeadsFile(file)
		}
	}

	fn removeDeads(mut &self) {
		for (_, mut used) in self.ir.Used {
			if !used.Binded {
				self.removeDeadsPackage(used.Package)
			}
		}
		self.removeDeadsPackage(self.ir.Main)
	}

	fn elimanate(mut &self) {
		self.collectLive()
		self.removeDeads()
	}
}

fn EliminateDefines(mut &ir: &obj::IR) {
	mut ocd := ObjectDeadCode.new(ir)
	ocd.elimanate()
}

// Reports whether all methods live of structure.
fn isAllMethodsLive(s: &sema::Struct): bool {
	ret s == meta::Program.Runtime.Map ||
		s == meta::Program.Runtime.MapIterator ||
		s == meta::Program.Runtime.Pchan
}