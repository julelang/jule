// Copyright 2024-2025 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "obj"
use "std/jule/sema"

// Dead code eliminate optimizer for scopes.
struct scopeDeadCode {
	parent: &scopeDeadCode
	s:      &sema::Scope
	i:      int // Position.
	labels: &[]&sema::Label
	gotos:  &[]&sema::Goto
	except: bool
}

impl scopeDeadCode {
	fn optimizeChild(mut &self, mut &s: &sema::Scope) {
		mut sdc := &scopeDeadCode{
			parent: self,
			s: s,
			labels: self.labels,
			gotos: self.gotos,
		}
		unsafe { sdc.optimizeStmts() }
	}

	fn optimizeBodyChildExceptional(mut &self, mut &s: &sema::Scope) {
		mut sdc := &scopeDeadCode{
			s: s,
			labels: self.labels,
			gotos: self.gotos,
			except: true,
		}
		unsafe { sdc.optimizeStmts() }
	}

	fn optimizeBodyChild(mut &self, mut &s: &sema::Scope) {
		mut sdc := &scopeDeadCode{
			s: s,
			labels: self.labels,
			gotos: self.gotos,
		}
		unsafe { sdc.optimizeStmts() }
	}

	fn optimizeExpr(mut &self, mut &model: sema::Expr) {
		edc := exprDeadCode.new(self)
		edc.optimize(model)
	}

	fn eliminateFollowed(mut self) {
		if len(*self.gotos) != 0 {
			ret
		}
		unsafe {
			self.s.Stmts = self.s.Stmts[:self.i+1]
		}
		mut parent := self.parent
		for parent != nil {
			unsafe {
				parent.s.Stmts = parent.s.Stmts[:parent.i+1]
			}
			parent = parent.parent
		}
	}

	fn pushGoto(mut &self, mut gt: &sema::Goto) {
		mut i := 0
		for i < len(*self.labels); i++ {
			if gt.Ident == (*self.labels)[i].Ident {
				ret
			}
		}
		*self.gotos = append(*self.gotos, gt)
	}

	fn pushLabel(mut &self, mut l: &sema::Label) {
		mut i := 0
		for i < len(*self.gotos) {
			if l.Ident == (*self.gotos)[i].Ident {
				copy((*self.gotos)[i:], (*self.gotos)[i+1:])
				*self.gotos = (*self.gotos)[:len(*self.gotos)-1]
				continue
			}
			i++
		}
		*self.labels = append(*self.labels, l)
	}

	fn optimizeRangeIter(mut &self, mut it: &sema::RangeIter) {
		self.optimizeExpr(it.Expr.Model)
		self.optimizeBodyChild(it.Scope)
	}

	fn optimizeWhileIter(mut &self, mut it: &sema::WhileIter) {
		self.optimizeExpr(it.Expr)
		if it.Next != nil {
			self.optimizeStmt(it.Next)
		}
		self.optimizeBodyChild(it.Scope)
	}

	fn optimizeInfIter(mut &self, mut it: &sema::InfIter) {
		self.optimizeBodyChild(it.Scope)
	}

	fn optimizeConditional(mut &self, mut c: &sema::Conditional) {
		for (_, mut case) in c.Elifs {
			if case != nil {
				self.optimizeExpr(case.Expr)
				self.optimizeBodyChild(case.Scope)
			}
		}
		if c.Default != nil {
			if len(c.Elifs) == 0 {
				// Use just child.
				// There one case, this default scope can accepted as plain anonymous scope.
				self.optimizeChild(c.Default.Scope)
			} else {
				self.optimizeBodyChild(c.Default.Scope)
			}
		}
	}

	fn optimizeMatch(mut &self, mut m: &sema::Match) {
		for (_, mut case) in m.Cases {
			for (_, mut expr) in case.Exprs {
				self.optimizeExpr(expr.Model)
			}
			self.optimizeBodyChild(case.Scope)
		}
		if m.Default != nil {
			if len(m.Cases) == 0 {
				// Use just child.
				// There one case, this default scope can accepted as plain anonymous scope.
				self.optimizeChild(m.Default.Scope)
			} else {
				self.optimizeBodyChild(m.Default.Scope)
			}
		}
	}

	fn optimizeAssign(mut &self, mut assign: &sema::Assign) {
		self.optimizeExpr(assign.Left.Model)
		self.optimizeExpr(assign.Right.Model)
	}

	fn optimizeMultiAssign(mut &self, mut assign: &sema::MultiAssign) {
		for (_, mut l) in assign.Left {
			if l != nil {
				self.optimizeExpr(l.Model)
			}
		}
		self.optimizeExpr(assign.Right)
	}

	fn optimizeStmt(mut &self, mut st: sema::Stmt) {
		match type st {
		| &sema::Scope:
			mut scope := st.(&sema::Scope)
			if scope.Deferred {
				eliminateDeadCodeOfScope(scope)
			} else {
				self.optimizeChild(scope)
			}
		| &sema::RangeIter:
			self.optimizeRangeIter(st.(&sema::RangeIter))
		| &sema::WhileIter:
			self.optimizeWhileIter(st.(&sema::WhileIter))
		| &sema::InfIter:
			self.optimizeInfIter(st.(&sema::InfIter))
		| &sema::Conditional:
			self.optimizeConditional(st.(&sema::Conditional))
		| &sema::Match:
			self.optimizeMatch(st.(&sema::Match))
		| &sema::Assign:
			self.optimizeAssign(st.(&sema::Assign))
		| &sema::MultiAssign:
			self.optimizeMultiAssign(st.(&sema::MultiAssign))
		| &sema::Value:
			mut val := st.(&sema::Value)
			if self.except {
				break
			}
			match type val.Model {
			| &sema::BuiltinErrorCallExpr
			| &sema::BuiltinPanicCallExpr:
				// Remove followed statements of the function call.
				// Unreachable code.
				self.eliminateFollowed()
			|:
				self.optimizeExpr(val.Model)
			}
		| &sema::Ret:
			// Remove followed statements of the return statement.
			// Unreachable code.
			self.eliminateFollowed()
		| &sema::Select:
			sl := st.(&sema::Select)
			if len(sl.Cases) == 0 && sl.Default == nil {
				// Remove followed statements of the empty select statement.
				// Unreachable code.
				self.eliminateFollowed()
			}
		| &sema::Goto:
			self.pushGoto(st.(&sema::Goto))
			// Remove followed statements of the return statement.
			// Unreachable code.
			self.eliminateFollowed()
		| &sema::Label:
			self.pushLabel(st.(&sema::Label))
		|:
			// no-op
		}
	}

	unsafe fn optimizeStmts(mut &self) {
		self.i = 0
		for self.i < len(self.s.Stmts); self.i++ {
			self.optimizeStmt(self.s.Stmts[self.i])
		}
	}
}

fn eliminateDeadCodeOfScope(mut s: &sema::Scope) {
	labels := make([]&sema::Label, 0, 1<<4)
	gotos := make([]&sema::Goto, 0, 1<<4)
	mut sdc := &scopeDeadCode{
		s: s,
		labels: unsafe { (&[]&sema::Label)(&labels) },
		gotos: unsafe { (&[]&sema::Goto)(&gotos) },
	}
	unsafe { sdc.optimizeStmts() }
}

// Eliminates dead scope elements of functions.
fn eliminateScopeFunctions(mut &funcs: []&sema::Func) {
	for (_, mut func) in funcs {
		for (_, mut ins) in func.Instances {
			eliminateDeadCodeOfScope(ins.Scope)
		}
	}
}

// Eliminates dead scope elements of struct methods.
fn eliminateScopeStructMethods(mut &structs: []&sema::Struct) {
	for (_, mut s) in structs {
		for (_, mut ins) in s.Instances {
			eliminateScopeFunctions(ins.Methods)
		}
	}
}

fn eliminateFunctionsScopePackage(mut &pkg: &sema::Package) {
	for (_, mut f) in pkg.Files {
		eliminateScopeFunctions(f.Funcs)
		eliminateScopeStructMethods(f.Structs)
	}
}

fn eliminateScopePackage(mut &pkg: &sema::Package) {
	eliminateFunctionsScopePackage(pkg)
}

// Eliminate dead scope codes.
fn EliminateScopes(mut &ir: &obj::IR) {
	mut i := len(ir.Used) - 1
	for i >= 0; i-- {
		mut u := ir.Used[i]
		if !u.Binded {
			eliminateScopePackage(u.Package)
		}
	}
	eliminateScopePackage(ir.Main)
}