// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "env"
use "obj"
use "std/jule/build"
use "std/jule/constant"
use "std/jule/sema"
use "std/jule/token"
use "std/os/filepath"
use "std/strings"

// Scope optimizer that applies target-independent optimizations.
struct scopeOptimizer {
	parent: &scopeOptimizer
	i:      int
	scope:  &sema::Scope
	data:   &data // Should be non-nil guaranteed.
}

impl scopeOptimizer {
	static fn new(mut scope: &sema::Scope): &scopeOptimizer {
		mut sc := &scopeOptimizer{
			scope: scope,
			data: &data{
				boundary: new(boundary),
				nils: new(nils),
				dynamic: new(dynamic),
			},
		}
		ret sc
	}

	fn setCurrentStmt(mut &self, mut stmt: any) {
		self.scope.Stmts[self.i] = unsafe { *(*sema::Stmt)(&stmt) }
	}

	// Removes current statement.
	fn removeCurrent(mut &self) {
		self.scope.Stmts = append(self.scope.Stmts[:self.i], self.scope.Stmts[self.i+1:]...)
	}

	fn isLastStmt(mut &self): bool {
		mut r := self
		for r != nil; r = r.parent {
			if len(r.scope.Stmts)-r.i != 1 {
				ret false
			}
		}
		ret true
	}

	fn isIter(mut &self): bool {
		mut r := self.parent
		for r != nil; r = r.parent {
			match type r.scope.Stmts[r.i] {
			| &sema::RangeIter
			| &sema::WhileIter
			| &sema::InfIter:
				ret true
			}
		}
		ret false
	}

	fn optimizeData(mut &self, mut d: &sema::Data) {
		match type d.Model {
		| &sema::FnCallExprModel:
			mut m := (&sema::FnCallExprModel)(d.Model)
			if env::Production {
				if !m.Func.IsBuiltin() &&
					obj::IsStdPackage(m.Func.Decl.Token.File.Path, "debug") {
					self.setCurrentStmt(nil)
					ret
				}
			}
			exprOptimizer.optimizeData(d.Model, self.data, self)
			if Exceptional &&
				m.Func.Decl.Exceptional &&
				m.Except != nil &&
				len(m.Except.Stmts) == 1 &&
				obj::IsForwarded(m.Except) &&
				self.isLastStmt() &&
				!self.isIter() {
				self.setCurrentStmt(&ExceptionalForwardingExprModel{Expr: m})
			}
			ret
		}

		// CAST production is equals to &Data statement for "_ = EXPR" assignments.
		// And there is no informative or useful optimization data for some optimizations.
		// Therefore avoid using this expressions as useful data for relevant optimizations.
		dataFlags := self.data.flags
		self.data.flags |= dataFlag.NoConditional
		exprOptimizer.optimizeData(d.Model, self.data, self)
		self.data.flags = dataFlags
	}

	fn optimizeVar(mut &self, mut v: &sema::Var) {
		if v.Value != nil {
			if self.data.nils != nil &&
				isNilValidType(v.TypeSym.Type) &&
				isGuaranteedNonNilExpr(self.data.nils, v.Value.Data.Model) {
				const safe = true
				self.data.nils.pushVar(getNilVar(v), safe)
			}
			if self.data.dynamic != nil {
				mut kind := isTypeGuaranteedDynamicData(
					self.data.dynamic, v.TypeSym.Type, v.Value.Data.Model)
				if kind != nil {
					if isDynamicValidType(v.TypeSym.Type) {
						self.data.dynamic.pushVar(getDynamicVar(v), kind)
					}
				}
			}
			exprOptimizer.optimizeData(v.Value.Data.Model, self.data, self)
		}
	}

	fn optimizeConditional(mut &self, mut c: &sema::Conditional) {
		// Handle like optimizeChildHard(), but avoid calling this method for each scope.
		// So wrap same functionality and apply optimizations by calling the optimizeChild().
		// See documentation of the optimizeChildHard() for more information about difference.
		mut alive := data{}
		alive.loadCheckpoint(self.data.getCheckpoint())

		// Use checkpoint for the self.data to use same data for each child scope.
		mut checkpoint := self.data.getCheckpoint()

		for (_, mut elif) in c.Elifs {
			exprOptimizer.optimizeData(elif.Expr, self.data, self)
			self.optimizeChild(elif.Scope)
			alive.removeDeads(self.data)
			self.data.loadCheckpoint(checkpoint)
		}
		if c.Default != nil {
			self.optimizeChild(c.Default.Scope)
			alive.removeDeads(self.data)
		}

		self.data.loadCheckpoint(alive.getMutCheckpoint())

		if !Cond {
			ret
		}

		mut constCase := false // Has cosntant true case.

		// Remove unnecessary trailing cases that comes after constant true case.
		for (i, mut elif) in c.Elifs {
			if !isConstantValidConditionalCase(elif) {
				continue
			}
			constCase = true
			c.Elifs = c.Elifs[:i]
			c.Default = &sema::Else{
				Scope: elif.Scope,
			}
			break
		}

		if len(c.Elifs) == 0 {
			ret
		}

		// Remove unreachable cases.
		mut i := &c.Elifs[0]
		end := &c.Elifs[len(c.Elifs)-1]
		for i <= end; i++ {
			unsafe {
				if isUnreachableConditionalCase(*i) {
					*i = nil
				}
			}
		}

		if len(c.Elifs) == 0 {
			ret
		}

		// Skip one-case checking if const-case is not exist.
		if !constCase {
			ret
		}

		mut only := -1
		for j, elif in c.Elifs {
			if elif != nil {
				if only != -1 {
					// Break checking, there is more than one case.
					ret
				}
				only = j
			}
		}

		// Here is one case.
		if only != -1 {
			self.setCurrentStmt(c.Elifs[only].Scope)
		}
	}

	fn buildDefaultConstantTrueCase(mut &self, mut case: &sema::Case, mut &i: int, mut &m: &sema::Match) {
		case.Exprs = nil // Remove expressions to remove eval overhead.
		start := i
	loop:
		for {
			if len(case.Scope.Stmts) == 0 {
				break
			}
			mut &stmt := unsafe { *(&case.Scope.Stmts[len(case.Scope.Stmts)-1]) }
			match type stmt {
			| &sema::FallSt:
				i++
				if i >= len(m.Cases) {
					stmt = m.Default.Scope
					break loop
				}
				case = m.Cases[i]
				stmt = case.Scope
			|:
				break loop
			}
		}
		m.Default = m.Cases[start]
		m.Cases = m.Cases[:start]
	}

	fn optimizeMatch(mut &self, mut m: &sema::Match) {
		mut var := invalidDynamic
		if Dynamic && m.TypeMatch {
			var = getDynamicVar(m.Expr.Model)
		}

		// Handle like optimizeChildHard(), but avoid calling this method for each scope.
		// So wrap same functionality and apply optimizations by calling the optimizeChild().
		// See documentation of the optimizeChildHard() for more information about difference.
		mut alive := data{}
		alive.loadCheckpoint(self.data.getCheckpoint())

		// Use checkpoint for the self.data to use same data for each child scope.
		mut checkpoint := self.data.getCheckpoint()

		for (_, mut case) in m.Cases {
			for (_, mut expr) in case.Exprs {
				exprOptimizer.optimizeData(expr.Model, self.data, self)
			}
			if len(case.Exprs) == 1 && self.data.dynamic != nil {
				// We can know the exact kind of dynamic type if expression is single.
				mut kind := isTypeGuaranteedDynamicData(self.data.dynamic, case.Exprs[0].Type, nil)
				if kind != nil {
					self.data.dynamic.pushVar(var, kind)
				} else {
					self.data.dynamic.removeVar(var)
				}
			}
			self.optimizeChild(case.Scope)
			alive.removeDeads(self.data)
			self.data.loadCheckpoint(checkpoint)
		}
		if m.Default != nil {
			self.optimizeChild(m.Default.Scope)
			alive.removeDeads(self.data)
		}

		self.data.loadCheckpoint(alive.getMutCheckpoint())

		if !Cond {
			ret
		}

		mut constCase := false // Has cosntant true case.

		// Remove unnecessary trailing cases that comes after constant true case.
		for (mut i, mut case) in m.Cases {
			if !isConstantValidMatchCase(case) {
				continue
			}
			constCase = true
			self.buildDefaultConstantTrueCase(case, i, m)
			break
		}

		if len(m.Cases) == 0 {
			ret
		}

		// Remove unreachable cases.
		mut i := &m.Cases[0]
		end := &m.Cases[len(m.Cases)-1]
		for i <= end; i++ {
			unsafe {
				if isUnreachableMatchCase(*i) {
					*i = nil
				}
			}
		}

		// Skip one-case checking if const-case is not exist.
		if !constCase {
			ret
		}

		mut only := -1
		for j, case in m.Cases {
			if case != nil {
				if only != -1 {
					// Break checking, there is more than one case.
					ret
				}
				only = j
			}
		}

		// Here is one case.
		if only != -1 {
			self.setCurrentStmt(m.Cases[only].Scope)
		}
	}

	fn optimizeRangeIter(mut &self, mut it: &sema::RangeIter) {
		// Optimize scope first, following alrgorithms related with expression.
		// It might be skip this, so scope optimizater should be guaranteed to run.
		if self.data.boundary != nil && it.KeyA != nil {
			// Add index variable to boundary analysis.
			self.data.boundary.pushVar(getBoundaryVar(it.Expr.Model), it.KeyA)
		}
		self.optimizeChildHard(it.Scope)

		if Iter {
			match type it.Expr.Model {
			| &sema::CastingExprModel:
				mut cem := (&sema::CastingExprModel)(it.Expr.Model)
				eprim := cem.ExprType.Prim()
				if eprim == nil || !eprim.IsStr() {
					break
				}
				mut s := cem.Type.Slc()
				if s == nil {
					break
				}
				prim := s.Elem.Prim()
				if prim == nil {
					break
				}
				match {
				| prim.IsU8():
					// Expression is: []byte(str)
					// Use string expression directly, byte casting is unnecessary.
					it.Expr = cem.Expr
				| prim.IsI32():
					// Expression is: []rune(str)
					// Avoid making allocation, iterate runes of string.
					exprOptimizer.optimizeData(cem.Expr.Model, self.data, self) // Optimize string expression.
					self.setCurrentStmt(&StrRuneIter{
						Expr: cem.Expr,
						Base: it,
					})
					ret
				}
			| &sema::SliceExprModel:
				// make slice expression model iteration as array iteration
				mut slc := (&sema::SliceExprModel)(it.Expr.Model)
				mut arr := &sema::Arr{
					N: len(slc.Elems),
					Elem: slc.ElemType,
				}
				it.Expr.Type.Kind = arr
				it.Expr.Model = &sema::ArrayExprModel{
					Kind: arr,
					Elems: slc.Elems,
				}
			}
		}
		exprOptimizer.optimizeData(it.Expr.Model, self.data, self)
	}

	fn optimizeWhileIter(mut &self, mut it: &sema::WhileIter) {
		exprOptimizer.optimizeData(it.Expr, self.data, self)
		self.optimizeStmt(it.Next)
		self.optimizeChildHard(it.Scope)
	}

	fn substr(mut &self, mut a: &sema::Assign): bool {
		if a.Op.Id != token::Id.Eq {
			ret false
		}
		match type a.Right.Model {
		| &sema::SlicingExprModel:
			mut sem := (&sema::SlicingExprModel)(a.Right.Model)
			if obj::EqualModels(a.Left.Model, sem.Expr) {
				self.setCurrentStmt(&MutSlicingExprModel{
					Token: sem.Token,
					Expr: sem.Expr,
					Left: sem.Left,
					Right: sem.Right,
				})
				ret true
			}
		}
		ret false
	}

	fn strAssign(mut &self, mut a: &sema::Assign): bool {
		if !Str {
			ret false
		}
		lp := a.Left.Type.Prim()
		if lp == nil || !lp.IsStr() {
			ret false
		}
		ret self.substr(a)
	}

	fn sliceAssign(mut &self, mut a: &sema::Assign): bool {
		if !Slice || a.Left.Type.Slc() == nil {
			ret false
		}
		// [self.substr] applies this optimization without type dependence.
		ret self.substr(a)
	}

	fn optimizePostfix(mut &self, mut postfix: &sema::Postfix) {
		if self.data.boundary != nil {
			possibleBoundaryRemove(self.data.boundary, postfix.Expr)
		}
		exprOptimizer.optimizeData(postfix.Expr, self.data, self)
	}

	fn cmplxAssign(mut &self, mut &assign: &sema::Assign): bool {
		if !StdMathCmplx || assign.Op.Id != token::Id.Eq || obj::ProgramMeta.MathCmplx == nil {
			ret false
		}
		mut s := assign.Left.Type.Struct()
		if s == nil || s != obj::ProgramMeta.MathCmplx.Cmplx {
			ret false
		}
		match type assign.Right.Model {
		| &sema::FnCallExprModel:
			break
		|:
			ret false
		}
		mut fc := (&sema::FnCallExprModel)(assign.Right.Model)
		match type fc.Expr {
		| &sema::StructSubIdentExprModel:
			break
		|:
			ret false
		}
		mut siem := (&sema::StructSubIdentExprModel)(fc.Expr)
		if !obj::EqualModels(assign.Left.Model, siem.Expr.Model) {
			ret false
		}
		match fc.Func {
		| obj::ProgramMeta.MathCmplx.CmplxAdd:
			fc.Func = obj::ProgramMeta.MathCmplx.CmplxAddZ
		| obj::ProgramMeta.MathCmplx.CmplxSub:
			fc.Func = obj::ProgramMeta.MathCmplx.CmplxSubZ
		| obj::ProgramMeta.MathCmplx.CmplxMul:
			fc.Func = obj::ProgramMeta.MathCmplx.CmplxMulZ
		| obj::ProgramMeta.MathCmplx.CmplxDiv:
			fc.Func = obj::ProgramMeta.MathCmplx.CmplxDivZ
		|:
			ret false
		}
		siem.Method = fc.Func
		self.setCurrentStmt(&sema::Data{Model: fc})
		ret true
	}

	fn optimizeAssign(mut &self, mut assign: &sema::Assign) {
		if assign.Op.Id == token::Id.Eq &&
			obj::EqualModels(assign.Left.Model, assign.Right.Model) {
			self.removeCurrent()
			self.i-- // In next iteration, point to correct statement.
			ret
		}

		if self.data.boundary != nil {
			if isBoundaryRiskyType(assign.Left.Type) {
				possibleBoundaryRemove(self.data.boundary, assign.Left.Model)
			}
		}
		if self.data.nils != nil {
			if isGuaranteedNonNilExpr(self.data.nils, assign.Right.Model) {
				if isNilValidType(assign.Left.Type) {
					const safe = true
					self.data.nils.pushVar(getNilVar(assign.Left.Model), safe)
				}
			} else {
				possibleNilRemove(self.data.nils, assign.Left.Model)
			}
		}
		if self.data.dynamic != nil {
			mut kind := isTypeGuaranteedDynamicData(
				self.data.dynamic, assign.Right.Type, assign.Right.Model)
			if kind != nil {
				if isDynamicValidType(assign.Left.Type) {
					self.data.dynamic.pushVar(getDynamicVar(assign.Left.Model), kind)
				}
			} else {
				possibleDynamicRemove(self.data.dynamic, assign.Left.Model)
			}
		}

		match {
		| self.strAssign(assign)
		| self.sliceAssign(assign)
		| self.cmplxAssign(assign):
			ret
		}

		exprOptimizer.optimizeData(assign.Left.Model, self.data, self)
		exprOptimizer.optimizeData(assign.Right.Model, self.data, self)

		match assign.Op.Id {
		| token::Id.CaretEq | token::Id.SolidusEq | token::Id.PercentEq
		| token::Id.ShlEq | token::Id.ShrEq | token::Id.PlusEq | token::Id.MinusEq:
			// Do not check structures.
			if !Math || assign.Left.Type.Struct() != nil {
				break
			}
			oldId, oldKind := assign.Op.Id, assign.Op.Kind
			match assign.Op.Id {
			| token::Id.PlusEq:
				assign.Op.Id = token::Id.Plus
				assign.Op.Kind = token::Kind.Plus
			| token::Id.MinusEq:
				assign.Op.Id = token::Id.Minus
				assign.Op.Kind = token::Kind.Minus
			| token::Id.SolidusEq:
				assign.Op.Id = token::Id.Solidus
				assign.Op.Kind = token::Kind.Solidus
			| token::Id.PercentEq:
				assign.Op.Id = token::Id.Percent
				assign.Op.Kind = token::Kind.Percent
			| token::Id.ShlEq:
				assign.Op.Id = token::Id.Shl
				assign.Op.Kind = token::Kind.Shl
			| token::Id.ShrEq:
				assign.Op.Id = token::Id.Shr
				assign.Op.Kind = token::Kind.Shr
			| token::Id.CaretEq:
				assign.Op.Id = token::Id.Caret
				assign.Op.Kind = token::Kind.Caret
			|:
				panic("opt: implementation mistake, this panic call should be unreachable")
			}
			mut b := &sema::BinaryExprModel{
				Op: assign.Op,
				Left: assign.Left,
				Right: assign.Right,
			}
			mut model := sema::ExprModel(b)
			exprOptimizer.optimizeData(model, self.data, self)
			if model != b || b.Op != assign.Op {
				// Binary model optimized.
				// Use optimized model directly.
				if assign.Op.Id == token::Id.Caret {
					// If caret operator optimized, it is equal to self assignment.
					// Remove this unnecessary statement.
					self.removeCurrent()
					ret
				}
				assign.Right = new(sema::OperandExprModel, *assign.Right)
				b.Op = new(token::Token, *b.Op) // Unsafe model wraps original model. Save token.
				assign.Op.Id = token::Id.Eq
				assign.Op.Kind = token::Kind.Eq
				assign.Right.Model = model
				ret
			}
			assign.Op.Id = oldId
			assign.Op.Kind = oldKind
			ret
		}

		if Append {
			match type assign.Right.Model {
			| &sema::BuiltinAppendCallExprModel:
				mut m := (&sema::BuiltinAppendCallExprModel)(assign.Right.Model)
				if !areSameLvalueExprModel(assign.Left.Model, m.Dest) {
					ret
				}
				match type m.Elements {
				| &sema::SliceExprModel:
					// Push items one-by-one for self-appended memory.
					self.setCurrentStmt(&PushToSliceExprModel{
						Dest: m.Dest,
						Elems: (&sema::SliceExprModel)(m.Elements),
					})
				|:
					// Append directly if appended to slice and assigned to the same memory.
					self.setCurrentStmt(&AppendToSliceExprModel{
						Dest: assign.Left.Model,
						Slice: m.Elements,
					})
				}
			}
		}
	}

	// Tries to optimize swap assignments, such as "x, y = y, x", and reports whether optimization applied.
	// Also reports whether additional analysis is required such as expression optimizations for operands.
	fn tryOptimizeSwap(mut &self, mut &assign: &sema::MultiAssign): (applied: bool, analysis: bool) {
		if !Assign || len(assign.Left) != 2 ||
			assign.Left[0] == nil || assign.Left[1] == nil {
			ret false, false
		}
		let mut tup: &sema::TupleExprModel
		match type assign.Right {
		| &sema::TupleExprModel:
			tup = (&sema::TupleExprModel)(assign.Right)
		}
		if tup == nil || len(tup.Datas) != 2 {
			ret false, false
		}

		// Catch self assignments.
		if obj::EqualModels(assign.Left[0].Model, tup.Datas[0].Model) &&
			obj::EqualModels(assign.Left[1].Model, tup.Datas[1].Model) {
			self.removeCurrent()
			self.i-- // In next iteration, point to correct statement.
			// Optimization applied but additional analysis is not necessary.
			// Because this statement is removed. Do not affect the behavior of the program.
			ret true, false
		}

		// Catch swaps.
		if !obj::EqualModels(assign.Left[0].Model, tup.Datas[1].Model) ||
			!obj::EqualModels(assign.Left[1].Model, tup.Datas[0].Model) {
			ret false, false
		}
		mut model := &SwapExprModel{
			Left: assign.Left[0],
			Right: assign.Left[1],
		}
		self.setCurrentStmt(model)
		// Do not optimize the operands.
		// The additional analysis will do it.
		ret true, true
	}

	fn optimizeMultiAssign(mut &self, mut assign: &sema::MultiAssign) {
		{
			applied, analysis := self.tryOptimizeSwap(assign)
			if applied && !analysis {
				ret
			}
		}

		// Use self.data to determine whether optimization opportunities.
		// But use this checkpoint for optimizations. Because,
		// any swap expression may cause conflicts.
		// For example, --opt-dynamic optimizations;
		//
		//   mut x := any(nil)
		//   mut y := any(20)
		//   x, y = y, x
		//   _ = int(y)
		//
		//   In the example program above, the "int(y)" expression will be optimized to unsafe,
		//   which is removes the type checking. But the variable |y| will be nil.
		//   This bug appears because of using common optimization data.
		//   First, the variable |x| takes the value of the variable |y|,
		//   which is non-nil and type-is-nil guaranteed. Then the variable |y|
		//   takes the value of the variable |x|. It should be nil, but it's not.
		//   Because the variable |x| will be marked like |y|, since common data used.
		//   So use the self.data to determine optimization opportunities,
		//   but apply optimization data update on the checkpoint copy to avoid conflicts.
		//
		// Do not forget load checkpoint to self.data to keep updated data.
		mut checkpoint := self.data.getCheckpoint()

		mut tup := (&sema::TupleExprModel)(nil)
		match type assign.Right {
		| &sema::TupleExprModel:
			tup = (&sema::TupleExprModel)(assign.Right)
		}
		for (i, mut l) in assign.Left {
			if l != nil {
				if self.data.boundary != nil {
					if isBoundaryRiskyType(l.Type) {
						possibleBoundaryRemove(checkpoint.boundary, l.Model)
					}
				}
				if self.data.nils != nil {
					if tup != nil && isGuaranteedNonNilExpr(self.data.nils, tup.Datas[i].Model) {
						if isNilValidType(l.Type) {
							const safe = true
							checkpoint.nils.pushVar(getNilVar(l.Model), safe)
						}
					} else {
						possibleNilRemove(checkpoint.nils, l.Model)
					}
				}
				if self.data.dynamic != nil && tup != nil {
					mut kind := isTypeGuaranteedDynamicData(
						self.data.dynamic, tup.Datas[i].Type, tup.Datas[i].Model)
					if kind != nil {
						if isDynamicValidType(l.Type) {
							checkpoint.dynamic.pushVar(getDynamicVar(l.Model), kind)
						}
					} else {
						possibleDynamicRemove(checkpoint.dynamic, l.Model)
					}
				}
				exprOptimizer.optimizeData(l.Model, self.data, self)
			}
		}
		exprOptimizer.optimizeData(assign.Right, self.data, self)

		// Load data to optimized checkpoint.
		self.data.loadCheckpoint(checkpoint)
	}

	fn optimizeRet(mut &self, mut r: &sema::RetSt) {
		exprOptimizer.optimizeData(r.Expr, self.data, self)

		// Break algorithm is exceptional-specific optimizations are not enabled.
		// The following algorithms tries to apply specific optimizations for exceptionals.
		if !Exceptional {
			ret
		}
		match type r.Expr {
		| &sema::FnCallExprModel:
			break
		|:
			ret
		}
		mut fc := (&sema::FnCallExprModel)(r.Expr)
		if !fc.Func.Decl.Exceptional ||
			fc.Except == nil ||
			len(fc.Except.Stmts) != 1 ||
			!obj::IsForwarded(fc.Except) {
			ret
		}
		self.setCurrentStmt(&ExceptionalForwardingExprModel{Expr: fc})
	}

	fn optimizeStmt(mut &self, mut stmt: sema::Stmt) {
		match type stmt {
		| &sema::Scope:
			self.optimizeChildHard((&sema::Scope)(stmt))
		| &sema::Data:
			self.optimizeData((&sema::Data)(stmt))
		| &sema::Var:
			self.optimizeVar((&sema::Var)(stmt))
		| &sema::Conditional:
			self.optimizeConditional((&sema::Conditional)(stmt))
		| &sema::RangeIter:
			self.optimizeRangeIter((&sema::RangeIter)(stmt))
		| &sema::WhileIter:
			self.optimizeWhileIter((&sema::WhileIter)(stmt))
		| &sema::InfIter:
			self.optimizeChildHard((&sema::InfIter)(stmt).Scope)
		| &sema::Assign:
			self.optimizeAssign((&sema::Assign)(stmt))
		| &sema::MultiAssign:
			self.optimizeMultiAssign((&sema::MultiAssign)(stmt))
		| &sema::Match:
			self.optimizeMatch((&sema::Match)(stmt))
		| &sema::RetSt:
			self.optimizeRet((&sema::RetSt)(stmt))
		| &sema::Postfix:
			self.optimizePostfix((&sema::Postfix)(stmt))
		}
	}

	// Optimizes child scope using mutable data.
	fn optimizeChild(mut &self, mut child: &sema::Scope) {
		mut so := scopeOptimizer.new(child)
		so.parent = self
		so.data = self.data
		so.optimize()
	}

	// Optimizes child scope using responsive data.
	// This child optimization algorithm is safer but slower.
	// It should be used when the child scope may mutate the data,
	// because optimization may conflict and mutable data may cause issues.
	// This optimizer method will use responsive data to detect conflicts and other cases.
	//
	// For example;
	//
	//   z := getSlice()
	//   mut x := (&int)(nil)
	//   for in z {
	//       x = new(int)
	//   }
	//   println(*x)
	//
	//   In the example code above, the variable |x| may optimized to unsafe dereferencing,
	//   which is removes the nil checking. It probably will appear when the scope of the
	//   iteration optimized with the optimizeChild method. Since the using mutable data,
	//   the variable |x| will be marked as non-nil because of `new(int)` assignment.
	//   But, the iteration may not iterate even once. So, the variable |x| may still remain nil.
	//   In this case, the program behavior will be unsafe because of optimization mistake.
	//
	//   This optimizer method, uses responsive data. So handles dead variables and mutations.
	//   For example, if scope code will assign the variable |x| to nil,
	//   it remove the alive state of the variable |x|. Another example,
	//   example case is not appear when used this method. Because it uses an immutable copy,
	//   internally, so child optimizer will not mutate the checkpoint.
	//   Removes possibly dead variables if checkpoint marks as alive after child optimizer,
	//   and ignored possibly alive variables to avoid mistakes such as example case.
	fn optimizeChildHard(mut &self, mut child: &sema::Scope) {
		// Create the immutable copy of the data to determine alive and dead data.
		// As described in the method documentation.
		mut alive := data{}
		alive.loadCheckpoint(self.data.getCheckpoint())

		self.optimizeChild(child)

		// Remove possibly dead variables after child optimization.
		alive.removeDeads(self.data)

		// Load alive as data.
		self.data.loadCheckpoint(alive.getMutCheckpoint())
	}

	// Optimizes scope by enabled optimizations.
	fn optimize(mut &self) {
		self.i = 0
		for self.i < len(self.scope.Stmts); self.i++ {
			self.optimizeStmt(self.scope.Stmts[self.i])
		}
	}
}

fn isConstantValidConditionalCase(&i: &sema::If): bool {
	match type i.Expr {
	| &constant::Const:
		c := (&constant::Const)(i.Expr)
		ret c.IsBool() && c.ReadBool()
	}
	ret false
}

fn isUnreachableExpr(&expr: sema::ExprModel): bool {
	match type expr {
	| &constant::Const:
		c := (&constant::Const)(expr)
		ret c.IsBool() && !c.ReadBool()
	| &sema::BinaryExprModel:
		m := (&sema::BinaryExprModel)(expr)
		if m.Op.Id == token::Id.DblAmper {
			ret isUnreachableExpr(m.Left.Model) ||
				isUnreachableExpr(m.Right.Model)
		}
	}
	ret false
}

fn isConstantValidMatchCase(&case: &sema::Case): bool {
	for _, expr in case.Exprs {
		if expr.IsConst() && expr.Constant.IsBool() && expr.Constant.ReadBool() {
			ret true
		}
	}
	ret false
}

fn isUnreachableConditionalCase(&i: &sema::If): bool {
	ret isUnreachableExpr(i.Expr)
}

fn isUnreachableMatchCase(&case: &sema::Case): bool {
	for _, expr in case.Exprs {
		if !isUnreachableExpr(expr.Model) {
			ret false
		}
	}
	ret true
}