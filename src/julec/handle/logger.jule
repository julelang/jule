// Copyright 2023 The Jule Project Contributors. All rights reserved.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/conv"
use "std/jule/build"
use "std/jule/log"
use "std/strings"

// Logger for compiler logs.
struct Logger{}

impl Logger {
	// Prints flag log.
	fn LogFlat(&l: *log::Log) {
		println(l.Text)
	}

	// Prints error log.
	fn LogError(&l: *log::Log) {
		print(AnsiRed)
		print("error: ")
		print(l.Text)
		AnsiEscape.Reset()

		if len(l.Path) != 0 {
			print("\n  --> ")
			print(l.Path)
		}
		if l.Row != 0 && l.Column != 0 {
			print(":")
			print(conv::Itoa(l.Row))
			print(":")
			print(conv::Itoa(l.Column))
		}
		if len(l.Line) != 0 {
			// For correct handling, trim leading and trailing space bytes.
			// Also replace tabs with spaces for deterministic size.
			mut line := strings::Trim(l.Line, "\r\n\v\b\t ")
			line = strings::ReplaceAll(line, "\t", " ")

			mut offset := len(l.Line) - len(line) + 1
			print("\n  ")
			row := conv::Itoa(l.Row)
			print(row)
			print(" | ")
			print(line)
			print("\n  ")
			print(strings::Repeat(" ", len(row)))
			print(" | ")
			print(strings::Repeat(" ", l.Column-offset))
			print("^")
			if len(l.Suggestion) != 0 {
				print("\n  ")
				print(strings::Repeat(" ", len(row)))
				print(" | ")
				AnsiEscape.Print(AnsiBrightMagenta, "suggestion: ")
				print(l.Suggestion)
			}
		} else if len(l.Suggestion) != 0 {
			print("\n    | ")
			AnsiEscape.Print(AnsiBrightMagenta, "suggestion: ")
			print(l.Suggestion)
		}
		println("\n")
	}

	// Log.
	fn Log(&l: *log::Log) {
		match l.Kind {
		| log::Flat:
			Logger.LogFlat(l)
		| log::Error:
			Logger.LogError(l)
		}
	}

	// Prints all logs.
	fn PrintLogs(&logs: *[]log::Log) {
		for _, l in *logs {
			Logger.Log(&l)
		}
		print("=== ")
		print(conv::Itoa(len(*logs)))
		println(" error generated ===")
	}
}