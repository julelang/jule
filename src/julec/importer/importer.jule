// Copyright 2023 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::fs::{OFlag, FsError, DirEntry, read_dir, read_file, open}
use std::fs::path::{join}
use std::jule::ast::{Ast}
use std::jule::build::{
    Directive,
    LogKind,
    Log,
    EXT,
    is_pass_file_annotation,
}
use std::jule::lex::{new_file_set, lex}
use std::jule::parser::{parse_file}
use std::jule::sema::{
    Importer,
    ImportInfo,
}
use std::process::{exit}

fn exit_err(msg: str) {
    const ERROR_EXIT_CODE = 1

    outln(msg)
    exit(ERROR_EXIT_CODE)
}

// Read buffer by file path.
fn read_buff(path: str): []byte {
    let (mut buffer, err) = read_file(path)
    if err != FsError.Ok {
        exit_err("error: file cannot read")
    }
    ret buffer
}

// Make compiler error, just text.
// Not includes row, column, and etc. informations.
fn flat_compiler_err(text: str): Log {
    ret Log{
        kind: LogKind.Error,
        text: text,
    }
}

// Read package directory entries by package (directory) path.
fn read_package_dirents(path: str): ([]&DirEntry, err_msg: str) {
    let (mut dirents, err) = read_dir(path)
    if err != FsError.Ok {
        ret nil, "connot read package directory: " + path
    }

    let mut passed_dirents: []&DirEntry = nil
    for (_, mut dirent) in dirents {
        let name = dirent.name

        // Skip directories, and non-jule files.
        if dirent.stat.is_dir() || !name.has_suffix(EXT) {
            continue
        }

        passed_dirents = append(passed_dirents, dirent)
    }

    ret passed_dirents, ""
}

// Concatenate all strings into single string.
fn concat_all_parts(parts: ...str): str {
    let mut s = ""
    for _, part in parts {
        s += part
    }
    ret s
}

// Reports whether file passes build directives.
fn is_pass_build_directives(mut &file: &Ast): bool {
    let mut i = 0
    for i < file.top_directives.len(); i++ {
        let mut td = file.top_directives.at(i)
        if td.tag != str(Directive.Build) {
            continue
        }

        let expr = concat_all_parts(td.args...)
        let result = eval_directive_expr(expr)
        if !result {
            ret false
        }
    }
    ret true
}

// Common importer of JuleC.
pub struct PlainImporter {
    pub all_packages: []&ImportInfo
}

impl Importer for PlainImporter {
    pub fn get_import(mut self, path: str): &ImportInfo {
        for (_, mut p) in self.all_packages {
            if p.path == path {
                ret p
            }
        }

        ret new(ImportInfo)
    }

    pub fn import_package(mut self, path: str): ([]&Ast, []Log) {
        let (dirents, err_msg) = read_package_dirents(path)
        if err_msg != "" {
            let mut errors = [flat_compiler_err(err_msg)]
            ret nil, errors
        }

        let mut asts = make([]&Ast, 0, dirents.len)
        for _, dirent in dirents {
            let path = join(path, dirent.name)
            let mut file = new_file_set(path)
            file.fill(read_buff(file.path()))
            let mut errors = lex(file)
            if errors.len > 0 {
                ret nil, errors
            }

            let mut finfo = parse_file(file)
            if finfo.errors.len > 0 {
                ret nil, finfo.errors
            }

            // Skip this source file if file annotation is failed.
            if !is_pass_file_annotation(dirent.name) {
                continue
            }

            // Skip file if can't pass build directives.
            if !is_pass_build_directives(finfo.ast) {
                continue
            }

            asts = append(asts, finfo.ast)
        }

        ret asts, nil
    }

    pub fn imported(mut self, mut imp: &ImportInfo) {
        // Already imported?
        for _, p in self.all_packages {
            if p.cpp_linked == imp.cpp_linked && p.link_path == imp.link_path {
                ret
            }
        }

        self.all_packages = append(self.all_packages, imp)
    }
}
